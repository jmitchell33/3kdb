#class {default_actions} {kill}
#class {default_actions} {open}

#var my[hm][heal] 1;
#var my[hm][heal_other] 1;
#var inventory[encumberance] 0;

#NOP -- Inventory;
#action {^%*: Too heavy.$} {
    #var inventory[encumberance] 100
} {2};

#action {^%*: Ok.$} {
    #var inventory[encumberance] 0
} {4};

#NOP -- Hockey corpse defaults;
#alias .hockeyCorpse {
    #if $gags[combat_items] {#line gag};
    #var hockey_corpse 1;
    #if {"$guild" == "bard"} {
        #if {$my[song][rounds] > 1} {
            #var randNumber @rnd{1;200};
            add_action {{{name} {$randNumber} {action} {perform dirge} {priority} {3}}};
        } {
            perform dirge
        };
    };
    #if {"$guild" == "gentech"} {recharge;reclaim};
    #if {"$guild" == "juggernaut"} {deener;truck deener;fdeener};
    #if {"$guild" == "mage"} {cast corpse blast};
    #if {"$guild" == "necromancer"} {
        preserve remains;
        #NOP -- If red spark hits, don't absorb it and save the corpse;
        #if $spark_corpse {
            get preservation;keep all preservation;#var hockey_corpse 0;
        } {
            absorb;
        };
    };
    #if {"$guild" == "priest"} {offer corpse};
    #if {"$guild" == "fremen"} {reclaim};
};

#action {^Out of nowhere a crazed hockey fan leaps towards the Cup.} {.hockeyCorpse} {1};

#action {^A hockey fan sprints out and dies.$} {.hockeyCorpse} {1};

#NOP -- Dead/Alive variable;
#action {A dark chill enters the room, filling you with awe, mixed with fear.} {#var my[dead] 0} {2};
#action {Demanding an audience with Death, %* instructs him to return your} {#var my[dead] 0} {2};

#action {^You lift the drink to your mouth, but in your stomach} {#var my[drunk] 1} {2};
#action {^You drink the wine and feel healed.$} {#var my[drunk] 0} {2};

#action {^The effects of the%*blue capsule fade away.$} {#if !$idle_flag {swallow blue capsule}} {2};
#action {^The effects of the%*orange capsule fade away.$} {#if !$idle_flag {swallow orange capsule}} {2};

#NOP #action {Zosimus says: There, it is done, push your hand into the rump of the beast} {push hand into cows rump};

#action {^%w begs you.} {#if !$idle_flag {#format {lower} {%l} {%1};unkeep blue;give blue to $lower;unkeep orange;give orange to $lower};} {6};
#action {^The daemon hisses: Dost thou choose to graft with me %w?} {#format {graft} {%l} {%1};#if {"$user" == "$graft"} {say yes}};
#action {^%1 begins to follow %2\.$} {#format {follower} {%l} {%1};#if {"$user" == "$follower"} {#format {following} {%n} {%2}}};
#action {^You stop following %1} {#unvar following};
#action {^%1 leaves %2\.$} {
    #if {"%1" == "$following"} {
        #format {follow_target} {%l} {%1};
        #local follow_dir %2;
        #switch {"$follow_dir"} {
            #case {"north"}     {#map move n};
            #case {"northeast"} {#map move ne};
            #case {"northwest"} {#map move nw};
            #case {"south"}     {#map move s};
            #case {"southeast"} {#map move se};
            #case {"southwest"} {#map move sw};
            #case {"east"}      {#map move e};
            #case {"west"}      {#map move w};
            #case {"down"}      {#map move d};
            #case {"up"}        {#map move u};
            #default            {#map move $follow_dir};
        };
    }
}; 

#NOP -- Rewear the knight shield if you use the wrong one;
#action {There is no reason to 'bash' here.} {
    #if {$my[song][rounds] == 0} {
        remove shield;give knight shield to $user;keep knight shield;wear knight shield;
    };
} {2};

#NOP -- Toggle that mortiis mask has been worn or removed;
#action {^Head:         The mask of Gemino Mortis} {#var equipped[mask] 1} {2};
#action {helmet   - %*The mask of Gemino Mortis%*} {#var equipped[mask] 1} {2};
#action {^You fit the mask on and your eyes glow dimly green.$} {#var equipped[mask] 1} {2};
#action {^You already wear the Mask of Gemino Mortis!} {#var equipped[mask] 1} {2};
#action {^As you look through the mask, the eyes flash briefly with a dim, blue light.} {#var mask[active] 1} {2};
#action {^Your eyes darken as you remove the mask.} {#var equipped[mask] 0} {2};

#format userCap {%n} {$user};
#action {^${userCap}'s eyes glow bright green upon} {#var mask[active] 1} {2};


#action {^It turns dark.$} {#var room_darkness 1;} {2};
#action {^You can see again.$} {#var room_darkness 0;} {2};

#action {^%w taps you on the shoulder.$} { 
    #if !$idle_flag {
        #format followtarget {%l} {%1}; 
        follow $followtarget;
        cassist $followtarget;
    };
} {2};

#action {^From afar, %w stares at you in %w Jesus Christ pose.$} {
    #if !$idle_flag {
        #format healtarget {%l} {%1}; 
        hmheal $healtarget;
        #action {^You need rest to use that power again.$} {tell $healtarget No, die.  Hmheal is on cooldown.} {2};
        #delay 2 {#unaction {^You need rest to use that power again.$}};
    };
} {2};

#action {^%w stares at you in %w Jesus Christ pose.$} { 
    #if !$idle_flag {
        #format healtarget {%l} {%1}; 
        hmheal $healtarget;
        #action {^You need rest to use that power again.$} {tell $healtarget No, die.  Hmheal is on cooldown.} {2};
        #delay 2 {#unaction {^You need rest to use that power again.$}};
    };
} {2};

#action {^%w stares at you in %w best Anti-Christ pose.} {
    #if !$idle_flag {
        #format healtarget {%l} {%1}; 
        (heal $healtarget;
        #action {^You must wait %* to use that ability.$} {tell $healtarget No, die.  (heal is on cooldown for another %%1.} {1};
        #delay 2 {#unaction {^You must wait %* to use that ability.$}};
    };
} {2};

#action {^From afar, %w stares at you in %w best Anti-Christ pose.} {
    #if !$idle_flag {
        #format healtarget {%l} {%1}; 
        (heal $healtarget;
        #action {^You must wait %* to use that ability.$} {tell $healtarget No, die.  (heal is on cooldown for another %%1.} {1};
        #delay 2 {#unaction {^You must wait %* to use that ability.$}};
    };
} {2};

#action {^%w says, 'Greetings, Comrade!' to you.$} { 
    #if !$idle_flag {
        #10 corpse_select
    };
} {2};


#NOP -- Skill Training;
#action {^To do so, type: tskill %1$} {#if {!$idle_flag} {tskill %1}} {2};


#NOP -- Mob Life Counting;
#action {^Beloch falters slightly but shakes off the assault.} {
    #showme -------------------------------------------------;
    #showme ---------        BELOCH LIFE DOWN         -------;
    #showme -------------------------------------------------;
} {2};

#NOP -- AUTO PULLING ROPE TO AND FROM PLAYER;
#action {^Pull the rope, and you will be teleported to %*.$} {#if {!$idle_flag} {pull rope}};
#action {^Pulling the rope, will allow %* passage.$} {#if {!$idle_flag} {pull rope}};

#NOP -- Message for Pride of the Kwik-e Mart Wearing Off;
#action {^The last of the oil leaks from your body.$} {eat pride} {2};

#class {default_actions} {close}