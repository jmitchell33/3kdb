#class {common_room} {kill};
#class {common_room} {open};


#NOP -- Add anything to the familiar mix;
#action {^-M-_A tank named %*.} {#list room[familiars] add {A tank named %1}} {1};
#action {^-M-_%* Jugger Support Mech%*.} {#list room[familiars] add {%1 Jugger Support Mech}} {1};
#action {^-M-_$my[golem][name] \(%w Golem\) %*.} {#list room[familiars] add {$my[golem][name] (%1 Golem) %2}} {1};

#alias .getMapRoomData {
    #map {get} {ROOMVNUM} {roomvnum};
    #map {get} {ROOMAREA} {roomarea};
    #map {get} {ROOMNAME} {roomname};
    #map {get} {ROOMDESC} {roomdesc};
    #format {roomname_len}	{%L} {${roomname}};
    #format {roomvnum_len}	{%L} {${roomvnum}};
    #format {roomarea_len}	{%L} {${roomarea}};
    #format {roomdesc_len}	{%L} {${roomdesc}};
};

#alias .clearRoomData {
    #list room[mobs] clear;
    #list room[familiars] clear;
    #list room[summons] clear;
    #list room[items] clear;
    #list room[players] clear;
    #list room[botmobs] clear;
    #unvar room[name];
    #var room[updated] 0;
    #var room_coins 0;
    #var room_tome 0;
    #var first_in_room 1;

    #if $bot[active] {
        #var bot[foundmob] 0;
        #var bot[foundobject] 0;
        #var bot[foundplayer] 0;
        #var bot[aggro-mob] 0;
    };
};


#NOP -- Get the current scaler of the dungeon;
#action {^Current scaled level of the dungeon you're in: %d} {
    #format area_scaler {%d} {%1};
} {2};



#NOP -- Update if there is a player in the room not in friends list;
#act {{^-P-_(.*)}} {
    #if $bot_first_in_room_check {
        #var first_in_room 0;
    };

    #NOP -- If the player is in the room/players list, use that name.  Otherwise use the string from the room;
    #local tmpPlayer {};
    #local foundPlayer 0;
    #loop 1 &players[] tmpPlayer {
        #if {"%1" == "%*$players[$tmpPlayer]%*"} {
            #list room[players] add $players[$tmpPlayer];
            #var foundPlayer 1;
        }
    };
    #if !$foundPlayer {
        #NOP -- Remove the prefix before the player and period from the end of the player string;
        #var tmpPlayer %1;
        #replace tmpPlayer {-P-_} {};
        #format tmpLength {%L} {$tmpPlayer};
        #math tmpLength {$tmpLength - 1};
        #format tmpPlayer {%.${tmpLength}s} {$tmpPlayer};
        #list room[players] add {$tmpPlayer};
    };

    #if $bot[active] {
        #if $bot[playercheck] {
            #if !$first_in_room {
                #unvar item {};
                #var noPlayerCheck 1;
                #if {"$whitelist" !== ""} {
                    #if {&whitelist[] > 0} {
                        #loop 1 &whitelist[] item {
                            #regex {"%1"} {"%*$whitelist[$item]%*"} {
                                #var noPlayerCheck 0;
                            };
                        };
                    } {
                        #var bot[foundplayer] 1;
                    };
                } {
                    #var bot[foundplayer] 1;
                };
                #if $noPlayerCheck {#var bot[foundplayer] 1};
            };
        };
    };
} {2};


#act {{^-R-_(.*)-R-_}} {
    .clearRoomData;
    #var room_short %1;
    
    #line oneshot #action {^>$} {

        #var bot_first_in_room_check 0;
        #var room[updated] 1;


        #if $bot[active] {
            #if $debug[bot] {#echo {Prompt reached, calling .determineBotAction}};
            .determineBotAction
        };
        #if $explore[updateDB] {
            #var explore[walk] 0;
            #format checkRoomLength {%L} {$room[long]};
            apiPostAll;
            #delay 2 {#var explore[walk] 1};
        };
        #if {"$mapping" == "on"} {.map_create_exits};
    } {1};
} {2};

#action {^-D-_%*\n%a-D-_$} {
    #unvar room[long];
    #unvar long;
    #format long1 {%p} {%1};
    #format long2 {%p} {%2};
    #var long {$long1 $long2};
    #replace long {\n} { };
    #var room[long] {${long}};
} {2};


#act {^{-i-|-w-|-a-|-o-}%*.$} {
    #var item_number 1;
    #var item_long %2;
    #var item_long_full %0;

    #replace item_long {.} {};
    #replace item_long { \[%*\]} {};

    #regex {%2} {7B{[0-9]+}} {#var item_number &1;#replace item_long { \\x7B%*} {};};
    #regex {%2} {7B{[A-Za-z]+}} {#replace item_long { \\x7B%*} {};};
    #regex {%2} {coins} {#var room_coins 1};
    #regex {%2} {A Tome of %*, Volume %*} {#var room_tome 1};

    #list room[items] add $item_long_full;

    #if {$bot[active] && !$bot[paused] && $bot[walk]} {
        #if {$collector[items]} {
            #foreach $item_grab_list[%*] item {
                #regexp {"$item_long"} {$item[long]} {
                    #$item_number get $item[target];
                    #if {"$guild" == "bard"} {
                        identify $item[target];
                    };
                    #if {"$guild" == "priest"} {
                        recognize $item[target];
                    };
                    #if {"$item[target]" == "bag"} {
                        #loop 2 20 i {put bag in bag $i};
                    };
                };
            };
        };
    };
} {2};

#function checkPeriod {
    #var result 0;
    #local tmpWord %0;
    #format revTmpWord {%p} {$tmpWord};
    #format revTmpWord {%r} {$tmpWord};
    #format tmpWord {%.1s} {$revTmpWord};
    #if {"$tmpWord" == "."} {#var result 1};
};

#NOP -- Clean up the mob long a bit;
#alias .mobNameClean {
    #var isPeriod 0;
    #var mob[long] {};
    #var mob[autoName] {};
    #var foundTimes 1;
    #var mob[long] %0;
    #var mob[aggressive] 0;

    #regex {$mob[long]} {attacking you} {
        #NOP -- There's a mage bug where your summon will show Attacking you but not actually be attacking you;
        #regex {$mob[long]} {[summons]} {#NOP} {
            #var bot[aggro-mob] 1;
            #replace mob[long] { attacking%*} {};
        };
        #var mob[aggressive] 1;
    };
    #regex {$mob[long]} {7B{[0-9]+}} {#var foundTimes &1;#replace mob[long] { \\x7B%*} {};};
    #regex {$mob[long]} {7B{[A-Za-z]+}} {#replace mob[long] { \\x7B%*} {};};
    #format {mob[long]} {%p} {$mob[long]};

    #replace mob[long] { [scratched]%*} {};
    #replace mob[long] { [wounded]%*} {};
    #replace mob[long] { [dying]%*} {};
    #replace mob[long] { <%*>} {};

    #NOP -- If there is a period at the end, remove it;
    #var isPeriod @checkPeriod{$mob[long]};
    #format mobLength {%L} $mob[long];
    #if $isPeriod {
        #math mobLength {$mobLength - 1};
        #format mob[long] {%.${mobLength}s} {$mob[long]};
    };
    #var tmpMob $mob[long];

    #NOP -- Get rid of anything in brackets;
    #replace tmpMob { \[%*\]%*} {};
    #replace tmpMob { \(%*\)%*} {};
    #replace tmpMob { \\x7B%*\\x7D%*} {};

    #list tmpMob {explode} { };
    #format mob[autoName] {%l} $tmpMob[&tmpMob[]];
};

#act {^-M-_%*} {
    .mobNameClean %1;
    #var mobIndex 1;
    #var botTarget 0;
    #var mobFamiliar 0;

    #NOP -- toggle maintenance if cassisting;
    #if {"$cassist[status]" == "1"} {
        #NOP -- fremen;
        #if {"$guild" == "fremen"} {
            #if {"$toggles[fremen][hold]" == "0"} {
                #var toggles[fremen][hold] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: cassist active - hold toggle set to 1:<088> $toggles[fremen][hold]}};
            };
            #if {"$toggles[fremen][stun]" == "0"} {
                #var toggles[fremen][stun] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: cassist active - stun toggle set to 1:<088> $toggles[fremen][stun]}};
            };
            #if {"$toggles[fremen][enfeeblement]" == "0"} {
                #var toggles[fremen][enfeeblement] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: cassist active - enfeeblement toggle set to 1:<088> $toggles[fremen][enfeeblement]}};
            };
        };
    };
    #NOP -- toggle maintenance if mob is aggro;
    #if {"$bot[aggro-mob]" == "1"} {
        #NOP -- fremen;
        #if {"$guild" == "fremen"} {
            #if {"$toggles[fremen][hold]" == "0"} {
                #var toggles[fremen][hold] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: aggro mob - hold toggle set to 1:<088> $toggles[fremen][hold]}};
            };
            #if {"$toggles[fremen][stun]" == "0"} {
                #var toggles[fremen][hold] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: aggro mob - stun toggle set to 1:<088> $toggles[fremen][stun]}};
            };
            #if {"$toggles[fremen][enfeeblement]" == "0"} {
                #var toggles[fremen][enfeeblement] 1;
                #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: aggro mob - enfeeblement toggle set to 1:<088> $toggles[fremen][enfeeblement]}};
            };
        };
        #NOP -- reset corpsetrigfailed variable to 1 if aggro mob;
        #if {"$bot[corpsetrigfailed]" == "0"} {
            #var bot[corpsetrigfailed] 1;
            #if {$debug[corpsetrig][toggle]} {#echo {<ffa>debug toggles: corpsetrig failed - resetting corpsetrigfailed to 1:<088> $bot[corpsetrigfailed]}};
        };
    };

    #NOP -- Auto ask/solve/return missions;
    #regex {%1} {<?>} {#if $toggles[mission] {mission ask}};
    #regex {%1} {<!>} {mission return};
    #regex {%1} {<!.>} {mission return;mission ask};
    
    #NOP -- Mob health if it has not taken damage does not appear and is 100%;
    #var tmpHealth 100;
    #regex {$mob[long]} {(%d%)} {#var tmpHealth &1};

    #NOP -- If the mob is a familiar, we put it into a room[familiars];
    #NOP -- There's a risk of this grouping mobs that aren't familiars (like skeleton, wight for necro undeads);
    #regex {$mob[long]} {^%w the Skeleton$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Greesh$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Zombie$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Wight$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Ghoul$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Heucuva$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Valpur$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Tholen$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Ghast$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Bayen$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Manshi$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Quarin$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Bolwyn$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Mohrg$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Grimare$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^%w the Valithyn$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^$my[familiar]$} {#var mobFamiliar 1};
    #regex {$mob[long]} {^$my[golem][name] \(%w Golem\) %*$} {#var mobFamiliar 1};
    #regex {$mob[long]} {A shadowbound follower of %*} {#var mobFamiliar 1};

    #NOP -- Knight Familiars;
    #regex {$mob[long]} {%*, %*'s %* Percheron} {#var mobFamiliar 1};
    #regex {$mob[long]} {%*, %*'s %* Kiso} {#var mobFamiliar 1};

    #if $mobFamiliar {
        #list room[familiars] add {{{name} {$mob[long]}}};
    } {
        #NOP -- Separate mage summons into their own group/exclude from room[mobs];
        #regex {$mob[long]} {%* [summons] (%d%)%*} {
            #var summonIndex 1;
            #if $debug[room] {#echo {<cfa>DEBUG ROOM: Summon found &0}};
            #format tmpSummonName {%p} {&0};
            #replace tmpSummonName { [summons]%*} {};
            #var summonTankIndex 1;
            
            #NOP -- We don't want to assign the Golem to tanking since it's damage drops if it's tanking;
            #var skipSummon 0;
            
            #if {$foundTimes == 1} {
                #if {&room[summons] > 0} {
                    #var tmpFound 0;
                    #foreach $room[summons][%*] tmpItem {
                        #if {"$tmpItem[name]" == "$tmpSummonName"} {
                            #math summonIndex {$tmpItem[index] + 1};
                        };
                    };
                    #list room[summons] add {{{name} {$tmpSummonName} {health} {&2} {index} {$summonIndex}}};
                };
            } {
                #loop 1 $foundTimes tmpIndex {
                    #list room[summons] add {{{name} {$tmpSummonName} {health} {&2} {index} {$tmpIndex}}};
                };
            };

            #if $summonTankCheck {
                #regex {"$tmpSummonName"} {"%*Golem%*"} {#var skipSummon 1};
                #if !$skipSummon {
                    #var summonTankIndex 1;
                    #foreach $summonTankList[%*] item {
                        #if {"$item[name]" == "$tmpSummonName"} {#math summonTankIndex {$summonTankIndex + 1}};
                    };
                    #list summonTankList add {{{name} {$tmpSummonName} {health} {&2} {index} {$summonTankIndex}}};
                };
            };
        } {
            #NOP -- THIS SECTION HANDLES BOT MOBS AND MOBS OTHER THAN MAGE SUMMONS;
            #if $bot[active] {
                #foreach $bot[mobs][%*] botmob {
                    #if !$bot[foundmob] {
                        #regexp {$mob[long]} {%i$botmob[long]} {
                            #if $debug[bot] {#echo {<cfa>DEBUG BOT: MOB FOUND $mob[long] MATCHED $botmob[long]}};
                            #if $debug[bot] {#echo {<cfa>DEBUG BOT: SETTING TARGET TO $botmob[target]}};
                            #var bot[foundmob] 1;
                            #var mob[target] $botmob[target];
                            #var botTarget 1;
                        };
                    };
                };
                #undelay {bot_kick};
                #undelay {bot_restart};
            } {
                #var mob[target] $mob[autoName];
            };

            #if {$foundTimes == 1} {
                #if {&room[mobs] > 0} {
                    #var tmpFound 0;
                    #foreach $room[mobs][%*] tmpItem {
                        #if {"$tmpItem[name]" == "$mob[long]"} {
                            #math mobIndex {$tmpItem[index] + 1};
                        };
                    };
                    #list room[mobs] add {{{autoName} {$mob[autoName]} {name} {$mob[long]} {health} {$tmpHealth} {index} {$mobIndex} {botTarget} {$botTarget} {botName} {$mob[target]}}};
                };
            } {
                #loop 1 $foundTimes tmpIndex {
                    #list room[mobs] add {{{autoName} {$mob[autoName]} {name} {$mob[long]} {health} {$tmpHealth} {index} {$tmpIndex} {botTarget} {$botTarget} {botName} {$mob[target]}}};
                };
            };
        };
    }
} {2};

#class {common_room} {close};