#class draw_mapFull kill;
#class draw_mapFull open;

#NOP #event {MAP REGION PRESSED MOUSE BUTTON ONE} {#echo {CLICKED MAP REGION (%0,%1) vnum %4 [%5]}};

#screen get rows screen[height];
#screen get cols screen[width];

#math mapAreaRightCol {.2 * $screen[width]};
#format mapAreaRightCol {%d} $mapAreaRightCol;

#NOP -- The map area picks up 2 cols after the area section;
#math mapLeftCol {$mapAreaRightCol + 2};
#math mapInfoTop {.28 * $screen[height]};
#format mapInfoTop {%d} {$mapInfoTop};

#math mapAreaBotRow {$mapInfoTop + 2};

#math mapOffset {$screen[height] / 2.5 * -1};
#format mapOffset {%d} {$mapOffset};

#NOP -- The area describer in the bottom area summary;
#math mapInfoButtonTop {$mapAreaBotRow - 1};

#var tmpRoomSelected {};

#alias _init_mapFull {
    #buffer clear;
    #screen clear all;
    #split 0 $mapInfoTop $mapAreaRightCol 0 1;
    #unmacro %*;
    #map read common/map/3k_shared.map;
    #var tmpRoomSelected[area] $area;
    #map goto $map[full][startVnum];
    #map map;
    #map flag vtmap on;
    #Map offset 1 $mapLeftCol $mapOffset $mapOffset;
    #config mouse on;
    #read common/data/areas.tin;
    #macro {q} {#{ats} {#port {untilialize}};#zap {port_mapFull}};
    #delay 3 {_draw_mapAreas_initiate};
};

#function {min} {#if {%1 < %2} {#return %1} {#return %2}};
#function {max} {#if {%1 > %2} {#return %1} {#return %2}};

#alias {_draw_mapAreas_initiate} {
    #NOP -- Set how large of a slice we should be taking;
    #math map[full][list][bounds] 15;

    #NOP -- Populate area list and set where the area list box starts at since;
    #NOP -- we want the current area to be highlighted and centered on the list to start;
    #unvar tmpMapAreaList;
    #if {"$area" == "" || "$area" == "$area"} {
        #var area Pinnacle;
        #list tmpMapAreaList find {Pinnacle} {map[full][list][current_area_index]}
    };

    #loop 1 &data[areas][] index {
        #if {"$area" == "$data[areas][$index][name]"} {
            #list tmpMapAreaList add {<fff> ** $data[areas][$index][name] ** <ada>};
            #var map[full][list][current_area_index] $index;
        } {
            #if {"$area" != ""} {#list tmpMapAreaList add {$data[areas][$index][name]}};
        };
    };

    #var map[full][list][max] &data[areas][];

    #if {$map[full][list][current_area_index] > 0} {
        #math map[full][list][start] {$map[full][list][current_area_index] - $map[full][list][bounds]};
        #math map[full][list][end] {$map[full][list][current_area_index] + $map[full][list][bounds]};

        #var map[full][list][start] @max{1;$map[full][list][start]};
        #var map[full][list][end] @min{$map[full][list][max];$map[full][list][end]};
    } {
        #var map[full][list][start] 1;
    };

    _draw_mapAreas;
};

#alias {_draw_mapAreas} {
    #var tmpString  ;
    #if {"$tmpRoomSelected[area]" != ""} {#var tmpString $tmpRoomSelected[area]};
    #if {"$tmpRoomSelected[roomname]" != ""} {#var tmpString {$tmpString \n  <fff>Room : $tmpRoomSelected[roomname]<088>}};
    #if {"$tmpRoomSelected[exits]" != ""} {#var tmpString {$tmpString \n  <fff>Exits: $tmpRoomSelected[exits]<088>}};
    #if {"$tmpRoomSelected[roomnote]" != ""} {#var tmpString {$tmpString \n  <fff>Notes: $tmpRoomSelected[roomnote]<088>}};
    #line sub var #line sub esc #draw green left right bottom side 2 1 -$mapAreaBotRow $mapAreaRightCol $tmpMapAreaList[$map[full][list][start]..$map[full][list][end]];
    #draw <fba> box -$mapInfoButtonTop 1 -2 $mapAreaRightCol {$tmpString};
    #draw <fba> box -$mapInfoButtonTop $mapAreaRightCol -2 -1 {<fff>$tmpRoomSelected[roomdesc]<088>};
    #draw green calign jeweled line 1 1 1 ${mapAreaRightCol}  {    AREA LIST    };
};

#alias {_draw_mapAreasScrollUp} {
    #if {$map[full][list][start] > 1} {
        #math map[full][list][start] {$map[full][list][start] - 1};
        #math map[full][list][end] {$map[full][list][end] - 1};
    } {
        #NOP
    };
    _draw_mapAreas;
};

#alias {_draw_mapAreasScrollDown} {
    #if {$map[full][list][end] <= $map[full][list][max]} {
        #math map[full][list][start] {$map[full][list][start] + 1};
        #math map[full][list][end] {$map[full][list][end] + 1};
    } {
        #NOP
    };
    _draw_mapAreas;
};



#NOP -- Click and drag map;
#NOP #event {MAP REGION PRESSED MOUSE BUTTON ONE} {   ;
#NOP     #var {o_x} %0;   ;
#NOP     #var {o_y} %1;   ;
#NOP };   ;

#event {MAP REGION MOVED MOUSE BUTTON ONE} {
    #var d_x %0;
    #var d_y %1;
    #math {c_x} {$o_x - $d_x};
    #math {c_y} {$d_y - $o_y};
    #map center $c_y $c_x 0;
};


#EVENT {SCROLLED MOUSE WHEEL UP} {
    _draw_mapAreasScrollUp
};

#EVENT {SCROLLED MOUSE WHEEL DOWN} {
    _draw_mapAreasScrollDown
};

#EVENT {SHORT-CLICKED MOUSE BUTTON ONE} {
    #if $mapClick {#NOP} {
        #NOP -- %0 is row from the top, %1 is column;
        
        #math map[full][list][selected][index] {$map[full][list][start] + %0 + 5};
        #var map[full][list][selected][name] $tmpMapAreaList[$map[full][list][selected][index]];

        #var tmpRoomSelected[area] $map[full][list][selected][name];
        #var tmpRoomSelected[roomname]  ;
        #var tmpRoomSelected[roomdesc]  ;
        #var tmpRoomSelected[roomexits]  ;
        _draw_mapAreas;

        #map list {roomarea}{$map[full][list][selected][name]} {variable}{arealist};
        #map goto *arealist[+1];
        #map map;
        _draw_mapAreas;
    };
};

#EVENT {MAP REGION PRESSED MOUSE BUTTON ONE} {
    #var mapClick 1;
    #var {o_x} %0;
    #var {o_y} %1;

    #if {"%4" == "0"} {#NOP} {
        #map goto %4;
        #map get roomname tmpRoomSelected[roomname];
        #map get roomdesc tmpRoomSelected[roomdesc];
        #map get roomnote tmpRoomSelected[roomnote];
        #map get roomexits tmpRoomExits;
        #var tmpRoomSelected[exits]  ;
        #foreach *tmpRoomExits[] key {
            #cat tmpRoomSelected[exits] {$key,};
        };
        #format exitLen {%L} {$tmpRoomSelected[exits]};
        #math exitLen {$exitLen - 1};
        
        #format tmpRoomSelected[exits] {%.${exitLen}s} {$tmpRoomSelected[exits]};
        
        #replace tmpRoomSelected[roomdesc] {\n} { };
        #replace tmpRoomSelected[roomname] {\n%*} {};
        _draw_mapAreas;
    };
    #delay .2 {#var mapClick 0};
};


#class draw_mapFull close;