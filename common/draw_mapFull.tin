#class draw_mapFull kill;
#class draw_mapFull open;

#NOP #event {MAP REGION PRESSED MOUSE BUTTON ONE} {#echo {CLICKED MAP REGION (%0,%1) vnum %4 [%5]}};

#var mapLeftCol 32;
#var mapAreaRightCol 30;
#var mapDriverTopRow 30;

#alias _init_mapFull {
    #buffer clear;
    #screen clear all;
    #split 0 8 $mapAreaRightCol 0 1;
    #unmacro %*;
    #map read common/map/3k_shared.map;
    #var buttons[area_selected][text] $area;
    #map goto $map[full][startVnum];
    #map map;
    #map flag vtmap on;
    #Map offset 1 $mapLeftCol -15 -15;
    #config mouse on;
    #read common/data/areas.tin;
    #macro {q} {#{ats} {#port {untilialize}};#zap {port_mapFull}};
    #delay 3 {_draw_mapAreas_initiate};
};

#function {min} {#if {%1 < %2} {#return %1} {#return %2}};
#function {max} {#if {%1 > %2} {#return %1} {#return %2}};

#alias {_draw_mapAreas_initiate} {
    #NOP -- Set how large of a slice we should be taking;
    #math map[full][list][bounds] 15;

    #NOP -- Populate area list and set where the area list box starts at since;
    #NOP -- we want the current area to be highlighted and centered on the list to start;
    #unvar tmpMapAreaList;
    #if {"$area" == "" || "$area" == "$area"} {
        #var area Pinnacle;
        #list tmpMapAreaList find {Pinnacle} {map[full][list][current_area_index]}
    };

    #loop 1 &data[areas][] index {
        #if {"$area" == "$data[areas][$index][name]"} {
            #list tmpMapAreaList add {<fff> ** $data[areas][$index][name] ** <ada>};
            #var map[full][list][current_area_index] $index;
        } {
            #if {"$area" != ""} {#list tmpMapAreaList add {$data[areas][$index][name]}};
        };
    };

    #var map[full][list][max] &data[areas][];

    #if {$map[full][list][current_area_index] > 0} {
        #math map[full][list][start] {$map[full][list][current_area_index] - $map[full][list][bounds]};
        #math map[full][list][end] {$map[full][list][current_area_index] + $map[full][list][bounds]};

        #var map[full][list][start] @max{1;$map[full][list][start]};
        #var map[full][list][end] @min{$map[full][list][max];$map[full][list][end]};
    } {
        #var map[full][list][start] 1;
    };

    _draw_mapAreas;
};

#alias {_draw_mapAreas} {
    #line sub var #line sub esc #draw green left right bottom side 2 1 -10 25 $tmpMapAreaList[$map[full][list][start]..$map[full][list][end]];
    #draw <fba> box -9 1 -6 25 {$buttons[area_selected][text]};
    #draw green calign jeweled line 1 1 1 30  {    AREA LIST    };
};

#alias {_draw_mapAreasScrollUp} {
    #if {$map[full][list][start] > 1} {
        #math map[full][list][start] {$map[full][list][start] - 1};
        #math map[full][list][end] {$map[full][list][end] - 1};
    } {
        #NOP
    };
    _draw_mapAreas;
};

#alias {_draw_mapAreasScrollDown} {
    #if {$map[full][list][end] <= $map[full][list][max]} {
        #math map[full][list][start] {$map[full][list][start] + 1};
        #math map[full][list][end] {$map[full][list][end] + 1};
    } {
        #NOP
    };
    _draw_mapAreas;
};



#NOP -- Click and drag map;
#NOP #event {MAP REGION PRESSED MOUSE BUTTON ONE} {   ;
#NOP     #var {o_x} %0;   ;
#NOP     #var {o_y} %1;   ;
#NOP };   ;

#NOP #event {MAP REGION MOVED MOUSE BUTTON ONE} {   ;
#NOP     #var d_x %0;   ;
#NOP     #var d_y %1;   ;
#NOP     #math {c_x} {$o_x - $d_x};   ;
#NOP     #math {c_y} {$d_y - $o_y};   ;
#NOP     #map center $c_y $c_x 0   ;
#NOP };   ;


#EVENT {SCROLLED MOUSE WHEEL UP} {
    _draw_mapAreasScrollUp
};

#EVENT {SCROLLED MOUSE WHEEL DOWN} {
    _draw_mapAreasScrollDown
};

#EVENT {SHORT-CLICKED MOUSE BUTTON ONE} {
    #if $mapClick {#NOP} {
        #showme 0 (%0) 1 (%1) 2 (%2) 3 (%3) 4 (%4) 5 (%5);
        #NOP -- %0 is row from the top, %1 is column
        
        #math map[full][list][selected][index] {$map[full][list][start] + %0 - 2};
        #echo {index is $map[full][list][selected][index]};
        #var map[full][list][selected][name] $tmpMapAreaList[$map[full][list][selected][index]];

        #var buttons[area_selected][text] $map[full][list][selected][name];
        _draw_mapAreas;

        #map list {roomarea}{$map[full][list][selected][name]} {variable}{arealist};
        #map goto *arealist[+1];
        #map map;
        _draw_mapAreas
    };
};

#EVENT {MAP REGION PRESSED MOUSE BUTTON ONE} {
    #var mapClick 1;
    #map goto %4;
    #map get roomname buttons[room_selected][text];
    #replace buttons[area_selected][text] {\n%*} {};
    #var buttons[area_selected][text] {$buttons[area_selected][text] \n $buttons[room_selected][text]};
    _draw_mapAreas;
    #delay .2 {#var mapClick 0};
};


#class draw_mapFull close;