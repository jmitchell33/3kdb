#class common_strategy kill;
#class common_strategy open;

#NOP -- Set some variables for multi-round actions or toggles;
#var iprime_round 1;
#var subjugate_toggle 1;
#var botTickler 1;


#alias formatStrategy {
    #format result {<acf>-----<fff> %+15s  %s<088>} {%1} {%2};
    #showme $result;
};


#alias strategy-list {
    formatStrategy {Name} {Desc};
    #foreach $strategyList[%*] item {
        formatStrategy {$item[name]} {$item[description]};
    };
    #showme \n<bab>strategy<088> <bcc>name<088> to add or remove a strategy;
};

#alias strategy-clear {
    #list strategyActive clear;
    #if {"$draw[strategy][active]" == "on"} {draw_caption_strategy};
};

#alias strategy {
    #list {strategyList} {find} {%0} {tempListIndex};
    #var tempList $strategyList[+$tempListIndex];
    #if {&tempList[] > 0} {
        #NOP -- If the strategyActive list exists, check if the selected strategy is a member and toggle it;
        #NOP -- Otherwise, if there is no strategyActive list, we add it;
        #if {&strategyActive[] > 0} {
            #list strategyActive indexate name;
            #list {strategyActive} {find} {%0} {strategyExists};
            #if {$strategyExists} {
                #showme <caa>----  <fff> %0 is now an <baa>inactive<088> <fff>strategy <caa>  ----<088>;
                #list strategyActive filter {} {%0};
            } {
                #list strategyActive add {{{name} {$tempList[name]} {active} {1}}};
                #showme <caa>----  <fff> %0 is now an <bfd>active<088> <fff>strategy  <caa>  ----<088>;
            };
        } {
            #list strategyActive add {{{name} {$tempList[name]} {active} {1}}};
            #showme <caa>----  <fff> %0 is now an <bfd>active<088> <fff>strategy  <caa>  ----<088>;
        };
    } {
        #showme <caa>----  <fff> %0 not found in Strategy List.  <caa>  ----<088>;
        #showme <caa>----  <fff> 'strategy-list' will show a list of available strategies  <caa>  ----<088>;
    };
    #if {"$draw[strategy][active]" == "on"} {draw_caption_strategy};
};

#alias strategies {
    #showme {<ffb>   Active Strategies   <088>};
    #var inactiveStrats <ffb>   Inactive Strategies   <088>;
    #foreach $strategyActive[%*] item {
        #showme {<efe>   $item[name]   <088>};
    };
    #foreach $strategyList item {
        #var inactiveStrategies ${strategyList};
        #list inactiveStrategies indexate name;
        #foreach $strategyActive[%*] active {
            #list inactiveStrategies filter {} {$active[name]};
        };
    };
    #if {&inactiveStrategies[] > 0} {
        #showme {\n<ffb>   Inactive Strategies   <088>};
        #foreach $inactiveStrategies[%*] index {
            #if {"$index[guild]" == "all" || "$index[guild]" == "$guild"} {
                #showme {<cba>   $index[name]   <088>};
            };
        };
    };
    #showme \n<bab>strategy<088> <bcc>name<088> to add or remove a strategy;
    #showme <bab>strategy-list<088> to see available strategies;
};


#list strategyList clear;
#list strategyList add {{{guild} {all} {name} {lucanus} {active} {0} {description} {Automatically uses amulet and ring}}};
#list strategyList add {{{guild} {all} {name} {epowers} {active} {0} {description} {Automatically use fry, shred, ewell}}};
#list strategyList add {{{guild} {all} {name} {rolm}    {active} {0} {description} {Automatically use owl & raven runes}}};
#list strategyList add {{{guild} {all} {name} {rolm1}   {active} {0} {description} {ROLM Auto-Use Runes + Offhand Weapon}}};
#list strategyList add {{{guild} {all} {name} {bplate}  {active} {0} {description} {Blood Plate: Automatically feed}}};
#list strategyList add {{{guild} {all} {name} {tickler}  {active} {0} {description} {Bot: Leave mobs at 3%}}};
#list strategyList add {{{guild} {breed} {name} {enhance} {active} {0} {description} {Breed: Enhance}}};
#list strategyList add {{{guild} {necromancer} {name} {snare}  {active} {0} {description} {Vamp/Were: Snare}}};
#list strategyList add {{{guild} {necromancer} {name} {call}  {active} {0} {description} {Call Familiar}}};
#list strategyList add {{{guild} {necromancer} {name} {maim}  {active} {0} {description} {Were: Maim}}};
#list strategyList add {{{guild} {necromancer} {name} {scythe}  {active} {0} {description} {Reaper: Scythe}}};
#list strategyList add {{{guild} {necromancer} {name} {harvest}  {active} {0} {description} {Reaper: Grim Harvest}}};
#list strategyList add {{{guild} {necromancer} {name} {soulbind}  {active} {0} {description} {Reaper: Soulbind}}};
#list strategyList add {{{guild} {necromancer} {name} {rabies}  {active} {0} {description} {Were: Rabies}}};
#list strategyList add {{{guild} {necromancer} {name} {fury_prime}  {active} {0} {description} {Were: Fury Prime}}};
#list strategyList add {{{guild} {necromancer} {name} {fury} {active} {0} {description} {Were: Fury}}};
#list strategyList add {{{guild} {necromancer} {name} {spirithowl}  {active} {0} {description} {Were: Spirithowl}}};
#list strategyList add {{{guild} {necromancer} {name} {bloodarc}  {active} {0} {description} {Lich: Bloodarc}}};
#list strategyList add {{{guild} {necromancer} {name} {bloodlet}  {active} {0} {description} {Vamp: Bloodlet}}};
#list strategyList add {{{guild} {necromancer} {name} {bloodfuse}  {active} {0} {description} {Vamp: Bloodfuse}}};
#list strategyList add {{{guild} {necromancer} {name} {rage}  {active} {0} {description} {Vamp: Rage}}};
#list strategyList add {{{guild} {necromancer} {name} {rage_prime}  {active} {0} {description} {Vamp: Rage Prime}}};
#list strategyList add {{{guild} {necromancer} {name} {illume}  {active} {0} {description} {Pure: Illume}}};
#list strategyList add {{{guild} {necromancer} {name} {skullfire}  {active} {0} {description} {Pure: Skullfire}}};
#list strategyList add {{{guild} {necromancer} {name} {corrode}  {active} {0} {description} {Pure: Corrode}}};
#list strategyList add {{{guild} {necromancer} {name} {psibleed}  {active} {0} {description} {Pure: Psibleed}}};
#list strategyList add {{{guild} {necromancer} {name} {shackle}  {active} {0} {description} {Pure: Shackle}}};
#list strategyList add {{{guild} {necromancer} {name} {diurnum}  {active} {0} {description} {Necro: Diurnum}}};
#list strategyList add {{{guild} {necromancer} {name} {nocturnum}  {active} {0} {description} {Necro: Nocturnum}}};
#list strategyList add {{{guild} {necromancer} {name} {iprime}  {active} {0} {description} {Invoke Prime}}};
#list strategyList add {{{guild} {necromancer} {name} {conjure}  {active} {0} {description} {Conjure Undead: Conjure and Nuke Them}}};
#list strategyList add {{{guild} {necromancer} {name} {gemino}  {active} {0} {description} {Preserve Gemino Corpses}}};
#list strategyList add {{{guild} {juggernaut} {name} {frenzy}  {active} {0} {description} {Before Reset: Use Frenzy}}};
#list strategyList add {{{guild} {juggernaut} {name} {donateA}  {active} {0} {description} {Before Reset: Donate Airdrops}}};
#list strategyList add {{{guild} {juggernaut} {name} {donateF}  {active} {0} {description} {Before Reset: Donate Frenzy}}};
#list strategyList add {{{guild} {juggernaut} {name} {nukes}  {active} {0} {description} {Before Reset: Fire Nukes}}};
#list strategyList add {{{guild} {juggernaut} {name} {mech_resummon} {active} {0} {description} {Mech: Dismiss & resummon back into defend mode}}};
#list strategyList add {{{guild} {fremen} {name} {ambush} {active} {0} {description} {Fremen: Ambush your mobs with a crippling first blow}}};
#list strategyList add {{{guild} {fremen} {name} {agony} {active} {0} {description} {Fremen: Inflict pain (mind dmg)}}};
#list strategyList add {{{guild} {fremen} {name} {stun} {active} {0} {description} {Fremen: Stun mobs with a poison pellet}}};
#list strategyList add {{{guild} {fremen} {name} {voicehold} {active} {0} {description} {Fremen: Causes mob to stop fighting for a short time}}};
#list strategyList add {{{guild} {fremen} {name} {voicefeeble} {active} {0} {description} {Fremen: Mind AC shred}}};
#list strategyList add {{{guild} {fremen} {name} {spicebeer} {active} {0} {description} {Fremen: Drink spice beer}}};
#list strategyList add {{{guild} {fremen} {name} {focus} {active} {0} {description} {Fremen: Concentrate your attacks on a single mob}}};
#list strategyList add {{{guild} {psicorps} {name} {bfuel} {active} {0} {description} {Psicorps: Body Fuel}}};
#list strategyList add {{{guild} {psicorps} {name} {convert} {active} {0} {description} {Psicorps: Convert}}};
#list strategyList add {{{guild} {psicorps} {name} {body_adj} {active} {0} {description} {Psicorps: Body Adjustment}}};
#list strategyList add {{{guild} {psicorps} {name} {ab} {active} {0} {description} {Psicorps: Adapt Body}}};
#list strategyList add {{{guild} {psicorps} {name} {bear} {active} {0} {description} {Psicorps: AA - Bear (+Con)}}};
#list strategyList add {{{guild} {psicorps} {name} {cat} {active} {0} {description} {Psicorps: AA - Cat (+Dex)}}};
#list strategyList add {{{guild} {psicorps} {name} {owl} {active} {0} {description} {Psicorps: AA - Owl (+Wis)}}};
#list strategyList add {{{guild} {psicorps} {name} {fox} {active} {0} {description} {Psicorps: AA - Fox (+Cha)}}};
#list strategyList add {{{guild} {psicorps} {name} {bull} {active} {0} {description} {Psicorps: AA - Bull (+Str)}}};
#list strategyList add {{{guild} {psicorps} {name} {eagle} {active} {0} {description} {Psicorps: AA - Eagle (+Int)}}};
#list strategyList add {{{guild} {psicorps} {name} {rpain} {active} {0} {description} {Psicorps: Recall Pain}}};
#list strategyList add {{{guild} {psicorps} {name} {ragony} {active} {0} {description} {Psicorps: Recall Agony}}};
#list strategyList add {{{guild} {psicorps} {name} {demoralize} {active} {0} {description} {Psicorps: Demoralize}}};
#list strategyList add {{{guild} {monk} {name} {palm} {active} {0} {description} {Monk: Palm}}};
#list strategyList add {{{guild} {monk} {name} {pressure} {active} {0} {description} {Monk: Pressure}}};
#list strategyList add {{{guild} {priest} {name} {bark} {active} {0} {description} {Priest: Barkskin}}};
#list strategyList add {{{guild} {priest} {name} {haste} {active} {0} {description} {Priest: Haste}}};
#list strategyList add {{{guild} {priest} {name} {pro} {active} {0} {description} {Priest: Protection}}};
#list strategyList add {{{guild} {priest} {name} {vest} {active} {0} {description} {Priest: Vestment}}};
#list strategyList add {{{guild} {priest} {name} {bene} {active} {0} {description} {Priest: Beneficence}}};
#list strategyList add {{{guild} {priest} {name} {blade} {active} {0} {description} {Priest: Blade Barrier}}};
#list strategyList add {{{guild} {priest} {name} {blessing} {active} {0} {description} {Priest: Bless}}};
#list strategyList add {{{guild} {priest} {name} {poe} {active} {0} {description} {Priest: Protection of Elements}}};
#list strategyList add {{{guild} {priest} {name} {power} {active} {0} {description} {Priest: Protection of Power}}};
#list strategyList add {{{guild} {priest} {name} {invin} {active} {0} {description} {Priest: Invincible}}};
#list strategyList indexate name;

#alias checkStrategy {
    #if $debug[strategy] {#echo {<efc>RECEIVED %0<088>}};
    #var strategy_found 0;
    #if {&strategyActive[] > 0} {
        #foreach $strategyActive[%*] item {
            #if {"$item[name]" == "%1"} {
                #var strategy_found 1;
                #if $debug[strategy] {#echo {<efc>Strategy: %1   <fab>ACTION: %2<088>}};
                %2;
            };
        };
    };
    #NOP -- If there is a 3rd arguement, do this if the strategy isn't listed;
    #NOP -- This is useful for items like gemino preservations where you still want to do;
    #NOP -- a different action if the strategy isn't active;
    #if {!$strategy_found && "%3" !== ""} {
        #if $debug[strategy] {#echo {<efc>Strategy: %1   <fab>ACTION: %3<088>}};
        %3;
    };
};

#if {"$guild" == "priest"} {
    #alias {_strategy_priest} {
        #if {!idle_flag} {
            #NOP
        };
    };
};

#if {"$guild" == "monk"} {
    #alias {_strategy_monk} {
        #if {!$idle_flag} {
            #if {$mip[round] == 1} {
                #var mob[palm] 0;
                #var mob[pressure] 0;
            };
            #if {$action_round < $mip[round]} {
                checkStrategy {palm} {#if {!$mob[palm] && $my[monk][ae][current] > 0} {palm}};
                checkStrategy {pressure} {#if {!$mob[pressure]} {pressure}};
            };
        };
    };
};

#if {"$guild" == "psicorps"} {
    #alias {_strategy_psicorps} {
        #NOP
    };
};

#if {"$guild" == "fremen"} {
    #alias {_strategy_fremen} {
        #if {!$idle_flag} {
            #if {$mip[round] > 1} {
                checkStrategy {agony} {#if {$my[gp1][current] > 0 && $my[sp][current] > 0} {agony}};
                checkStrategy {stun} {#if {$my[gp1][current] > 0} {stun}};
                checkStrategy {voicehold} {#if {$my[gp1][current] > 0} {voice}};
                checkStrategy {voicefeeble} {#if {$my[gp1][current] > 0} {voice $enemy[target] to enfeeblement}};
                checkStrategy {spicebeer} {#if {$spicebeertoggle} {smuggle remove spice beer;drink spice beer}};
            };
        };
    };
    #alias _strategy_fremen_start {
        #if {$my[gp1][current] > 0} {
            checkStrategy {focus} {focus};
        };
    };
};

#if {"$guild" == "juggernaut"} {
    #var frenzy_toggle 1;
    #var nuke_toggle 1;
    #var donate_toggle 1;
    #var dismiss_support 1;

    #alias _strategy_juggernaut {
        #if {!$idle_flag} {
            #NOP -- resummon support mech after dismissing and back into defend mode when mech hp is below 25 percent;
            #if {"$shp" != "" && $dismiss_support && $my[clan] && $shp < 25} {
                checkStrategy {mech_resummon} {#var dismiss_support 0;dismiss;support summon;hp;#delay 4 {#var dismiss_support 1;support defend;hp}};
            };
            #NOP -- Use your powers when close to reset;
            #if {$reset >= 95} {
                #if {$frenzy_toggle && $my[clan] > 0 && $mip[round]} {
                    checkStrategy {frenzy} {#send !frenzy;#var frenzy_toggle 0};
                };
                #if {$nuke_toggle && $my[nukes] > 0 && $mip[round]} {
                    checkStrategy {nukes} {fire $my[nukes];#var nuke_toggle 0};
                };
                #NOP -- donate airdrops to needy friends;
                #if {$donate_toggle && $my[clan] > 1} {
                    checkStrategy {donateA} {donateSupport airdrop;#var donate_toggle 0;#delay 10 {#var donate_toggle 1}};
                };
                #if {$donatep_toggle && $my[clan] > 1} {
                    checkStrategy {donateF} {donateSupport frenzy;#var donate_toggle 0;#delay 10 {#var donate_toggle 1}};
                };
            };
        };
    };
};


#NOP -- First round strategies;
#if {"$guild" == "necromancer"} {

    #alias _strategy_necro_start {
        #if {$my[necromancer][maim]} {checkStrategy {maim} {#if {$my[necromancer][altered]} {maim} {qtrance alter hybrid/maim}};};

        checkStrategy {snare} {snare};
        checkStrategy {shackle} {shackle};
        checkStrategy {rabies} {rabies};
        checkStrategy {corrode} {corrode};
        checkStrategy {scythe} {scythe};
        checkStrategy {call} {call};
        checkStrategy {psibleed} {psibleed};
        checkStrategy {fury_prime} {#if {$my[necromancer][glamors][current]} {fury prime}};
        checkStrategy {fury} {#if {$my[necromancer][glamors][current]} {fury}};
        checkStrategy {rage_prime} {#if {$my[necromancer][glamors][current]} {rage prime}};
        checkStrategy {rage} {#if {$my[necromancer][glamors][current]} {rage}};
        checkStrategy {bloodlet} {#if {$my[necromancer][decanters][empty] > 0} {#if $vaf[numbers] {bloodlet $enemy[name_numbers]} {bloodlet $enemy[target]}};};
    };

    #alias _strategy_necro {
        #if {$mip[round] > 7 && $enemy[kill_rate_avg] >= 30 && $mip[round] < 15} {
            #if {$iprime_round && $my[necromancer][invoke-prime] > 0} {
                checkStrategy {iprime} {#var iprime_round 0;ip1;#delay 10 {#var iprime_round 1}};
            };

            #if {$my[necromancer][diurnum] > 0 || $my[necromancer][nocturnum] > 0} {
                checkStrategy {diurnum} {diurnum prime;#var my[necromancer][diurnum] 0;#var my[necromancer][nocturnum] 0;};
                checkStrategy {nocturnum} {nocturnum prime;#var my[necromancer][diurnum] 0;#var my[necromancer][nocturnum] 0;};
            };
            #if {$my[necromancer][bloodfuse] > 0 && $my[necromancer][decanters][powered] > 0} {
                checkStrategy {bloodfuse} {
                    corpse_select;
                    #if $mask[active] {
                        #send {qatrance cackle;qtrance smuggle remove potion/drink potion/preserve remains/bloodfuse corpse};
                        } {
                        #send {qtrance smuggle remove potion/drink potion/preserve remains/bloodfuse corpse};
                    };
                };
            };

            #if {$my[necromancer][soulbind] && $my[necromancer][souls] >= 7 && $my[necromancer][glamors][current] >= 1} {
                checkStrategy {soulbind} {soulbind};
            };

            #if {$my[necromancer][bloodarc] > 0 && !$mob[bloodarc]} {
                checkStrategy {bloodarc} {bloodarc};
            };
            
            #if {$my[necromancer][harvest] > 0} {
                checkStrategy {harvest} {harvest};
            };

            #if {$my[necromancer][skullfire] > 0 && $my[necromancer][souls] >= 7 && $my[necromancer][glamors][current] >= 1} {
                checkStrategy {skullfire} {skullfire};
            };

            #if {$my[necromancer][conjure_undead] > 0} {
                #if {$my[gp1][current] < 50000} {
                    checkStrategy {conjure} {nukeUndeadAbs};
                };
                #if {$my[gp1][current] > 50000 || $my[gp2][current] > 90} {
                    checkStrategy {conjure} {nukeUndeadPres};
                };
            };
        };

        #if {$my[necromancer][spirit-howl] > 0} {
            checkStrategy {spirithowl} {spirithowl;#var my[necromancer][spirit-howl] 0;};
        };

        #if {$my[necromancer][glamors][current] >= 1 && $my[gp1][current] < 50000 && !$my[necromancer][glamors][illuming]} {
            checkStrategy {illume} {illume};
        };  
    };
};


#alias _strategy_heartbeat {
    #if {!$idle_flag && $action_round < $mip[round]} {

        #NOP -- Mob Tickler/Leave them at 3%;
        #if {$bot[active] && $incombat} {
            checkStrategy {tickler} {
                #if {$enemy[hp] < 4 && $botTickler} {
                    #var botTickler 0;
                    .botStep;
                    #delay 4 {#var botTickler 1};
                };
            };
        };

        #NOP -- Actions to perform the first rounds of combat;
        #if {$mip[round] >= 1 && $mip[round] <= 3} {
            #if {"$guild" == "necromancer"} {_strategy_necro_start};
        };
        #NOP -- Actions to perform only once in first round;
        #if {$mip[round] == 1} {
            #if {"$guild" == "fremen"} {_strategy_fremen_start};
        };
        #NOP -- Actions to perform only in the beginning of the fight;
        #if {$mip[round] > 2 && $mip[round] < 6} {
            checkStrategy {epowers} {
                #if $eternal {
                    #if {$my[eternal][energy well]} {(energy well;#var mob[ewell] 1;#delay 180 {#var mob[ewell] 0};} {#if {${my[eternal][shred]} && !$mob[ewell]} {(shred;};};
                    #if {$my[eternal][fry]} {(fry;};
                };
            };

            checkStrategy {lucanus} {
                #if $lucanus_ring {push diamond};
                #if $lucanus_amulet {push symbol};
            };

            #if {$rolm[owl]} {
                checkStrategy {rolm} {touch owl rune};
                checkStrategy {rolm1} {
                    unwield;
                    wield rod of lordly might;
                    touch owl rune;
                    #delay 610 {unwield;wield $rolm[offhand]};
                };
            };
            #if {$rolm[raven] && $rolm[charges] > 50} {
                checkStrategy {rolm} {touch raven rune};
                checkStrategy {rolm1} {touch raven rune};
            };
            #if {$rolm[gremlin] && $rolm[charges] > 50} {
                checkStrategy {rolm} {touch gremlin rune};
                checkStrategy {rolm1} {touch gremlin rune};
            };
        };

        checkStrategy {bplate} {
            #class {eq_bloodplate} {size} {eq_bloodplate_loaded};
            #if $eq_bloodplate_loaded {#NOP} {#read ${cwd}/common/eq/bloodplate.tin};
        } {
            #class {eq_bloodplate} {size} {eq_bloodplate_loaded};
            #if $eq_bloodplate_loaded {#class eq_bloodplate kill;#var eq_bloodplate_loaded 0} {#NOP};
        };

        #NOP -- Actions to perform at any time in the fight;
        #if {"$guild" == "necromancer"} {_strategy_necro};
        #if {"$guild" == "bard"} {_strategy_guild};
        #if {"$guild" == "mage"} {_strategy_guild};
        #if {"$guild" == "juggernaut"} {_strategy_juggernaut};
        #if {"$guild" == "fremen"} {_strategy_fremen};
        #if {"$guild" == "monk"} {_strategy_monk};
        #if {"$guild" == "priest"} {_strategy_priest};
    };
};

#NOP -- Actions to perform before start of combat;
#if {"$guild" == "fremen"} {
    #var pre_fight_actions 1;
    #alias {.pre_fight_actions} {
        checkStrategy {ambush} {ambush $mob[target]};
        kill $mob[target];
    };
};

#class common_strategy close;