#class {bot} {open}

#var bot_message_1 '---- KAYOS WARS STEPPER ----';
#var bot_message_2 '---- ALL TARGETS ----';
#echo {$bot_message_1};
#echo {$bot_message_2};

#var check_death 1;

#NOP -- Clear old kayos variables and check inventory to see if there are any keys/cells;
#var kayos[key] 0;
#var kayos[cell] 0;
#var kayos[bunker] {};
#var kayos_bunker 1;
#var kayos[ship] 0;
#send {i};

#var {bot[path]} {e;e;e;e;e;e;e;n;w;w;w;w;w;w;w;n;e;e;e;e;e;e;e;n;w;w;w;w;w;w;w;n;e;e;e;e;e;e;e;n;w;w;w;w;w;w;w;n;e;e;e;e;e;e;e;n;w;w;w;w;w;w;w;s;s;s;s;s;s;s};
#var {bunker_path} {enter bunker;n;w;e;e;n;{grab cell;keep cell;s;w;s;out}};
#var {spaceship_path} {e;e;e;e;{open briefcase;touch beacon;.stop}};

#act {^An intricate digital key: Ok.$} {#math kayos[key] {$kayos[key]+1}};
#act {^key: Taken.$} {#math kayos[key] {$kayos[key]+1}};
#act {^You grab the power cell from the locker.$} {#math kayos[cell] {$kayos[cell] + 1}};

#NOP -- When the bot completes the main field, loop the 3 bunkers and then the spaceship;
#act {^------- BOT COMPLETE -----} {
    #var checkBunker 0;
    #var bot[step] 1;
    .pause;
    #if {$kayos_bunker == 1} {
        #echo {MAIN FIELD CLEAR -- ENTERING BUNKERS};
    };
    #delay 2 {
        #if {$kayos_bunker == 1} {
            #map run $kayos[bunker][1];
            #delay 3 {
                #list bot[steps] create {$bunker_path};
                unlock bunker;
                #map goto 22675;
                ..;
            };
        };
        #if {$kayos_bunker == 2} {
            #map goto ${kayos[bunker][1]};
            #delay 1 {#map run $kayos[bunker][2]};
            #delay 3 {
                #list bot[steps] create {$bunker_path};
                unlock bunker;
                #map goto 22675;
                ..;
            }
        };
        #if {$kayos_bunker == 3} {
            #map goto ${kayos[bunker][2]};
            #delay 1 {#map run $kayos[bunker][3]};
            #delay 3 {
                #list bot[steps] create {$bunker_path};
                unlock bunker;
                #map goto 22675;
                ..;
            };
        };
        #if {$kayos_bunker == 4} {
            #map goto ${kayos[bunker][3]};
            #delay 1 {#map run $kayos[ship]};
            #delay 3 {
                #3 insert cell;
                #list bot[steps] create {$spaceship_path};
                teleport;
                #map goto 10382;
                ..;
            }
        };
        #math kayos_bunker {$kayos_bunker + 1};
    };
} {1};

#NOP -- These two are for necromancer inventory, which is of course different than standard;
#var checkKey 1;
#act {%+2sAn intricate digital key} {
    #if {$checkKey && $inventory_check} {
        #var checkKey 0;
        #var kayos[key] 1;
        #delay 1 {#var checkKey 1};
    } {
        #math kayos[key] {$kayos[key] + 1};
    };
} {5};

#var checkCell 1;
#act {%+2sA dark mass of energy} {
    #if {$checkCell && $inventory_check} {
        #var checkCell 0;
        #var kayos[cell] 1;
        #delay 1 {#var checkCell 1};
    } {
        #math kayos[cell] {$kayos[cell] + 1};
    };
} {5};

#act {%d%*An intricate digital key.$} {
    #if $inventory_check {
        #var tempVar %2;
        #replace tempVar { } {};
        #replace tempVar {*} {};
        #var kayos[key] $tempVar;
    };
} {6};

#act {%d%*A dark mass of energy.$} {
    #if $inventory_check {
        #var tempVar %2;
        #replace tempVar { } {};
        #replace tempVar {*} {};
        #var kayos[cell] $tempVar;
    };
    #delay 1 {
        #if {$kayos[cell] >= 1} {
            #math kayos_bunker {$kayos[cell] + 1};
        };
    };
} {6};


#alias {.check_death} {
    wrap;slab;get key;get all from corpse;grab cell;keep key;keep cell;..
};

#list botmobs clear;
#list botmobs add {{{long} {A demon soldier} {target} {mob}}};
#list botmobs add {{{long} {A flameroller} {target} {mob}}};
#list botmobs add {{{long} {A refugee} {target} {mob}}};
#list botmobs add {{{long} {A robotic lion} {target} {mob}}};
#list botmobs add {{{long} {A robotic hound} {target} {mob}}};
#list botmobs add {{{long} {A robotic tiger} {target} {mob}}};
#list botmobs add {{{long} {A robotic velociraptor} {target} {mob}}};
#list botmobs add {{{long} {A small imp} {target} {mob}}};
#list botmobs add {{{long} {A TANK} {target} {mob}}};
#list botmobs add {{{long} {A time-lost pirate} {target} {mob}}};
#list botmobs add {{{long} {A tornado of Kayos energy} {target} {mob}}};
#list botmobs add {{{long} {A stowaway alien} {target} {mob}}};
#list botmobs add {{{long} {Beaster the Kayotic CyberBot} {target} {mob}}};
#list botmobs add {{{long} {Beelz, the Demon King} {target} {mob}}};
#list botmobs add {{{long} {Captain Harneth} {target} {mob}}};
#list botmobs add {{{long} {Commander Garn} {target} {mob}}};
#list botmobs add {{{long} {Ethebren, The Dark Wizard} {target} {mob}}};
#list botmobs add {{{long} {Gorian the Cloud-Eater} {target} {mob}}};
#list botmobs add {{{long} {Harnem the Kayos-Twisted Giant} {target} {mob}}};
#list botmobs add {{{long} {Karaket the Twisted God-King} {target} {mob}}};
#list botmobs add {{{long} {Myrr the Robot Swarm} {target} {mob}}};
#list botmobs add {{{long} {Omnicron the Devourer} {target} {mob}}};
#list botmobs add {{{long} {Q-97 the Android} {target} {mob}}};
#list botmobs add {{{long} {Ratl the Devil's Hand} {target} {mob}}};
#list botmobs add {{{long} {Shan the Night's Shadow} {target} {mob}}};
#list botmobs add {{{long} {Spacemancer Spiff} {target} {mob}}};
#list botmobs add {{{long} {Szthsz the Lizard} {target} {mob}}};
#list botmobs add {{{long} {Thill the Quill, Kayos Demon} {target} {mob}}};
#list botmobs add {{{long} {Thorion the Hammer} {target} {mob}}};
#list botmobs add {{{long} {Yttra the Great Worm} {target} {mob}}};



#var checkBunker 1;
#act {^-i-A squat bunker.$} {
    #if $checkBunker {
        #map get roomvnum bunkerVnum;
        #if {&kayos[bunker][] == 0} {
            #var kayos[bunker][1] $bunkerVnum;
        } {
            #if {$kayos[bunker][1] == $bunkerVnum} {#NOP} {
                #if {&kayos[bunker][] == 1} {
                    #var kayos[bunker][2] $bunkerVnum;
                } {
                    #if {{$kayos[bunker][1] == $bunkerVnum} || {$kayos[bunker][2] == $bunkerVnum}} {#NOP} {
                        #var kayos[bunker][3] $bunkerVnum;
                    };
                };
            };
        };
    };
} {1};

#act {^-i-A scorch-marked landing pad.$} {
    #map get roomvnum kayos[ship];
} {1};

#act {^-i-An intricate digital key%*$} {get key;} {1};
#act {You cannot go} {mapUndo};

#class {bot} {close}
#read common/bot/generic.tin;