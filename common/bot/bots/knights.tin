#class {bot} {kill}
#class {bot} {open}

#var {area} {Knights of Erah};
#var check_death 1;

#var bot_message_1 '----  KNIGHTS OF ERAH TRAINING  ----';
#showme $bot_message_1;

#var bot[path] {e;e;n;e;e;n;w;w;nw;w;w;ne;e;e;n;w;w;n;};

#var knight1_1 0;
#var knight2_1 0;
#var knight2_2 0;
#var knight2_3 0;
#var knight3_1 0;
#var knight3_2 0;


#action {%*|  Defeat challenger in arena               : %d/10} {#math knight1_1 {%2/10};};
#action {%*|  Defeat a 2nd class knight in arena       : %d/5} {#math knight2_1 {%2 / 5};};
#action {%*|  Defeat an officer class knight in arena  : %d/5} {#math knight2_2 {%2 / 5};};
#action {%s|  Defeat a knight commander in arena       : %d/1} {#math knight2_3 {%2 / 1};};

#action {%*|  Defeat a knight commander in arena       : %d/5} {#math knight3_1 {%2 / 5}};
#action {%*|  Defeat a grand commander class knight in arena : %d/5} {#math knight3_2 {%2 / 5}};

#action {-i-The Challenger Board.} {
    #NOP We treat the board like a mob;
    #var bot[foundmob] 1;
    examine board;
} {1};

#action {There is no reason to 'challenge' here.} {examine board} {1};
#action {There {is|are} %w {challenger|challengers} awaiting a noble challenger.} {
    #var knights_left @word_to_number{%2};
    challenge;
} {1};
#action {There are no challengers remaining here.} {
    #var knights_left 0;
    .botStep
} {1};

#action {%*Accepting %* defeat, your challenger} {
    #var incombat 0;
    #if {$knights_left > 0} {
        challenge
    } {
        .botStep
    };
} {1};

#action {%* shifts %* combat style to the %*.} {
    #var bot[foundmob] 1;
    #var mob[target] knight;
    #math knights_left {$knights_left - 1};
    .pre_fight_check;
    #if $mob_kill_action {
        .mob_kill_action;
    } {
        kill knight;
    };
} {1};

#action {Congratulations!  You have completed the mission} {mission ask;};
#action {You have been offered the mission} {mission accept;};

#action {You have a challenger right in front of you - focus!} {mapUndo} {1};
#action {You cannot leave while your challenge partner is present.} {mapUndo} {1};

#list botmobs clear;
#list botmobs add {{{long} {An%*Knight%*} {target} {knight}}};
#list botmobs add {{{long} {A%*Knight%*} {target} {knight}}};

#class {bot} {close}
#read common/bot/generic.tin;
