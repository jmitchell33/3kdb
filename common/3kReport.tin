#class 3kReport kill;
#class 3kReport open;

#NOP --- Byron's Extravagant Reporting Machina ---;
#NOP --- 3kReport to see the report            ---;

#NOP -- dreport is the object we use to select/customize which items to include in the report;
#list dReportList clear;
#list dReportList add {{{key} {killer} {lookup} {tmpKiller} {header_text} {Killer} {totals} {} {width} {15} {active} {1}}};
#list dReportList add {{{key} {rounds} {lookup} {tmpRounds} {header_text} {Rounds} {totals} {tmpTotalRnds} {width} {6} {active} {1}}};
#list dReportList add {{{key} {class} {lookup} {tmpClass} {header_text}   {Class} {totals} {tmpTotalClass} {width} {6} {active} {1}}};
#list dReportList add {{{key} {xp} {lookup} {tmpXP} {header_text}   {XP} {totals} {tmpTotalXp} {width} {6} {active} {0}}};
#list dReportList add {{{key} {xp_ratio} {lookup} {tmpRatio} {header_text}   {% of XP} {totals} {tmpTotalRatio} {width} {7} {active} {0}}};
#list dReportList add {{{key} {coins} {lookup} {tmpCoins} {header_text}   {Coins} {totals} {tmpTotalCoins} {width} {6} {active} {1}}};
#list dReportList add {{{key} {dmg_dealt} {lookup} {tmpDmg} {header_text}   {Dmg Dlt} {totals} {tmpTotalDmg} {width} {10} {active} {0}}};
#list dReportList add {{{key} {dmg_received} {lookup} {tmpDmgReceived} {header_text}   {Dmg Rcd} {totals} {tmpTotalDmgRec} {width} {15} {active} {0}}};
#list dReportList add {{{key} {corpses} {lookup} {tmpCorpses} {header_text}   {Corpses} {totals} {tmpTotalCorpses} {width} {6} {active} {0}}};
#list dReportList add {{{key} {gxp_earned} {lookup} {tmpGxp} {header_text}   {GXP Earned} {totals} {tmpTotalGxp} {width} {11} {active} {0}}};
#list dReportList add {{{key} {gxp_per_rnd} {lookup} {tmpGxpPerRnd} {header_text}   {GXP Per Rnd} {totals} {tmpAvgGxpPerRnd} {width} {11} {active} {0}}};
#list dReportList add {{{key} {mob_attacks_melee} {lookup} {tmpMobAttacksMelee} {header_text}   {Melee} {totals} {tmpTotalMobAttacksMelee} {width} {11} {active} {0}}};
#list dReportList add {{{key} {mob_attacks_special} {lookup} {tmpMobAttacksSpecial} {header_text}   {Specials} {totals} {tmpTotalMobAttacksSpecial} {width} {11} {active} {0}}};


#NOP -- Necromancer Specific;
#if {"$guild" == "necromancer"} {
    #list dReportList add {{{key} {dsave_count} {lookup} {tmpPerMobDsaves} {header_text}   {DS #} {totals} {tmpTotalDsaves} {width} {6} {active} {0}}};
    #list dReportList add {{{key} {dsave_nps} {lookup} {tmpPerMobDsaveNP} {header_text}   {DS NP} {totals} {tmpTotalDsaveNP} {width} {6} {active} {0}}};
};

#NOP -- Mage Specific;
#if {"$guild" == "mage"} {
    #list dReportList add {{{key} {sp_spent} {lookup} {tmpMobSpSpent} {header_text}   {SP Spent} {totals} {tmpTotalSpSpent} {width} {8} {active} {0}}};
};

#list dReportList indexate key;

#NOP -- This section manages the aliases and toggling items in 3kReport;
#alias format3kReportItem {
    #format result {<acf>%+10s  %s<088>} {%1} {%2};
    #echo { $result};
};

#alias 3kReport-config {
    #echo {<ffb>   Current 3kReport Settings   <088>\n};
    format3kReportItem {Active} {Key};
    #loop 1 &dReportList[] item {
        #if $dReportList[$item][active] {#var tmpActive <afb>ON<088>;} {#var tmpActive <caa>OFF<088>};
        format3kReportItem {$tmpActive} {$dReportList[$item][key]};
    };
    
    #echo {\n  <cdc>3kReport<088> to view the 3k Kill Report};
    #echo {\n  <efa>3kReport+<088> <bcc>key<088> to add an item to the Report};
    #echo {  <efa>3kReport-<088> <bcc>key<088> to remove an item from the Report};
};

#alias 3kReport- {
    #list dReportList indexate key;
    #if {"%0" != ""} {
        #list dReportList {find} {%0} {tempListIndex};

        #if {$tempListIndex > 0} {
            #var tempList $dReportList[+$tempListIndex];

            #if $tempList[active] {
                #var dReportList[+$tempListIndex][active] 0;
                #echo {<caa>----  <fff> %0 <faa>removed <eee>from the <fff>3kReport <caa>  ----<088>};
            } {
                #var dReportList[+$tempListIndex][active] 0;
                #echo {<caa>----  <fff> %0 <eee>is not currently on the <fff>3kReport  <caa>  ----<088>}   
            };
        } {
            #echo { <caa>----  <fff> %0 <eee>is not an option.  Please check spelling of the "key" field.  <caa>  ----<088>};
        };
    };
};

#alias 3kReport+ {
    #list dReportList indexate key;
    #if {"%0" != ""} {
        #list dReportList {find} {%0} {tempListIndex};

        #if {$tempListIndex > 0} {
            #var tempList $dReportList[+$tempListIndex];

            #if $tempList[active] {
                #echo {<caa>----  <fff> %0 is already in the <fff>3kReport <caa>  ----<088>};
            } {
                #var dReportList[+$tempListIndex][active] 1;
                #echo {<caa>----  <fff> %0 has been <bfd>added<088> to the <fff>3kReport  <caa>  ----<088>}   
            };
        } {
            #echo { <caa>----  <fff> %0 is not an option.  Please check spelling of the "key" field.  <caa>  ----<088>};
        };
    };
};



#alias _update_cstats {
    #local tmpClassKilled 0;
    #var captureKills 0;
    #if {&tmpLastKill[] > 0} {
        #list tmpLastKill clear;
    } {
        #list tmpLastKill create;
    };
    
    #local tmpLastKillClass 0;
    #local tmpLastKillMob  ;
    #local tmpLastKillDamageTaken 0;
    #action {---------------------------  Combat Statistics  ---------------------} {
        #class cstatsCapture open;
        #local tmpNumber 0;
        #event {CATCH PROCESSED LINE} {
            #format tmpLine {%p} {%%%1};
            #regex {$tmpLine} {Damage Taken:%*} {
                #format tmpNumber {%d} {&1};
                #NOP -- T1 is the prior total so we can see the difference since last fight;
                #if {$cmbt[damage_received] > 0} {
                    #var cmbt[damage_received_t1] $cmbt[damage_received];
                    #var cmbt[damage_received] $tmpNumber;
                } {
                    #var cmbt[damage_received_t1] $tmpNumber;
                    #var cmbt[damage_received] $tmpNumber;
                };
                #if {$cmbt[damage_received] != $cmbt[damage_received_t1]} {
                    #math tmpLastKillDamageTaken {$cmbt[damage_received] - $cmbt[damage_received_t1]};
                };
            };
            #regex {$tmpLine} {Rooms Explored} {
                #var captureKills 0;
                #var tmpLastKill $tmpLastKill[&tmpLastKill[]];
                #var mob[damage_received] $tmpLastKill[damage_received];
                #var mob[class] $tmpLastKill[class];
                #var deathReport[class] $tmpLastKill[class];
                #var deathReport[damage_received] $tmpLastKill[damage_received];
                
            };
            #if $captureKills {
                #regex {$tmpLine} {%* : %*} {
                    #local tmpLastKillClass &1;
                    #replace tmpLastKillClass {,} {};
                    #format tmpLastKillClass {%p} {$tmpLastKillClass};
                    #format tmpLastKillMob {%p} {&2};
                    #list tmpLastKill add {{{mob} {$tmpLastKillMob} {class} {$tmpLastKillClass} {damage_received} {$tmpLastKillDamageTaken}}};
                };
            };
            #regex {$tmpLine} {Last 5 kills:} {#var captureKills 1;};
            
            #if !$debug[event] {#NOP};
        };
        #class {cstatsCapture} {close};
    } {1};
    
    #action {Total Time Spent online : %*} {
        #class cstatsCapture kill;
        #unaction {---------------------------  Combat Statistics  ---------------------};
        #var tmpClassKilled 1;
        #line gag;
    } {1};

    #delay 2 {
        #if !$tmpClassKilled {
            #class cstatsCapture kill;
            #unaction {---------------------------  Combat Statistics  ---------------------};
        };
    };
    #send {!(cstats};
};

#alias update_xp {
    #if {"%1" == "start"} {
        #line oneshot #action {^You have %* total xp.$} {
            #if {$bot[active]} {
                #math bot[stats][xp] 0;
                #math bot[stats][loop][xp] 0;
            };
            #format my[xp] {%d} {%%1};
            #unaction {^You have %* total xp.$};
        } {2};
        xp;
        #if {"$draw[status][active]" == "on"} {#delay 2 {draw_caption_status}};
    } {
        #if {$bot[active]} {
            #math bot[stats][loop][mobs] {$bot[stats][loop][mobs] + 1};
            #math bot[stats][mobs] {$bot[stats][mobs] + 1};
        };
        
        #line oneshot #action {^You have %* total xp.$} {
            #format temp_xp {%d} {%%1};
            #if {$my[xp] > 0 && "$temp_exp" != "$my[xp]"} {
                #if {$temp_xp > $my[xp]} {
                    #math fight_xp {$temp_xp - $my[xp]};
                    #if {$bot[active]} {
                        #math bot[stats][xp] {$bot[stats][xp] + $fight_xp};
                        #math bot[stats][loop][xp] {$bot[stats][loop][xp] + $fight_xp};
                    };
                    #format my[xp] {%d} {%%1};
                } {
                    #NOP -- If current xp is less than total xp (spent it on stats or died) update my[xp] to reflect the new baseline;
                    #format my[xp] {%d} {%%1}; 
                }
            } {
                #format my[xp] {%d} {%%1};
            };
            #unaction {^You have %* total xp.$};
        };
        xp;
        #if {"$draw[status][active]" == "on"} {#delay 2 {draw_caption_status}};
    };

    #line oneshot #action {^At this rate you will level in %*} {
        #line gag;
    } {2};
};

#alias update_coins {
    #NOP -- If the coins don't increase, we know we're down or there's another change making a measurement unreliable;
    #var fight_coins 0;
    #action {^You are carrying %d coins in loose change.$} {
        #var temp_coins %%1;
        #if {$my[coins] > 0 && "$temp_coins" != "$my[coins]"} {
            #if {$temp_coins > $my[coins]} {
                #math fight_coins {$temp_coins - $my[coins]};
                #if {$bot[active]} {
                    #math bot[stats][coins] {$bot[stats][coins] + $fight_coins};
                    #math bot[stats][loop][coins] {$bot[stats][loop][coins] + $fight_coins};
                };
                #format my[coins] {%d} {%%1};
            } {
                #NOP -- If current coins are less than starting coins, we'll update the new baseline;
                #format my[coins] {%d} {%%1}; 
            };
        } {
            #format my[coins] {%d} {%%1};
        };
        #unaction {^You are carrying %d coins in loose change.$};
    };
    coins;
};

#alias _reportLine {
    #echo {-------------------------------------------------------------------------------------------};
};

#alias _reportHeader {
    #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>           <eaa>3k KILL REPORT           <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
};

#alias _reportHeader1 {
    #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>    <cdc>3kReport-config<088> to customize    <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
};

#alias 3kReport-clear {
    #list deathSummary_mob clear;
    #list deathSummary_killer clear;
    #list deathSummary_rounds clear;
    #list deathSummary_xp_earned clear;
    #list deathSummary_gxp_earned clear;
    #list deathSummary_gxp_earned_per_round clear;
    #list deathSummary_damage_dealt clear;
    #list deathSummary_coins_earned clear;
    #list deathSummary_corpses_used clear;
    #list deathSummary_damage_received clear;
    #list deathSummary_mob_dodges clear;
    #list deathSummary_class clear;
    #list deathSummary_xp_to_class clear;
    #list deathSummary_dsave_count clear;
    #list deathSummary_dsave_nps clear;
    #list deathSummary_mob_attacks_melee clear;
    #list deathSummary_mob_attacks_specials clear;
    #list deathSummary_sp_spent clear;
    #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>      <eaa>3k KILL REPORT CLEARED          <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
};

#alias 3kReport {
    #if {&deathSummary_mob[] == 0} {
        #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>      <eaa>NO KILLS YET YOU PACIFIST     <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
        _reportHeader1;
    } {
        #list deathSummary_mob size tempReportSize;
        #var tempReportStart 1;
        #format tmpTotalKills {%g} {$tempReportSize};

        #NOP -- Filter the list either by # or mob name;            ;
        #NOP #if {"%1" != ""} {         ;
        #NOP     #NOP -- If a # is provided, show the last # of kills - otherwise show the entire list;         ;
        #NOP     #NOP -- If a word is provided, filter based on that monster;           ;
        #NOP     #NOP -- Since tintin matches numbers as the word pattern, we'll have to rely on no digits being provided;          ;
        #NOP     #regex {%1} {%+1..d} {         ;
        #NOP         #math tempReportStart {$tempReportSize - %1};          ;
        #NOP         #if {%1 > $tempReportSize} {           ;
        #NOP             #format tmpTotalKills {%g} {$tempReportSize}           ;
        #NOP         } {            ;
        #NOP             #format tmpTotalKills {%g} {%1}            ;
        #NOP         };         ;
        #NOP     } {            ;
        #NOP         #format tmpWord {%p} {%1};         ;
        #NOP         #list tmpList filter {%i%*{$tmpWord}%*};           ;
        #NOP         #list tmpList size tempReportSize;         ;
        #NOP         #format tmpTotalKills {%g} {$tempReportSize};          ;
        #NOP     };         ;
        #NOP };         ;
        
        _reportHeader;
        
        #var tmpTotalXp 0;
        #var tmpTotalRnds 0;
        #var tmpTotalDmg 0;
        #var tmpTotalCoins 0;
        #var tmpTotalCorpses 0;
        #var tmpTotalClass 0;
        #var tmpTotalDsaves 0;
        #var tmpTotalDsaveNP 0;
        #var tmpTotalGxp 0;
        #var tmpAvgGxp 0;
        #var tmpAvgGxpPerRnd 0;
        #var tmpSpSpent 0;

        #var tmpPerMobXp 0;
        #var tmpPerMobRnds 0;
        #var tmpPerMobDmg 0;
        #var tmpPerMobCoins 0;
        #var tmpPerMobCorpses 0;
        #var tmpPerMobDsaves 0;
        #var tmpPerMobDsaveNP 0;
        #var tmpGxp 0;
        #var tmpGxpPerRnd 0;
        #var tmpTotalSpSpent 0;

        #var tmpMobAttacksMelee 0;
        #var tmpMobAttacksSpecial 0;
        #var tmpTotalMobAttacksMelee 0;
        #var tmpTotalMobAttacksSpecial 0;

        #if {$tempReportSize > 0} {
            #var reportFormat1 {%-15s};
            #var reportFormat2 {\{Mob\}};
            #local tmpItem 1;
            #loop 1 &dReportList[] tmpItem {
                #if $dReportList[$tmpItem][active] {
                    #line substitute variables #cat reportFormat1 {  %-$dReportList[$tmpItem][width]s};
                    #cat reportFormat2 {  \{$dReportList[$tmpItem][header_text]\}};
                };
            };
            #replace reportFormat2 {\\} {};
            #replace reportFormat2 {\/} {};
            #line substitute variables #format reportLine {${reportFormat1}} $reportFormat2;
            #echo { <dff>$reportLine<088>};
            #loop $tempReportStart $tempReportSize item {
                #math tmpTotalXp {$deathSummary_xp_earned[$item] + $tmpTotalXp};
                #math tmpTotalRnds {$deathSummary_rounds[$item] + $tmpTotalRnds};
                #math tmpTotalDmg {$deathSummary_damage_dealt[$item] + $tmpTotalDmg};
                #math tmpTotalCoins {$deathSummary_coins_earned[$item] + $tmpTotalCoins};
                #math tmpTotalCorpses {$deathSummary_corpses_used[$item] + $tmpTotalCorpses};
                #math tmpTotalClass {$deathSummary_class[$item] + $tmpTotalClass};
                #math tmpTotalDmgRec {$deathSummary_damage_received[$item] + $tmpTotalDmgRec};
                #math tmpTotalGxp {$deathSummary_gxp_earned[$item] + $tmpTotalGxp};
                #math tmpTotalMobAttacksMelee {$deathSummary_mob_attacks_melee[$item] + $tmpTotalMobAttacksMelee};
                #math tmpTotalMobAttacksSpecial {$deathSummary_mob_attacks_specials[$item] + $tmpTotalMobAttacksSpecial};
                #math tmpTotalMobAttacksTotal {$deathSummary_mob_attacks_total[$item] + $tmpTotalMobAttacksTotal};
                #math tmpTotalSpSpent {$deathSummary_sp_spent[$item] + $tmpTotalSpSpent};
                
                #math tmpTotalRatio {$tmpTotalXp / $tmpTotalClass * 100.00};
                #format tmpTotalRatio {%d} $tmpTotalRatio;


                #format tmpMob {%.15s} {$deathSummary_mob[$item]};
                #format tmpKiller {%.15s} {$deathSummary_killer[$item]};
                #format tmpClass {%M} {$deathSummary_class[$item]};
                #math tmpRatio {$deathSummary_xp_to_class[$item] * 100};
                #format tmpRatio {%d} $tmpRatio;
                #var tmpRatio ${tmpRatio}%;
                #format tmpXP {%M} {$deathSummary_xp_earned[$item]};
                #format tmpCoins {%g} {$deathSummary_coins_earned[$item]};
                #format tmpDmg {%g} {$deathSummary_damage_dealt[$item]};
                #format tmpDmgReceived {%g} {$deathSummary_damage_received[$item]};
                #format tmpRounds {%g} {$deathSummary_rounds[$item]};
                #format tmpCorpses {%g} {$deathSummary_corpses_used[$item]};
                #format tmpGxp {%g} {$deathSummary_gxp_earned[$item]};
                #format tmpGxpPerRnd {%g} {$deathSummary_gxp_earned[$item]};
                #format tmpMobAttacksMelee {%g} {$deathSummary_mob_attacks_melee[$item]};
                #format tmpMobAttacksSpecial {%g} {$deathSummary_mob_attacks_specials[$item]};
                #format tmpMobAttacksTotal {%g} {$deathSummary_mob_attacks_total[$item]};
                #format tmpMobSpSpent {%g} {$deathSummary_sp_spent[$item]};
                

                #var tmpLookup[tmpMob] $tmpMob;
                #var tmpLookup[tmpKiller] $tmpKiller;
                #var tmpLookup[tmpClass] $tmpClass;
                #var tmpLookup[tmpRatio] $tmpRatio;
                #var tmpLookup[tmpXP] $tmpXP;
                #var tmpLookup[tmpCoins] $tmpCoins;
                #var tmpLookup[tmpDmg] $tmpDmg;
                #var tmpLookup[tmpDmgReceived] $tmpDmgReceived;
                #var tmpLookup[tmpRounds] $tmpRounds;
                #var tmpLookup[tmpCorpses] $tmpCorpses;
                #var tmpLookup[tmpGxp] $tmpGxp;
                #var tmpLookup[tmpGxpPerRnd] $tmpGxpPerRnd;
                #var tmpLookup[tmpMobAttacksMelee] $tmpMobAttacksMelee;
                #var tmpLookup[tmpMobAttacksSpecial] $tmpMobAttacksSpecial;
                #var tmpLookup[tmpMobSpSpent] $tmpMobSpSpent;
                
                
                #if {$tmpCoins == 0} {#var tmpCoins -};
                #if {$deathSummary_damage_dealt[$item] == 0} {#var tmpDmg -};
                #if {$deathSummary_corpses_used[$item] == 0} {#var tmpCorpses -};

                #if {"$guild" == "necromancer"} {
                    #math tmpTotalDsaves {$deathSummary_dsave_count[$item] + $tmpTotalDsaves};
                    #math tmpTotalDsaveNP {$deathSummary_dsave_nps[$item] + $tmpTotalDsaveNP};
                    #format tmpDsaveCount {%g} {$deathSummary_dsave_count[$item]};
                    #format tmpDsaveNP {%g} {$deathSummary_dsave_nps[$item]};
                    #var tmpLookup[tmpPerMobDsaves] $tmpDsaveCount;
                    #var tmpLookup[tmpPerMobDsaveNP] $tmpDsaveNP;
                };
                
                #var reportFormat3 {\{$tmpMob\}};
                #loop 1 &dReportList[] tmpItem {
                    #if $dReportList[$tmpItem][active] {
                        #cat reportFormat3 {  \{$tmpLookup[$dReportList[$tmpItem][lookup]]\}};
                    };
                };
                #replace reportFormat3 {\\} {};
                #replace reportFormat3 {\/} {};
                #line substitute variables #format reportLine {${reportFormat1}} $reportFormat3;
                #echo { <eed>$reportLine<088>};
            };
            #math tmpPerMobCoins {$tmpTotalCoins / $tmpTotalKills};
            #math tmpPerMobDmg {$tmpTotalDmg / $tmpTotalKills};
            #math tmpPerMobRnds {$tmpTotalRnds / $tmpTotalKills * 1.0};
            #math tmpPerMobXp {$tmpTotalXp / $tmpTotalKills};
            #math tmpPerMobCorpses {$tmpTotalCorpses / $tmpTotalKills * 1.0};
            #math tmpAvgGxp {$tmpTotalGxp / $tmpTotalKills * 1.0};
            #math tmpAvgGxpPerRnd {$tmpTotalGxp / $tmpTotalRnds * 1.0};
            #math tmpPerMobAttacksMelee {$tmpTotalMobAttacksMelee / $tmpTotalKills};
            #math tmpPerMobAttacksSpecial {$tmpTotalMobAttacksSpecial / $tmpTotalKills};

            #format tmpPerMobXp {%M} $tmpPerMobXp;
            #replace tmpPerMobXp {G} {B};
            #format tmpPerMobRnds {%g} $tmpPerMobRnds;
            #format tmpPerMobDmg {%g} $tmpPerMobDmg;
            #replace tmpPerMobDmg {G} {B};
            #format tmpPerMobCoins {%g} $tmpPerMobCoins;

            #format tmpTotalXp {%M} $tmpTotalXp;
            #replace tmpTotalXp {G} {B};
            #format tmpTotalRnds {%g} $tmpTotalRnds;
            #format tmpTotalDmg {%M} $tmpTotalDmg;
            #replace tmpTotalDmg {G} {B};
            #format tmpTotalCoins {%g} $tmpTotalCoins;
            #format tmpTotalGxp {%g} $tmpTotalGxp;

            #format tmpTotalClass {%M} $tmpTotalClass;
            #replace tmpTotalClass {G} {B};

            #format tmpTotalDmgRec {%M} $tmpTotalDmgRec;
            #replace tmpTotalDmgRec {G} {B};

            #var tmpTotalRatio ${tmpTotalRatio}%;

            #format tmpTotalSpSpent {%g} $tmpTotalSpSpent;

            #if {"$guild" == "necromancer"} {
                #math tmpPerMobDsaves {$tmpTotalDsaves / $tmpTotalKills * 1.0};
                #format tmpPerMobDsaves {%g} $tmpPerMobDsaves;
                #format tmpPerMobDsaveNP {%g} $tmpPerMobDsaveNP;
                #format tmpTotalDsaveNP {%M} $tmpTotalDsaveNP;
                #replace tmpTotalDsaveNP {G} {B};
            };
            
            

            _reportLine;
            
            #var reportFormat3 {\{TOTALS\}};
                #loop 1 &dReportList[] tmpItem {
                    #if $dReportList[$tmpItem][active] {
                        #cat reportFormat3 {  \${$dReportList[$tmpItem][totals]\}};
                    };
                };
            #replace reportFormat3 {\\} {};
            #replace reportFormat3 {\/} {};
            #line substitute variables #format reportLine {${reportFormat1}} $reportFormat3;
            #echo { <eed>$reportLine<088>};
            _reportLine;
            _reportHeader;
            _reportHeader1;
        } {
            #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>      <eaa>NO KILLS YET YOU PACIFIST     <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
            _reportHeader1;
        };
    };
};

#class 3kReport close;