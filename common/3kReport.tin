#class 3kReport kill;
#class 3kReport open;

#alias _update_cstats {
    #local tmpClassKilled 0;
    #action {---------------------------  Combat Statistics  ---------------------} {
        #echo {caught combat stats};
        #class tmpCapture open;
        #local tmpNumber 0;
        #event {CATCH PROCESSED LINE} {
            #format tmpLine {%p} {%%%1};
            #regex {$tmpLine} {Damage Taken:%*} {
                #format tmpNumber {%d} {&1};
                #NOP -- T1 is the prior total so we can see the difference since last fight;
                #if {$cmbt[damage_taken] > 0} {
                    #var cmbt[damage_taken_t1] $cmbt[damage_taken];
                    #var cmbt[damage_taken] $tmpNumber;
                    
                } {
                    #var cmbt[damage_taken_t1] $tmpNumber;
                    #var cmbt[damage_taken] $tmpNumber;
                }
            };
            #line gag;
        };
        #class {tmpCapture} {close};
    } {1};
    
    #action {Total Time Spent online : %*} {
        #class tmpCapture kill;
        #unaction {---------------------------  Combat Statistics  ---------------------};
        #var tmpClassKilled 1;
        #line gag;
    } {1};

    #delay 2 {
        #if !$tmpClassKilled {
            #class tmpCapture kill;
            #unaction {---------------------------  Combat Statistics  ---------------------};
        };
    };
    #send {!(cstats};
};

#alias update_xp {
    #if {"%1" == "start"} {
        #action {^You have %* total xp.$} {
            #if {$bot[active]} {
                #math bot[stats][xp] 0;
                #math bot[stats][loop][xp] 0;
            };
            #format my[xp] {%d} {%%1};
            #unaction {^You have %* total xp.$};
        } {2};
        xp;
        #if {"$draw[status][active]" == "on"} {#delay 2 {draw_caption_status}};
    } {
        #if {$bot[active]} {
            #math bot[stats][loop][mobs] {$bot[stats][loop][mobs] + 1};
            #math bot[stats][mobs] {$bot[stats][mobs] + 1};
        };
        
        #action {^You have %* total xp.$} {
            #format temp_xp {%d} {%%1};
            #if {$my[xp] > 0 && "$temp_exp" != "$my[xp]"} {
                #if {$temp_xp > $my[xp]} {
                    #math fight_xp {$temp_xp - $my[xp]};
                    #if {$bot[active]} {
                        #math bot[stats][xp] {$bot[stats][xp] + $fight_xp};
                        #math bot[stats][loop][xp] {$bot[stats][loop][xp] + $fight_xp};
                    };
                    #format my[xp] {%d} {%%1};
                } {
                    #NOP -- If current xp is less than total xp (spent it on stats or died) update my[xp] to reflect the new baseline;
                    #format my[xp] {%d} {%%1}; 
                }
            } {
                #format my[xp] {%d} {%%1};
            };
            #unaction {^You have %* total xp.$};
        };
        xp;
        #if {"$draw[status][active]" == "on"} {#delay 2 {draw_caption_status}};
    };

    #action {^At this rate you will level in %*} {
        #class tmpCapture kill;
        #line gag;
    } {2};
};

#alias update_coins {
    #NOP -- If the coins don't increase, we know we're down or there's another change making a measurement unreliable;
    #var fight_coins 0;
    #action {^You are carrying %d coins in loose change.$} {
        #var temp_coins %%1;
        #if {$my[coins] > 0 && "$temp_coins" != "$my[coins]"} {
            #if {$temp_coins > $my[coins]} {
                #math fight_coins {$temp_coins - $my[coins]};
                #if {$bot[active]} {
                    #math bot[stats][coins] {$bot[stats][coins] + $fight_coins};
                    #math bot[stats][loop][coins] {$bot[stats][loop][coins] + $fight_coins};
                };
                #format my[coins] {%d} {%%1};
            } {
                #NOP -- If current coins are less than starting coins, we'll update the new baseline;
                #format my[coins] {%d} {%%1}; 
            };
        } {
            #format my[coins] {%d} {%%1};
        };
        #unaction {^You are carrying %d coins in loose change.$};
    };
    coins;
};

#alias _reportLine {
    #echo {-------------------------------------------------------------------------------------------};
};

#alias _reportHeader {
    #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>           <eaa>3k KILL REPORT           <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
};

#alias 3kReport-clear {
    #list deathSummary clear;
    #list tmpList clear;
    #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>      <eaa>3k KILL REPORT CLEARED          <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
};

#alias 3kReport {
    #list tmpList copy deathSummary;
    #list tmpList size tempReportSize;
    #var tempReportStart 1;
    #format tmpTotalKills {%g} {$tempReportSize};

    #NOP -- Filter the list either by # or mob name;
    #if {"%1" != ""} {
        #NOP -- If a # is provided, show the last # of kills - otherwise show the entire list;
        #NOP -- If a word is provided, filter based on that monster;
        #NOP -- Since tintin matches numbers as the word pattern, we'll have to rely on no digits being provided;
        #regex {%1} {%+1..d} {
            #math tempReportStart {$tempReportSize - %1};
            #if {%1 > $tempReportSize} {
                #format tmpTotalKills {%g} {$tempReportSize}
            } {
                #format tmpTotalKills {%g} {%1}
            };
        } {
            #format tmpWord {%p} {%1};
            #list tmpList indexate mob;
            #list tmpList filter {%i%*{$tmpWord}%*};
            #list tmpList size tempReportSize;
            #format tmpTotalKills {%g} {$tempReportSize};
        };
    };
    
    _reportHeader;
    
    #var tmpTotalXp 0;
    #var tmpTotalRnds 0;
    #var tmpTotalDmg 0;
    #var tmpTotalCoins 0;
    #var tmpTotalCorpses 0;

    #var tmpPerMobXp 0;
    #var tmpPerMobRnds 0;
    #var tmpPerMobDmg 0;
    #var tmpPerMobCoins 0;
    #var tmpPerMobCorpses 0;

    #if {$tempReportSize > 0} {
        #format reportLine {%-15s %-15s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-7s} {Killer} {Mob} {} {Rounds} {} {XP} {} {Coins} {} {Dmg} {} {Corpses};
        #echo { <dff>$reportLine<088>};
        #loop $tempReportStart $tempReportSize item {
            #math tmpTotalXp {$tmpList[$item][xp_earned] + $tmpTotalXp};
            #math tmpTotalRnds {$tmpList[$item][rounds] + $tmpTotalRnds};
            #math tmpTotalDmg {$tmpList[$item][damage_dealt] + $tmpTotalDmg};
            #math tmpTotalCoins {$tmpList[$item][coins_earned] + $tmpTotalCoins};
            #math tmpTotalCorpses {$tmpList[$item][corpses_used] + $tmpTotalCorpses};
            #format tmpMob {%.15s} {$tmpList[$item][mob]};
            #format tmpKiller {%.15s} {$tmpList[$item][killer]};
            #format tmpXP {%M} {$tmpList[$item][xp_earned]};
            #format tmpCoins {%g} {$tmpList[$item][coins_earned]};
            #format tmpDmg {%g} {$tmpList[$item][damage_dealt]};
            #format tmpRounds {%g} {$tmpList[$item][rounds]};
            #format tmpCorpses {%g} {$tmpList[$item][corpses_used]};
            
            #if {$tmpCoins == 0} {#var tmpCoins -};
            #if {$tmpList[$item][damage_dealt] == 0} {#var tmpDmg -};
            #if {$tmpList[$item][corpses_used] == 0} {#var tmpCorpses -};
            #format reportLine {%-15s %-15s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-7s} {$tmpKiller} {$tmpMob} {} {$tmpRounds} {} {$tmpXP} {} {$tmpCoins} {} {$tmpDmg} {} {$tmpCorpses};
            #echo { <eed>$reportLine<088>};
        };
        #math tmpPerMobCoins {$tmpTotalCoins / $tmpTotalKills};
        #math tmpPerMobDmg {$tmpTotalDmg / $tmpTotalKills};
        #math tmpPerMobRnds {$tmpTotalRnds / $tmpTotalKills * 1.0};
        #math tmpPerMobXp {$tmpTotalXp / $tmpTotalKills};
        #math tmpPerMobCorpses {$tmpTotalCorpses / $tmpTotalKills * 1.0};

        #format tmpPerMobXp {%M} $tmpPerMobXp;
        #replace tmpPerMobXp {G} {B};
        #format tmpPerMobRnds {%g} $tmpPerMobRnds;
        #format tmpPerMobDmg {%g} $tmpPerMobDmg;
        #replace tmpPerMobDmg {G} {B};
        #format tmpPerMobCoins {%g} $tmpPerMobCoins;

        #format tmpTotalXp {%M} $tmpTotalXp;
        #replace tmpTotalXp {G} {B};
        #format tmpTotalRnds {%g} $tmpTotalRnds;
        #format tmpTotalDmg {%g} $tmpTotalDmg;
        #replace tmpTotalDmg {G} {B};
        #format tmpTotalCoins {%g} $tmpTotalCoins;

        _reportLine;
        #format reportLine {%-15s %-15s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-7s} {TOTAL KILLS} {$tmpTotalKills} {} {$tmpTotalRnds} {} {$tmpTotalXp} {} {$tmpTotalCoins} {} {$tmpTotalDmg} {} {$tmpTotalCorpses};
        #echo { <eab>$reportLine<088>};
        #format reportLine {%-15s %-15s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-6s %+3s %-7s}  {PER MOB} {} {} {$tmpPerMobRnds} {} {$tmpPerMobXp} {} {$tmpPerMobCoins} {} {$tmpPerMobDmg} {} {$tmpPerMobCorpses};
        #echo { <eed>$reportLine<088>};
        _reportLine;
        _reportHeader;
    } {
        #echo {<eef>   -=+=-  -=+=-  -=+=-  -=+=-<088>      <eaa>NO KILLS YET YOU PACIFIST     <eef>-=+=-  -=+=-  -=+=-  -=+=-<088>};
    }
};

#class 3kReport close;