#class {default_aliases} {kill}
#class {default_aliases} {open}

#alias {echo_on} {#EVENT {SEND OUTPUT} {#line ignore #showme {<038>%0}};};
#alias {debug} {#debug all on;#EVENT {SEND OUTPUT} {#line ignore #showme {<038>%0}};};
#alias {debugoff} {#debug all off;#UNEVENT {SEND OUTPUT};};

#NOP -- Generic QOL improvements;
#alias {exa} {#send {!examine %0};#send {!search %0};#if {$explorer[updateDB]} {.lookAtMob %0}};
#alias {th} {#send {!tellhist}};
#alias {cwho1} {#send {!cwho -O}};
#alias {los} {#send {lineofsight}};

#NOP -- Generic toggle switch;
#delay 6 {
    #if {"${toggles[mission]}" == ""} {#var toggles[mission] 0};
    #if {"${toggles[3kreport]}" == ""} {#var toggles[3kreport] 1};
    #if {"${toggles[worlddrops]}" == ""} {#var toggles[worlddrops] 0};
    #if {"${toggles[chatlog_says]}" == ""} {#var toggles[chatlog_says] 1};
    #if {"${toggles[cult]}" == ""} {#var toggles[cult] 0};
    #if {"${toggles[corpse_echo]}" == ""} {#var toggles[corpse_echo] 0};
    #if {"${toggles[emote_follow]}" == ""} {#var toggles[emote_follow] 1};
    #if {"${toggles[emote_heal]}" == ""} {#var toggles[emote_heal] 1};
    #if {"${toggles[emote_eheal]}" == ""} {#var toggles[emote_eheal] 1};
    #if {"${toggles[emote_rally]}" == ""} {#var toggles[emote_rally] 1};
    #if {"${toggles[emote_corpse]}" == ""} {#var toggles[emote_corpse] 1};  
    #if {"${toggles[emote_dungeon_clear]}" == ""} {#var toggles[emote_dungeon_clear] 1};  
};

#alias {toggle} {
    #format toggle_var {%l} {%0};
    #format toggle_var {%p} {$toggle_var};
    #if {"$toggle_var" == ""} {
        #echo {<ffe>   -----  No toggle provided.  Use 'toggle-list' to see available options.  -----<088>};
    } {
        #if {"$toggles[$toggle_var]" == ""} {
            #echo {<ffe>   -----  $toggle_var not found in toggles  -----<088>};
        } {
            #if $toggles[$toggle_var] {
                #var toggles[$toggle_var] 0;
                #echo {<faa>   -----  $toggle_var has been turned off -----<088>};
            } {
                #var toggles[$toggle_var] 1;
                #echo {<cfb>   -----  $toggle_var has been turned on -----<088>};
            };
        };
    };
};

#alias {toggle-list} {
    #echo {<cfb>   ---++---       3kdb Current Toggle Status      ---++---<088>};
    #echo {<fff>       Usage: toggle <toggle alias> <088>};
    #format tmpLine {       %-25s          %+10s} {<fdc>Toggle Alias<088>} {<fdc>Status<088>};
    #echo {$tmpLine};
    #foreach *toggles[] item {
        #if {$toggles[$item]} {
            #format tmpLine {        %-25s         %+8s} {$item} {on};
            #echo {<dfc>$tmpLine<088>};
        } {
            #format tmpLine {        %-25s         %+9s} {$item} { off};
            #echo {<fbb>$tmpLine<088>};
        };
    };
    #echo {<cfb>   ---++---        ---++---        ---++---       ---++---<088>};
};

#NOP -- calculator;
#alias {calc} {
	#math {math} {%0};
	#format {math_format} {%g} {$math};
	#echo $math_format;
};

#NOP -- dupc and dove witch;
#alias {dwitch} {
    dc;
    dove;
    kill witch 1
};

#alias {bagall} {
    #var bagsFull 1;
    #format tmpBagCount {%d} %0;
    #loop $tmpBagCount 1 tmpBag {
        put all in bag $tmpBag
    };
    #line oneshot #action {You put %* in %*.} {#var bagsFull 0};
    #delay 4 {#if {$collector[items] && $bagsFull} {autocollect}};
};

#alias {unbagall} {
    #format tmpBagCount {%d} %0;
    drop all;
    #loop 1 $tmpBagCount tmpBag {
        get all from bag $tmpBag;drop all
    };
    #var bagsFull 0;
};

#NOP -- Use hmheal if possible, eternal heal otherwise;
#NOP -- If you're a mage, check if immersion is running;
#alias {useHeal} {
     #if {!$my[eternal][heal] && $my[hm][heal]} {
         #if {"$guild" == "mage"} {
            #if {!$my[immersion][active]} {hmheal};
         } {
            hmheal
         };
          
     };
     #if {$my[eternal][heal]} {
        #if {"$guild" == "mage"} {
            #if {!$my[immersion][active]} {(heal $user;};
         } {
            (heal $user;
         };
     };
};


#NOP -- Override the alias for inventory to reset the variables and set alias to pick which corpse to use;
#alias {i} {
    #if {"%1" == "unkept"} {
        #send {i unkept};
    } {
        #if {"$guild" == "necromancer"} {
            #NOP -- Gag these if you don't yet have chest;
            #line oneshot #gag {^There is no reason to 'retrieve' here.};
            #line oneshot #gag {^That is not here or it is uninteresting. (chest?)};
            #line oneshot #gag {^There is no reason to 'deliver' here.};

            #NOP -- Track decanters for bloodfuse;
            #var my[necromancer][decanters][empty] 0;
            #var my[necromancer][decanters][full] 0;
            #var my[necromancer][decanters][powered] 0;
            check_chest;
        };
        #var inventory_check 1;
        #var corpse_check 1;
        #var corpses[inventory] 0;
        #send i;
        #delay 2 {#var corpse_check 0;#var inventory_check 0;};
    };
};

#alias {autocollect} {
    #if $collector[items] {
        #showme {<ffe>------ ITEM COLLECTOR OFF --------<088>};
        #var collector[items] 0;
    } {
        #showme {<ffe>------ ITEM COLLECTOR ON --------<088>};
        #var collector[items] 1;
    };
};

#alias .kill_dmg_trackers {
    #class dmgtracker kill;
    #class dmgtracker_mage kill;
    #class dmgtracker_jugger kill;
};

#alias .kill_guild_classes {
    #class guild_actions kill;
    #class guild_aliases kill;
    #class guild_data kill;
    #class guild_gags kill;
    #class guild_heartbeat kill;
    #class guild_hpbar kill;
    #class guild_mage_defense_tracker kill;
    #class guild_miphp kill;
    #class guild_summoner kill;
    #class guild_tickers kill;
    #class guild_vars kill;
    #class guild_strategy kill;
    #class guild_eval kill;
};

#alias .kill_player_classes {
    #class player_actions kill;
    #class player_aliases kill;
    #class player_config kill;
    #class player_heartbeat kill;
    #class player_miphp kill;
    #class player_private kill;
    #class player_status kill;
    #class player_tickers kill;
};

#alias .reload {
    .kill_guild_classes;
    .kill_player_classes;
    #read chars/$user/vars.tin;
    #read chars/$user/actions.tin;
    #read chars/$user/aliases.tin;
    #read chars/$user/config.tin;
    #read chars/$user/heartbeat.tin;
    #read chars/$user/miphp.tin;
    #read chars/$user/private.tin;
    #read chars/$user/tickers.tin;
    #read common/guilds/$guild/index.tin;
    #read common/guilds/eternal/eternal.tin;
};


#NOP -- Generic action queue;
#var actions_stacked 0;
#alias do_action {
    #foreach $actionStack[%*] action {
        $action[action];
    };
    #list actionStack clear;
    #var actions_stacked 0;
};

#alias add_action {
    #var tmp %1;
    #var action_found 0;
    #var actions_stacked 1;
    #foreach $actionStack[%*] action {
        #regexp {"$tmp[name]"} {"$action[name]"} {
            #var action_found 1;
        };
    };
    #delay 0.2 {
        #if !$action_found {
            #list actionStack add {%1};
        }
    };
};

#alias remove_action {
    #var tmp %1;
    #if {&actionStack[] > 0} {
        #list actionStack indexate name;
        #list actionStack filter {} {%1};
        #if {&actionStack[] > 0} {#var actions_stacked 1;} {#var actions_stacked 0;};
    };
};

#var beepon 1;
#alias {beep} {#if {$beepon == 1} { #bell; }};
#alias {beepon} {#var beepon 1;#show Beep on.;};
#alias {beepoff} {#var beepon 0;#show Beep off.;};

#alias {grep} {#grep {%1} {}};

#ALIAS {ld} {
    _variable_export;
    #delay 1 {#end};
};

#alias {donate_house} {
    #var bot[paused] 1;
    keep all equip;
    #map get roomvnum donate_go_back;
    go house_box;
    #line oneshot #action {Byron's Treetop Gambling Emporium (d)} {
        put all in box;
        go morgue;
    } {1};

    #line oneshot #action {Pinnacle Morgue (n)} {
        donate all;
        donate coffin;
        #map run $donate_go_back;
        #delay 10 {
            #var bot[paused] 0;
        };
    } {1};
};


#alias {donate_necro} {
    #var bot[paused] 1;
    keep all equip;
    #map get roomvnum donate_go_back;
    #Map run 49586;
    #line oneshot #action {The Manager's Office (s,sw)} {
        qtrance {unkeep flask/put flask in box>50};
        go necro_box;
    } {1};

    #line oneshot #action {Theela the Necromancer (moral).} {
        #10 donate all;
        go necro_corpse;
    } {1};

    #line oneshot #action {Duncan, Keeper of the Dead.} {
        unwrap all;
        #if $inventory[freezer] {deslab all};
        #if $inventory[cooler] {#50 uncooler corpse};
        unkeep all corpse;
        store all;
        #delay 6 {
            qtrance preserve gemino/get corpse>50;
            get all;
            store all;
            qtrance preserve gemino/get corpse>50;
            get all;
            store all;
            qtrance preserve gemino/get corpse>50;
            get all;
            store all;
        };
        #delay 10 {#map run $donate_go_back};
        #delay 20 {#var bot[paused] 0;};
    } {1};
};

#alias {sell_catacombs} {
    #list add items slicer;
    #list add items chainmail;
    #list add items bone sword;
    #list add items leather armor;
    #list add items silver platemail;
    #list add items ragged cloak;
    stash all;
    keep all component;
    go shop2;
    #line oneshot #action {%* says: You do not have a %*.$} {
        #list items filter {} {%%2};
    };
    #16 sell chainmail;
    go shop4;
    #16 sell chainmail;
    go shop5;
    #16 sell chainmail;
    go shop6;
    #16 sell chainmail;
    go shop7;
    #16 sell chainmail;
    go shop8;
    #16 sell chainmail;
    go shop10;
};

#alias {sell_banshee} {
    stash all;
    keep all component;
    go shop2;
    #16 {sell bow;sell bracers};
    go shop4;
    #16 {sell bow;sell bracers};
    go shop5;
    #16 {sell bow;sell bracers};
    go shop6;
    #16 {sell bow;sell bracers};
    go shop7;
    #16 {sell bow;sell bracers};
    go shop8;
    #16 {sell bow;sell bracers};
    go shop10;
};


#alias {sell_whores} {
    stash all;
    keep all component;
    #loop 1 10 i {
        go shop$i;
        #16 {sell yeast;sell skoal;sell bitch boots;sell bitch blade;sell whips;sell purple heart;sell bitch plate;sell pimp shells;sell pimp gun;sell slickness;sell voucher;sell dermal plating}
    };
};

#alias {stashlist} {
    #if {"%1" == ""} {
        #send stashlist;
    } {
        #if {"%1" == "assemble"} {
            #echo {<fba>Loading assemble module...<088>};
            #read modules/crafting/assemble.tin;
            #send stashlist;
            #delay 2 {#class {autoassemble} {kill}};
        } {
            #send {stashlist %0};
        };
    };
};

#alias .rep {
    #if { "%0" == "" }
    {
        reps;
    };
    #else
    {
        #highlight {%%i%0%%*\|%%*} {<338>};reps;

        #delay {2} 
        {
                #unhighlight {%%i%0%%*\|%%*} {<338>};
        } {1};
    };
};

#NOP -- gswap;
#alias {.gswap} {
    #NOP -- kill guild classes;
    .kill_guild_classes;
    #NOP -- kill player classes;
    .kill_player_classes;
    #NOP -- kill dmgtrackers;
    .kill_dmg_trackers;
    #NOP -- kill botcycle;
    #class botcycle kill;
    #NOP -- kill mka actions;
    #class common_mka kill;
    #NOP -- load new guild variable;
    #var guild %0;
    #NOP -- load common;
    #read common/index.tin;
    #NOP -- load guild;
    #read common/guilds/$guild/index.tin;
    #NOP -- load character;
    #read chars/$user/actions.tin;
    #read chars/$user/aliases.tin;
    #read chars/$user/config.tin;
    #read chars/$user/heartbeat.tin;
    #read chars/$user/miphp.tin;
    #read chars/$user/private.tin;
    #read chars/$user/tickers.tin;
    #NOP -- load defaults;
    mipon;
    loadmap;
    postload;
    i;
    examine cooler;
    touch diamond rune;
    #send {(powers};
    .update_gags;
    _mob_status_clear;
    reset_trackers;
    #NOP -- Gag the few things this checks if it's not there for spam;
    #line oneshot #gag {^That is not here or it is uninteresting. (cooler?)};
    #line oneshot #gag {^You find nothing of interest.};
    #line oneshot #gag {^Perform touch at whom?};
    #line oneshot #gag {^There is no reason to '(powers' here.};
    #line oneshot #gag {^There is no reason to 'spellDmg-clear' here.};
    #line oneshot #gag {^There is no reason to 'summonDmg-clear' here.};
    #NOP -- Wait for variables to initialize, then redraw screen;
    #delay 6 {init_screen};
    #delay 7 {
        draw_all;
        clearChatHeader;
        draw_chat;
    }
};

#class {default_aliases} {close}