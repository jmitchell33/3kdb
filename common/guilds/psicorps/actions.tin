#class {guild_actions} {kill}
#class {guild_actions} {open}

#NOP --- PSICORPS ACTIONS;
#var reality_folded 0;
#var psi_entrance 53329;

#action {You reach out, tear the fabric of reality and then fold it around yourself.} {
	#if $reality_folded {
		#map goto $fold_reality_source;
		#var reality_folded 0;
	} {
		#var reality_folded 1;
		#map get roomvnum fold_reality_source;
		#map goto $psi_entrance;
		#if $psi_donate {
			#unticker donate_check;
			d;n;e;e;e;ne;put all in box;sw;se;put all in box;
			#NOP -- When we get to the common storage box, we'll fold reality and resume;
			#if {$bot[paused]} {#line oneshot #action {-i-General Storage box %*.} {#var bot[paused] 0;fold reality} {1}};
			#var psi_donate 0;
		};
	};
} {2};

#action {You can't focus on folding while fighting.} {
	#var reality_folded 0;
};


#NOP -- GXP Tracking;
#var gxp[total-kills] 0;
#var gxp[total-rounds] 0;
#var gxp[total-gxp] 0;

#alias {gxp_reset} {
	#var gxp[total-kills] 0;
	#var gxp[total-rounds] 0;
	#var gxp[total-gxp] 0;
	#var gxp[total-kills] 0;
	#var gxp[total-gxp] 0;
	#var gxp[total-rounds] 0;
	#var gxp[avg-gxp-per-round] 0;
	gs;
};

#action {^- Tour Number : %d %*} {
	#var psi_tour_base 5;
	#var my[psi_tour] %1;
	#math gxp[tour] {$my[psi_tour]/$psi_tour_base * $psi_tour_base};
	#foreach $psi_gxp[%*] index {#if {$index[tour] == $gxp[tour]} {#var gxp[round-max] $index[max_gxp_per_round];}};
} {1};

#alias gs_powers {
	#var add_powers 1;
	#unvar gs_powers;
	#unvar my[implants];
	#list gs_powers clear;
	#var powers_added {Added the following powers: };
	#send gs;
	#delay 3 {
		#foreach $psi_powers[%*] psi_power {
			#format power1 {%l} {$psi_power[power]};
			#foreach $gs_powers power {
				#format power2 {%l} {$power};
				#if {"$power1" == "$power2"} {
					#cat powers_added {$power2, };
					addp $psi_power[key];
				};
			};	
		};
		#showme $powers_added;
		#var add_powers 0;
	};
};

#act {^- T%d: %* %s-$} {
	#if $add_powers {
		#var temp %2;
		#replace temp {, } {;};
		#list gs_powers add {$temp};
	};
};


#act {^GXP Awarded from %*: %d} {
	#var gxp[last-fight-mob] %1;
	#var gxp[last-fight] %2;
	#math gxp[total-kills] {$gxp[total-kills] + 1};
	#math gxp[total-gxp] {$gxp[total-gxp] + %2};
	#math gxp[total-rounds] {$gxp[total-rounds] + $last_fight_rounds};
	#math gxp[avg-gxp-per-round] {$gxp[total-gxp] / $gxp[total-rounds]};
	
	#math gxp[last-fight-per-round] {$gxp[last-fight] / $last_fight_rounds};
	#echo {LAST FIGHT GXP: $gxp[last-fight-per-round] per rnd from $gxp[last-fight-mob]};
	#echo {TOTAL GXP: $gxp[total-gxp] ($gxp[avg-gxp-per-round] per round)};

} {1};

#NOP --- See power stats;
#substitute {Psychometabolism} {Psychometabolism [Str]};
#substitute {Psychoportation} {Psychoportation [Dex]};
#substitute {Psychokinesis} {Psychokinesis [Con]};
#substitute {Metacreativity} {Metacreativity [Int]};
#substitute {Clairsentience} {Clairsentience [Wis]};
#substitute {Telepathy} {Telepathy [Cha]};

#NOP ---- Healing;

#sub {^{A blue light engulfs your body as you use psionic energy to repair damage|to your cells.}$} {
	<159>%0<900>
};
#action {You convert %d spell points into %d psionic energy.} {
	#var my[sp][current] 0;
	#var my[sp][percent] 0;
} {2};

#NOP ---- Implants;

#action {^Implant: Shutdown complete.} {
	#var my[powers] {};
	#var my[implants] {};
} {2};

#action {^Implant: Startup sequence initiated.$} {
	gs_powers;
} {2};

#NOP ---- Damage type

#act {^%1 melee attacks do %2 damage.$} {
	#regex {%2} {{(mostly )?slashing}} {#var _adapt_body edged};
	#regex {%2} {{(mostly )?crushing}} {#var _adapt_body blunt};
	#regex {%2} {{(mostly )?fire}} {#var _adapt_body fire};
	#regex {%2} {{(mostly )?ice}} {#var _adapt_body ice};
	#regex {%2} {{(mostly )?acid}} {#var _adapt_body acid};
	#regex {%2} {{(mostly )?electric}}	{#var _adapt_body electric};
	#regex {%2} {{(mostly )?mental}} {#var _adapt_body mind};
	#regex {%2} {{(mostly )?energy}} {#var _adapt_body energy};
	#regex {%2} {{(mostly )?poison}} {#var _adapt_body poison};
	#regex {%2} {{(mostly )?radiation}}	{#var _adapt_body radiation};
} {2};

#NOP ---- Offense

#action {^You agitate the molecules in the air into a blast of brilliant light} {#var _bb 1} {2};
#action {^You demoralize} {#var mob[demoralize] 1} {2};
#action {^That target cannot be re-demoralized.} {#var mob[demoralize] 1} {2};
#action {^You open a rift in the astral plane and summon a large construct} {#var my[powers][Ac4] 1;} {2};
#action {^You open a rift in the astral plane and summon a colossal construct} {#var my[powers][Ac6] 1;} {2};
#action {^Your large construct fades back to the astral plane.$} {#var my[powers][Ac4] 0;} {2};
#action {^Your colossal construct fades back to the astral plane.$} {#var my[powers][Ac6] 0;} {2};
#action {^You become a blur of motion allowing you to attack more often.$} {#var my[powers][Bl] 1} {2};
#action {^You extend your arms out to the sides and create a psychic field} {#var my[powers][CP] 1} {2};
#action {^You cause %* to retransmit pain} {
	#var mob[recall_pain] 1;
	#if $gags[combat_guild] {#line gag};
	checkStrategy {rpain} {#delay 3 {#var mob[recall_pain] 0}};
	checkStrategy {ragony} {#delay 11 {#var mob[recall_pain] 0}};
	checkStrategy {rtorment} {#delay 11 {#var mob[recall_pain] 0}};
} {2};


#class {guild_actions} {close}