#class {guild_aliases} {kill}
#class {guild_aliases} {open}

#NOP --- PSICORPS ALIASES

#alias {track_regen} {
  #ACTION { HP  \(%d/%d\):} {#var hp_cur %%1;#math gp_round {gp_round + 1};#math hp_change {$hp_cur - $hp_last};#list hp_stats add {       HP : $hp_change};#var hp_last %%1;};
  #ACTION { SP  \(%d/%d\):} {#var sp_cur %%1;#math sp_change {$sp_cur - $sp_last};#list hp_stats add {       SP  : $sp_change};#var sp_last %%1;};
  #ACTION { GP1 \(%d/%d\):} {#var gp1_cur %%1;#math gp1_change {$gp1_cur - $gp1_last};#list gp_stats add {      GP1 : $gp1_change};#var gp1_last %%1;};
  #ACTION { GP2 \(%d/%d\):} {#var gp2_cur %%1;#math gp2_change {$gp2_cur - $gp2_last};#list gp_stats add {      GP2 : $gp2_change};#var gp2_last %%1;};
};

#alias {track_regen_reset} {
  #list gp_stats clear;
  #list hp_stats clear;
};

#alias {addp} {
  #var combat[%1] 1;
};

#alias {remp} {
  #if {"%1" == "offense"} {
    #var combat[Bo] 0;
    #var combat[Tl] 0;
    #var combat[CP] 0;
    #var combat[Cg] 0;
    #var combat[E] 0;
    #var combat[Ij] 0;
    #var combat[Sj] 0;
    #var combat[pf] 0;
    #var combat[bc] 0;
    #var combat[BC] 0;
    #var combat[Cg] 0;
    #var combat[Bl] 0;
  } {
    #var combat[%1] 0;
  }
};

#alias {_show_power} {
  #regex {*combat[]} {%1} {
      #if {$combat[%1]} {#showme %1 - %2;};
    } {
      #NOP;
    };
};

#alias {usep} {
  #var power_override %0;
};

#alias {showp} {
  #echo {----------------};
  #echo {--- Defense ----};
  #echo {----------------};

  _show_power {AA} {animal affinity bull};
  _show_power {ab} {adapt body};
  _show_power {Bf} {biofeedback};
  _show_power {Cp} {combat precognition};
  _show_power {D} {dodge};
  _show_power {Di} {displacement};
  _show_power {Eb} {energy barrier};
  _show_power {Ib} {inertial barrier};
  _show_power {IB} {improved biofeedback};
  _show_power {IV} {improved valor};
  _show_power {Vg} {vigor};
  _show_power {Vr} {valor};
  _show_power {Vv} {verve};
  _show_power {Iv} {Improved verve};
  _show_power {BFuel} {Body Fuel};

  #echo {----------------};
  #echo {--- Offense ----};
  #echo {----------------};
  
  _show_power {Bo} {bolt};
  _show_power {Tl} {talons};
  _show_power {CP} {combat prescience};
  _show_power {Cg} {conductive grasp};
  _show_power {E} {expertise};
  _show_power {Ij} {iron jaw};
  _show_power {Sj} {steel jaw};
  _show_power {pf} {psychofeedback};
  _show_power {Bl} {blur};
  
  #echo {----------------};
  #echo {--- Targeted ---};
  #echo {----------------};

  _show_power {Ac2} {astral construct 2};
  _show_power {Ac3} {astral construct 3};
  _show_power {Ac4} {astral construct 4};
  _show_power {Ac5} {astral construct 5};
  _show_power {bc} {biocurrent};
  _show_power {BC} {greater biocurrent};
  _show_power {Cg} {conductive grasp};
  _show_power {rpain} {recall pain};
  _show_power {ragony} {recall agony};

  #echo {----------------};
  #echo {---  Group   ---};
  #echo {----------------};

  _show_power {ML} {greater mindlink $greater_mind_link_target};

  #echo {----------------};
};

#class {guild_aliases} {close}
