#class {eternal} {kill}
#class {eternal} {open}

#var eternal 1;

#NOP -- Turn off eternal if either of these fail;
#action {There is no reason to '(cstats' here.} {
	#var eternal 0;
} {1};

#action {There is no reason to '(powers' here.} {
	#var eternal 0;
} {1};

#alias {_update_eternal_status} {
	#if {$my[eternal][heal]} {#format {heal_string} {<bfa>HEAL<088>}} {#format {heal_string} {<faa>HEAL<088>}};
	#if {$my[eternal][energy well]} {#format {ewell_string} {<bfa>EWELL<088>}} {#format {ewell_string} {<faa>EWELL<088>}};
	#if {$my[eternal][shred]} {#format {shred_string} {<bfa>SHRED<088>}} {#format {shred_string} {<faa>SHRED<088>}};
	#if {$my[eternal][duplicate creature]} {#format {dupec_string} {<bfa>DUPE-C<088>}} {#format {dupec_string} {<faa>DUPE-C<088>}};
	#if {$my[eternal][enlarge]} {#format {enlarge_string} {<bfa>ENLARGE<088>}} {#format {enlarge_string} {<faa>ENLARGE<088>}};
	#if {$my[eternal][fry]} {#format {fry_string} {<bfa>FRY<088>}} {#format {fry_string} {<faa>FRY<088>}};
	#if {$my[eternal][protection]} {#format {protection_string} {<bfa>PROT<088>}} {#format {protection_string} {<faa>PROT<088>}};
	#var eternal_string {<fff>Eternal: $ewell_string $heal_string $shred_string $dupec_string $enlarge_string $fry_string $protection_string<088>};
	#if {"$draw[caption][active]" == "on"} {draw_caption_status};
};

#NOP -- Shortcuts for powers;

#NOP -- embiggen - boost your stats;
#alias {em} {eternal embiggen};

#NOP -- heightened awareness - xp bonus for 1hr;
#alias {hw} {eternal heightened awareness};

#NOP -- duplicate creature;
#alias {dc} {
	#echo {<efb>========== <baf>DUPLICATING CREATURE <efb>==========<088>};
	#send {eternal duplicate creature %0}
};

#NOP -- reset dungeon;
#alias {rd3} {
	#switch {"%0"} {
		#case {"mj"} {#send {eternal reset dungeon Mahjongg party};};
		#case {"sz"} {#send {eternal reset dungeon Section Z party};};
		#case {"aegis"} {#send {eternal reset dungeon Aegis Global party};};
		#case {""}   {#echo {<efb>========== <faa>NO DUNGEON PROVIDED <efb>==========<088>}};
		#default 	 {#send {eternal reset dungeon %0 party};};
	};
};

#NOP -- announce dungeon resets;
#alias {dung_reset} {
	#switch {"%0"} {
		#case {"mj"} {#send {talk Resetting MJ soon, toke me for a rally\nctell Resetting MJ soon, toke me for a rally\nhm Resetting MJ soon, toke me for a rally\nflapchat Resetting MJ soon, toke me for a rally};};
		#case {"sz"} {#send {talk Resetting SZ soon, toke me for a rally\nctell Resetting SZ soon, toke me for a rally\nhm Resetting SZ soon, toke me for a rally\nflapchat Resetting SZ soon, toke me for a rally};};
		#case {"aegis"} {#send {talk Resetting AEGIS soon, toke me for a rally\nctell Resetting AEGIS soon, toke me for a rally\nhm Resetting AEGIS soon, toke me for a rally\nflapchat Resetting AEGIS soon, toke me for a rally};};
		#case {""}   {#echo {<efb>========== <faa>NO DUNGEON PROVIDED <efb>==========<088>}};
		#default 	 {#send {talk Resetting %0 soon, toke me for a rally\nctell Resetting %0 soon, toke me for a rally\nhm Resetting %0 soon, toke me for a rally\nflapchat Resetting %0 soon, toke me for a rally};};
	};
};

#NOP -- energy well;
#alias {ewell} {
	#echo {<efb>========== <baf>FIRING ENERGY WELL <efb>==========<088>};
	#send {eternal energy well %0}
};

#NOP -- reload item;
#alias {rload} {
	#switch {"%0"} {
		#case {"cup"} {#send {eternal reload item lord stanley's cup};};
		#case {"cooler"} {#send {eternal reload item Tech Ultra-Cooler};};
		#case {"freezer"} {#send {eternal reload item Death's Freezer};};
		#case {"glimmering"} {#send {eternal reload item Glimmering full platemail};};
		#case {"feather"} {#send {eternal reload item A feather from a black dove};};
		#case {"valor"} {#send {eternal reload item Valor of Arachnus};};
		#case {"powerstone"} {#send {eternal reload item Powerstone};};
		#case {"used"} {#send {eternal reload item A set of used armor};};
		#case {"auroshield"} {#send {eternal reload item The Aurothon Shield};};
		#case {""}   {#echo {<efb>========== <faa>NO ITEM PROVIDED <efb>==========<088>}};
		#default 	 {#send {eternal reload item %0};};
	};
};

#NOP -- immortality;
#alias {imm} {
	#echo {<efb>========== <baf>IMMORTALITY <efb>==========<088>};
	#send {eternal immortality};
};

#NOP -- protection;
#alias {prot} {
	#echo {<efb>========== <baf>PROTECTION <efb>==========<088>};
	#send {eternal protection};
};

#NOP -- fry ALL;
#alias {fry} {
	#if {"%0" == ""} {
		#send {eternal fry ALL};
		#echo {<efb>========== <baf>FRYING ALL MOBS <efb>==========<088>};
	}; #else {
		#send {eternal fry %0};
		#echo {<efb>========== <baf>FRYING %0 <efb>==========<088>};
	};
};


#NOP -- refresh room;
#alias {refresh} {
	#echo {<efb>========== <baf>REFRESHING ROOM <efb>==========<088>};
	#send {eternal refresh room};
};

#NOP -- unload room;
#alias {unload} {
	#echo {<efb>========== <baf>UNLOADING ROOM <efb>==========<088>};
	#send {eternal unload room};
};

#NOP -- resurrect - raise the DEAD!;
#alias {res} {
	#switch {"%0"} {
		#case {"myself"} {#send {eternal resurrect};};
		#case {""}   {#echo {<efb>========== <faa>NO PLAYER PROVIDED <efb>==========<088>}};
		#default 	 {#send {eternal resurrect %0 here};};
	};
};
	
#NOP -- teleport yourself to cot or your present party to cot or yourself to another party member;
#alias {tp1} {
	#switch {"%0"} {
		#case {"myself"} {#send {eternal teleport to CoT};};
        #case {"party"} {#send {eternal teleport party to CoT};};
		#case {""}   {#echo {<efb>========== <faa>NO PLAYER PROVIDED <efb>==========<088>}};
		#default 	 {#send {eternal teleport to %0};};
	};
};

#NOP -- teleport present party to party member;
#alias {tp2} {#echo {<efb>========== <baf>TELEPORTING PARTY TO PARTY MEMBER <efb>==========<088>};
	#send {eternal teleport party to %0};
};

#NOP -- summon entire party or party member to you - summon any player to cot only;
#alias {summ1} {
	#switch {"%0"} {
		#case {"party"} {#send {eternal summon party};};
		#case {""}   {#echo {<efb>========== <faa>NO PLAYER PROVIDED <efb>==========<088>}};
		#default 	 {#send {eternal summon %0};};
	};
};

#NOP -- summmon another players party to you at cot only;
#alias {summ2} {#echo {<efb>========== <baf>SUMMONING ANOTHER PLAYERS PARTY <efb>==========<088>};
	#send {eternal summon %0's party};
};

#NOP -- duplicate item;
#alias {dupe} {#echo {<efb>========== <baf>DUPLICATING ITEM <efb>==========<088>};
	#send {eternal duplicate item %0};
};

#NOP -- Add a bit more details/data points to the cstats;
#alias {(cstats} {
	#send {!(cstats};
	#line oneshot #action {^Combat Rounds:%*} {
		#format cstatCombatRounds {%d} %%1;
	};
	#line oneshot #action {^Total Time Spent online : %*} {
		#var cstatTotalRounds @timeToRounds{%%1};
		#math cstatCombatPercent {$cstatCombatRounds / $cstatTotalRounds * 100.00};
		#format cstatCombatRounds {%g} $cstatCombatRounds;
		#format cstatTotalRounds {%g} $cstatTotalRounds;
		#echo <dfb>You have been in combat ${cstatCombatRounds} out of ${cstatTotalRounds} possible rounds (${cstatCombatPercent}%)<088>;
	};
};


#alias {update_eternal_power} {
	#NOP -- In case eternal was turned off, reset it to 1 if the powers screen works;
	#var eternal 1;
	#var found_power 0;
	#format temp {%p} {%1};
	#foreach $eternalList index {
		#regexp {"%1"} {"$eternalList[$index][power]"} {
			#var found_power 1;
			#list eternalList delete $index;
			#list eternalList add {{{key} {$temp} {power} {%1} {level} {%2} {nrg} {%3} {max_cd} {%4} {cur_cd} {%5}}};
		}
	};
	#if !$found_power {
		#list eternalList add {{{key} {$temp} {power} {%1} {level} {%2} {nrg} {%3} {max_cd} {%4} {cur_cd} {%5}}};
	};
	#switch {"$temp"} {
		#case {"energy well"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"heal"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"shred"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"duplicate creature"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"enlarge"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"fry"} {
			update_eternal_delay {$temp} %5 %2;
		};
		#case {"protection"} {
			update_eternal_delay {$temp} %5 %2;
		};
	};
};	

#alias update_eternal_delay {
	#var temp %2;
	#regexp {$temp} {RDY} {#var my[eternal][%1] 1} {
		#var my[eternal][%1] 0;
		#if {%3 > 0} {
			#regexp {$temp} {s} {#replace temp {s} {};#undelay {%1};#delay {%1} {#var my[eternal][%1] 1} {$temp};};
			#regexp {$temp} {m} {#replace temp {m} {};#math temp {$temp * 60};#undelay {%1};#delay {%1} {#var my[eternal][%1] 1} {$temp};};
			#regexp {$temp} {h} {#replace temp {h} {};#math temp {$temp * 60 * 60};#undelay {%1};#delay {%1} {#var my[eternal][%1] 1} {$temp};};
		};
	};
};

#alias _check_eternal_powers {
	#delay check_eternal {(powers} 6;
}

#action {{\s*([0-9]+)\s*/\s*([0-9]+)\s*([A-Za-z0-9_ ]*?):([\r\n\t ]*?)([0-9].+?)([\r\n\t ]*?)([[A-Za-z0-9.]+)([\r\n\t ]*?)([[A-Za-z0-9.]+)}} {
	update_eternal_power {%4} %2 %6 %8 %10
} {2};


#action {A shimmering ball of energy appears magically when you snap your fingers.} {
	#var my[eternal][energy well] 0;
	_check_eternal_powers;
};

#action {In a remarkable feat of cheating, you attack again!} {
	#var my[eternal][quicken] 0;
	#delay 180 {$my[eternal][quicken] 1};
};

#action {^$user_cap bathes you in the healing powers of the outer planes.$} {
	#var my[eternal][heal] 0;
	_check_eternal_powers;
} {2};

#action {^You tear apart %* defenses!$} {
	#var my[eternal][shred] 0;
	_check_eternal_powers;
} {2};

#action {Energy flashes down from the sky, frying %*!} {
	#var my[eternal][fry] 0;
	_check_eternal_powers;
	#var fry_round 1;
} {2};

#action {^You notice that %1 has come into view.} {
	#if {!$idle_flag} {ctell %1 has reset.}
	update_eternal_status;
};

#action {^Encircling %* in a yellowy haze of mystic adrenaline} {
	#var my[eternal][enlarge] 0;
	_check_eternal_powers;
};

#action {^With a quick clap of your hands, %* splits in two!} {
	#var my[eternal][duplicate creature] 0;
	_check_eternal_powers;
};

#class {eternal} {close}