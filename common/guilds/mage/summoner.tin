#class {guild_summoner} kill
#class {guild_summoner} open

#unvar summon_types_list;
#NOP - Phantasmal Killer;
#list summon_types_list add {Something};

#NOP -- Golems;
#list summon_types_list add {Straw golem};
#list summon_types_list add {Rope golem};
#list summon_types_list add {Wood golem};
#list summon_types_list add {Flesh golem};
#list summon_types_list add {Clay golem};
#list summon_types_list add {Stone golem};
#list summon_types_list add {Iron golem};
#list summon_types_list add {Crystal golem};

#NOP -- Monster Summoning 10;
#list summon_types_list add {Celestial lion};
#list summon_types_list add {Undead wraith};
#list summon_types_list add {Shadow panther};

#NOP -- Monster Summoning 9;
#list summon_types_list add {Demon hellhound};
#list summon_types_list add {Fey dryad};
#list summon_types_list add {Beast owlbear};

#NOP -- Monster Summoning 8;
#list summon_types_list add {Storm giant};
#list summon_types_list add {Cloud giant};

#NOP -- Monster Summoning 7;
#list summon_types_list add {10-headed hydra};
#list summon_types_list add {Night Hag};
#list summon_types_list add {Neo-otyugh};
#list summon_types_list add {Roper};
#list summon_types_list add {Mind flayer};
#list summon_types_list add {Fire lizard};
#list summon_types_list add {8-headed pyro-hydra};
#list summon_types_list add {Spectre};
#list summon_types_list add {Mummy};
#list summon_types_list add {Frost giant};
#list summon_types_list add {Fire giant};
#list summon_types_list add {Stone giant};
#list summon_types_list add {Hill giant};
#list summon_types_list add {Ettin};
#list summon_types_list add {Umber hulk};
#list summon_types_list add {Phoenix};
#list summon_types_list add {Gorgon};
#list summon_types_list add {Xorn};
#list summon_types_list add {Chimera};
#list summon_types_list add {Giant slug};

#NOP -- Monster Summoning 6;
#list summon_types_list add {Wyvern};
#list summon_types_list add {Wraith};
#list summon_types_list add {Otyugh};
#list summon_types_list add {Troll};
#list summon_types_list add {Manticore};
#list summon_types_list add {Weretiger};
#list summon_types_list add {Wight};
#list summon_types_list add {Rakshasa};
#list summon_types_list add {Jackalwere};
#list summon_types_list add {Phase spider};
#list summon_types_list add {Carrion crawler};
#list summon_types_list add {Ogre magi};
#list summon_types_list add {Salamander};
#list summon_types_list add {8-headed hydra};

#NOP -- Monster Summoning 5;
#list summon_types_list add {Minotaur};




#gag {  --> %*};
#gag {%*-hp-};

#var haste_icon âš¡;
#var mf_icon ðŸ’ª;
#var tank_icon !!;

#alias _prepSummonString {
	#unvar summon_string;
	#var summon_string Monsters($total_summons): <088>;
	checkStrategy {summon_haste} {#replace summon_string {(%d)} {&0 <ffa>${haste_icon}<088>}};
	checkStrategy {summon_mf} {#replace summon_string {(%d)} {&0 ${mf_icon}<088>}};
};

#alias summon_str {
	#foreach $my[summons][%*] item {
		#format lowerName {%l} $item[keyName];
		#loop 1 $item[count] index {
			cast giant strength on $lowerName $index;
		};
	};
	cast giant strength on $user;
    cast giant strength on golem;
};

#alias _handleSummonString {
	#list my[summons] clear;
	#var my[golem][active] 0;

	#loop 1 &summonList[] index {
		#var summon_haste 0;
		#var summon_mf 0;
		#var summon_tanking 0;
		#regex {$summonList[$index]} {%*:} {
				#var tempName &1;
				#list tempName explode { };
				#format summon_name {%n} {$tempName[&tempName[]]};
				#var keyName $summon_name;
		};
		#regex {$summonList[$index]} {~} {#var summon_tanking 1} {#var summon_tanking 0};
		#regex {$summonList[$index]} {!} {#var summon_haste 1} {
			#var summon_haste 0;
			checkStrategy {summon_haste} {
				#NOP -- We can't haste phantasmal killer or other illusions;
				#if {"$keyName" == "Something"} {#NOP} {#var summon_renew_haste 1}};
		};
		#regex {$summonList[$index]} {mf} {#var summon_mf 1} {
			#var summon_mf 0;
			checkStrategy {summon_mf} {#var summon_renew_mf 1};
			#if {"$keyName" == "Something"} {
				checkStrategy {phantasmal_mf} {cast monstrous fortitude on something}};
		};

		#var found_summon 0;
		#list my[summons] {size} {tempSize};
		#loop 1 $tempSize item {
			#if {"$my[summons][${item}][keyName]" == "$keyName"} {#var found_summon 1;#var itemIndex $item};
		};

		#if {$found_summon} {
			#math summon_type_count {$my[summons][${itemIndex}][count] + 1};
			#var my[summons][${itemIndex}][count] $summon_type_count;
			#var my[summons][${itemIndex}][haste] $summon_haste;
			#var my[summons][${itemIndex}][mf] $summon_mf;
			#if $summon_tanking {#var my[summons][${itemIndex}][tanking] 1};
		} {
			#list my[summons] add {{{keyName} {$keyName} {name} {$summon_name} {haste} {$summon_haste} {mf} {$summon_mf} {count} {1} {tanking} {$summon_tanking}}};
		};
	};

	#var total_summons 0;
	#foreach $my[summons][%*] item {
		#math total_summons {$total_summons + $item[count]};
	};

	_prepSummonString;

	#var mob[phantasmal] 0;
	
	#foreach $my[summons][%*] item {
		#NOP -- If phantasmal killer is in the summon list, we can update the mob status report;
		#if {"$item[keyName]" == "Something"} {#var mob[phantasmal] 1};

		#NOP -- Check if your golem is out;
		#if {"$item[keyName]" == "Golem"} {#var my[golem][active] 1};

		#unvar summonItemName;
		#if $item[mf] {#var summonItemName <eab>};
		#if $item[haste] {#var summonItemName <ffa>};
		#if {$item[count] > 1} {#var tempName ($item[count]) $item[name]} {#var tempName $item[name]};

		#if $item[tanking] {#cat summonItemName {${tank_icon} $tempName ${tank_icon} <088>}} {#cat summonItemName {$tempName<088>}};
		#cat summon_string {$summonItemName, };
	};

	#NOP -- Remove the last 2 chars from summon_string, which will be ', ';
	#format {summon_string_size} {%L} {$summon_string};
	#math summon_string_size {$summon_string_size - 2};
	#format {summon_string} {%.${summon_string_size}s} {$summon_string};
};


#action {  --> %*\n%a-hp-} {
	#list summonList clear;
	#format summon1 {%p} {%1};
	#format summon2 {%p} {%2};
	#var summonList $summon1 $summon2;
	#list summonList explode {, };
	_handleSummonString;
} {1};

#action {  --> %*-hp-} {
	#list summonList clear;
	#format summon1 {%p} {%1};
	#var summonList $summon1;
	#list summonList explode {, };
	_handleSummonString;
} {2};

#alias _summonTankSwap {
	#list summonTankList clear;
	#var summonTankCheck 1;
	look;
	#delay 2 {
		#var summonTankCheck 0;
		#list summonTankList indexate health;
		#list summonTankList order;
		#list summonTankList reverse;
		.summonLookupName $summonTankList[1][name];
		#send {say $summonTmpName $summonTankList[1][index], defend me};
		_summonHealthCull;
	};
};

#alias _summonHealthCull {
	#list summonTankList clear;
	#var summonTankCheck 1;
	look;
	#delay 2 {
		#var summonTankCheck 0;
		#list summonTankList indexate health;
		#list summonTankList refine {&0 < 50};
		#foreach $summonTankList[%*] item {
			.summonLookupName $item[name];
			say $summonTmpName $item[index], begone;
		};
	};
};

#NOP -- Eventually this logic will be collapsed into the -M- action that's going to be standardized;

#NOP -- Ignore your familiar for any mob checks, if you see it in the room set go_fetch to 0;
#action {^-M-_${my[familiar]}.$} {#NOP;#var go_fetch 0} {1};

#alias .summonLookupName {
	#var tmpName %0;
	#switch {"$tmpName"} {
		#case {"A Celestial Lion"} {#var summonTmpName lion;};
		#case {"An Undead Wraith"} {#var summonTmpName wraith;};
		#case {"A Shadow Panther"} {#var summonTmpName panther;};
		#case {"10-headed Hydra"} {#var summonTmpName hydra;};
		#case {"Night Hag"} {#var summonTmpName hag;};
		#case {"Neo-otyugh"} {#var summonTmpName neo-otyugh;};
		#case {"Roper"} {#var summonTmpName roper;};
		#case {"Mind Flayer"} {#var summonTmpName mind flayer;};
		#case {"Fire Lizard"} {#var summonTmpName fire lizard;};
		#case {"8-headed Pyro-hydra"} {#var summonTmpName hydra;};
		#case {"Spectre"} {#var summonTmpName spectre;};
		#case {"Mummy"} {#var summonTmpName mummy;};
		#case {"Frost Giant"} {#var summonTmpName giant;};
		#case {"Fire Giant"} {#var summonTmpName giant;};
		#case {"Stone Giant"} {#var summonTmpName giant;};
		#case {"Hill Giant"} {#var summonTmpName giant;};
		#case {"Ettin"} {#var summonTmpName ettin;};
		#case {"Umber Hulk"} {#var summonTmpName hulk;};
		#case {"Phoenix"} {#var summonTmpName phoenix;};
		#case {"Gorgon"} {#var summonTmpName gorgon;};
		#case {"Xorn"} {#var summonTmpName xorn;};
		#case {"Chimera"} {#var summonTmpName chimera;};
		#case {"Giant Slug"} {#var summonTmpName slug;};
		#case {"A Disfigured Wereboar"} {#var summonTmpName wereboar;};
		#case {"A Giant Spider"} {#var summonTmpName spider;};
		#case {"A Nasty Cockatrice"} {#var summonTmpName cockatrice;};
		#case {"An Enormous Minotaur"} {#var summonTmpName minotaur;};
		#case {"A Swift Leucrotta"} {#var summonTmpName leucrotta;};
		#case {"A Shimmering Black Displacer Beast"} {#var summonTmpName beast;};
		#case {"A Gnarly 7-headed Hydra"} {#var summonTmpName hydra;};
		#case {"A Giant Poisonous Snake"} {#var summonTmpName snake;};
	};
};


#action {^%* says: I can't do this anymore, someone else take over!$} {
	#math mob[summons_killed] {$mob[summons_killed] + 1};
};


#class {guild_summoner} close