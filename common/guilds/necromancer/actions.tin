#class {guild_actions} {kill}
#class {guild_actions} {open}

#list dgraft clear;
#var my[necromancer][repowers] 0;
#var spark_corpse 0;

#NOP --------------------------------------- NECROMANCER SPECIFIC ACTIONS ------------------------------------------;
#action {%*The protective spirits break away from your body, fading into nothingness.$} {#if !$idle_flag {protection}};


#action {You must be in altered form to maim someone %w.} {
    alter hybrid;
    #delay 2 {#if $my[necromancer][altered] {maim}};
} {2};

#NOP --------------------------------------- MOB STATUS TRACKING ACTIONS  ------------------------------------------;
#act {^You charge forward and close your jaws on %*} {#var opponent[effects][rabies] 1} {2};
#act {%* has overcome the effects of your rabies.$} {#var opponent[effects][rabies] 0;rabies;} {2};
#act {^Your blood boils and you call together all of the fleas that reside} {#var opponent[effects][infest] 1} {2};
#act {The creature has been permanently maimed!} {#var mob[maim] 1;#math my[necromancer][maim] {$my[necromancer][maim] - 1}} {2};
#action {>>>The psibleed upon %* has dissipated.} {psibleed};


#unvar moonSparkList;
#unvar planeSparkList;
#var stats[corpses][preserved] 0;
#var stats[corpses][spark-corpse] 0;

#NOP --------------------------------------- NECROMANCER STATUS TRACKING  ------------------------------------------;

#action {You prepare the corpse with your innate, necromantic energies.} {
    #math stats[corpses][preserved] {$stats[corpses][preserved] + 1};
} {2};
#action {A spark of red energy appears out of nowhere and enters the corpse.} {
    #var spark_corpse 1;
    #math stats[corpses][spark-corpse] {$stats[corpses][spark-corpse] + 1};
    #delay .5 {#var spark_corpse 0};
} {2};
#act {In ecstasy, your body contorts into a sleek, beastly form.} {#var my[necromancer][altered] 1};
#act {You are already altered into %* form %w.}{#var my[necromancer][altered] 1};
#act {You return to your normal were form.}{#var my[necromancer][altered] 1};
#act {You alter into the embodimental spirit of the %*!} {#var my[necromancer][altered] 1};
#act {You alter back to your normal, human form.}{#var my[necromancer][altered] 0};
#act {You can only alter your form into beast form %w.}{#var my[necromancer][altered] 0};
#act {You have already skullfired this repower %w.} {#var my[necromancer][skullfire] 0};
#act {^You currently have %d/%d open memory globes,} {#var powers[globes][open] %1;}
#act {Before the repowering, you attempt a last minute %*} {
    #if {!$idle_flag} {
        #var my[necromancer][invoke-prime] 0;
        #var my[necromancer][spirit-howl] 0;
        #var my[necromancer][maim] 0;
        #var my[necromancer][diurnum] 0;
        #var my[necromancer][nocturnum] 0;
        #var my[necromancer][bloodfuse] 0;
        #var my[necromancer][harvest] 0;
        #var my[necromancer][altered] 0;
        #var my[necromancer][bloodarc] 0;
        #var my[necromancer][conjure_undead] 0;
        #var my[necromancer][soulbind] 0;
        #var my[necromancer][souls] 0;
        #var my[necromancer][skullfire] 0;
        #var my[necromancer][unholy_ground] 0;
        #math my[necromancer][repowers] {$my[necromancer][repowers] + 1};
        #send {!repower};
    };
};
#act {The powerful force of your beastly nature wells a great power within and} {#var my[necromancer][spirit-howl] 0};

#NOP -- Update what repower powers are available;
#action {Your repower-restricted abilities are listed below %w...} {
    #action {'Soulbound' essences  - You have %w {souls|soul} bound with your own} {
        #var my[necromancer][souls] @word_to_number{%%1};
    } {2};
    #action {-> %* - %*} {
        #var tmpPower %%1;
        #var tmpReset %%2;
        #replace tmpPower {(%*)} {};
        #format tmpPower {%p} {$tmpPower};
        #format tmpReset {%p} {$tmpReset};

        #if {"$tmpReset" == "Must wait for repower%*"} {
            #var tmpReset 0;
        } {
            #regex {$tmpReset} {Available for use (%d)} {#var tmpReset &1} {#var tmpReset 1};
        };
        
        #switch {"$tmpPower"} {
            #case {"Conjure Undead"} {
                #var my[necromancer][conjure_undead] $tmpReset;
            };
            #case {"Invoke Prime"} {
                #var my[necromancer][invoke-prime] $tmpReset;
            };
            #case {"Spirithowl"} {
                #var my[necromancer][spirit-howl] $tmpReset;
            };
            #case {"Maim"} {
                #var my[necromancer][maim] $tmpReset;
            };
            #case {"Diurnum/Nocturnum/Prime"} {
                #var my[necromancer][diurnum] $tmpReset;
                #var my[necromancer][nocturnum] $tmpReset;
            };
            #case {"Bloodfuse"} {
                #var my[necromancer][bloodfuse] $tmpReset;
            };
            #case {"Grim Harvest"} {
                #var my[necromancer][harvest] $tmpReset;
            };
            #case {"Bloodarc"} {
                #var my[necromancer][bloodarc] $tmpReset;
            };
            #case {"Soulbind"} {
                #var my[necromancer][soulbind] $tmpReset;
            };
            #case {"Skullfire"} {
                #var my[necromancer][skullfire] $tmpReset;
            };
            #case {"Unholy Ground"} {
                #var my[necromancer][unholy_ground] $tmpReset;
            };
        };
    } {2};
    #delay 1 {
        #unaction {-> %* - %*};
        #unaction {'Soulbound' essences  - You have %w souls bound with your own};
    };
} {2};

#NOP -- General Necro Power Tracking;
#action {You scream with the power of the netherworld and thrust your hands toward} {
    stat_record;
    #var my[necromancer][unholy_ground] 0;
} {2};

#action {  You must wait until repower to rend flesh again %w.} {
    #var my[necromancer][maim] 0;
} {2};

#NOP -- Tracking Reaper abilities (grim harvest, soulbind, etc.);
#action {^The celestial blade slashes cleanly through %*} {
    #var my[necromancer][harvest] 0;
} {2};

#action {Raising your hands to the sky, you call forth the spirits of the underworld} {
    #var my[necromancer][soulbind] 0;
} {2};

#action {You merge your thoughts with the guild to see within your soul...} {
    #NOP - In the future build something to stock divine souls;
} {2};

#action {^The soul of %* dissipates into a cloud of ghostly mist.} {
    #math my[necromancer][souls] {$my[necromancer][souls] - 1};
} {2};

#action {In a bright crimson flash, the soulbind is broken!} {
    #send {!repower};
} {2}

#NOP -- Tracking Lich abilities (bloodarc/conjure undead);
#action {^Your eyes burn with an unholy crimson flame.$} {
    #if {$bot[active]} {.pause;#delay 12 {#var bot[paused] 0}};
    #var mob[bloodarc] 0;
    #math my[necromancer][bloodarc] {$my[necromancer][bloodarc] - 1};
} {2};

#action {^The lights in the room waver and swoon.$} {
    #math my[necromancer][conjure_undead] {$my[necromancer][conjure_undead] - 1};
} {2}

#action {The bloodarc crackles quietly as it dissipates into the ground.} {
    #if $bot[active] {#var bot[paused] 0};
} {2};

#action {The energies of the bloodarc crackle quietly as they dissipate into the air.} {
    #if $bot[active] {#var bot[paused] 0};
} {2};

#NOP -- Tracking Vampire abilities (bloodlet/bloodfuse potions);
#action {A small, very valuable crystal decanter} {
    #if $inventory_check {#math my[necromancer][decanters][empty] {$my[necromancer][decanters][empty] + 1}};
} {2};

#action {A small, blood-filled crystal decanter} {
    #if $inventory_check {#math my[necromancer][decanters][full] {$my[necromancer][decanters][full] + 1}};
} {2};

#action {A blood-filled crystal decanter \(glowing\)} {
    #if $inventory_check {#math my[necromancer][decanters][powered] {$my[necromancer][decanters][powered] + 1}};
} {2};

#action {^You drink the glowing blood, then hurl the decanter to the ground.} {
    #math my[necromancer][decanters][powered] {$my[necromancer][decanters][powered] - 1};
    @max{#math my[necromancer][bloodfuse] {$my[necromancer][bloodfuse] - 1};0};
} {2};


#NOP -- When using bloodlet, put the decanter in chest;
#action {You well up your vampiric strength and your eyes burn brightly with} {
    #math my[necromancer][decanters][full] {$my[necromancer][decanters][full] + 1};
    #math my[necromancer][decanters][empty] {$my[necromancer][decanters][empty] - 1};
    qtrance unkeep decanter/retrieve/open chest/put decanter in chest/deliver;
} {2};

#action {  That decanter already has blood in it %w.} {
    qtrance unkeep decanter/retrieve/open chest/put decanter in chest/deliver;
    checkStrategy {bloodlet} {bloodlet $enemy[target]};
} {2};


#NOP ----- The moon has x stages;
#NOP A new moon hangs in the dark sky.;
#NOP A waxing crescent moon hangs in the dark sky.;
#NOP A half moon hangs in the dark sky.  128 secs;
#NOP A waxing gibbous moon hangs in the dark sky. 278 secs;
#NOP A full moon hangs in the dark sky. 428 secs;
#NOP A waning gibbous moon hangs in the dark sky. 578 secs;
#NOP A half moon hangs in the dark sky. 728 secs;
#act {A waning crescent moon hangs in the dark sky.} {#var my[necromancer][moon]};

#NOP --------------------------------------- NECROMANCER SUPPORT ACTIONS  ------------------------------------------;
#ACTION {^%w gives you a low-five!} {#if !$idle_flag {#format {lower} {%l} {%1};daemon graft $lower;#list dgraft add $lower}} {6};
#ACTION {^%w goes 'Breakin the law! Breakin the law!' at you.} {#if !$idle_flag {qtrance preserve remains>50;qtrance preserve remains>50}} {6};
#ACTION {You see a shadowy form escape from %w and disappear!} {#SHOWME DAEMON GRAFT LEFT %1} {2};


#NOP -- Teleport helpers;
#action {Visions of the guild appear first in your mind, then reality...} {mapgo necro_rr} {2};

#NOP -- Glamor maintenance;
#NOP -- fury;
#action {You reach into your animal soul and fly into a fury!} {
    #math my[necromancer][glamors][current] {$my[necromancer][glamors][current] - 1}
};


#NOP ---NECRO TIER MSGS
#action {As you ponder the runes, they swim before your eyes.$} {
    #echo {<feb>1 The path before you is a long and hard one name here<088>};
    #echo {<dfb>2 The path before you is a long one, barely have you begin your quest<088>};
    #echo {<fcb>3 A long road remains before you in the quest for ascension<088>};
    #echo {<ffa>4 A portion complete, your quest still awaits<088>};
    #echo {<acf>5 More remains of your path than what you have seen thus far<088>};
    #echo {<caf>6 The end of your journey is not yet in sight<088>};
    #echo {<fac>7 The road before you is long, but shorter than that behind you<088>};
    #echo {<afc>8 The end draws near, you sense this in your soul<088>};
    #echo {<cfa>9 Your goal draws closer than you realize<088>};
    #echo {<fca>10 There remains but only a few steps in your quest<088>};
    #echo {<feb>11 You are ready to ascend in the ranks<088>}
} {2};

#class {guild_actions} {close}