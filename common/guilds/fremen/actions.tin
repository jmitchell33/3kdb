#class {guild_actions} {kill}
#class {guild_actions} {open}

#NOP -- action toggle maintenance;
#NOP -- voice hold;
#action {%* recoils from your voice and stops attacking!} {
    #if {!$idle_flag} {
        #var toggles[fremen][hold] 0;
        #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: voice hold hit - hold toggle set to 0:<088> $toggles[fremen][hold]}};
    };
} {2};

#action {recovers from the Voice!$} {
    #if {!idle_flag} {
        #var toggles[fremen][hold] 1;
        #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: voice hold recovered - hold toggle set to 1:<088> $toggles[fremen][hold]}};
    };
} {2};

#NOP -- stun;
#action {cannot be stunned} {
    #if {!$idle_flag} {
        #var toggles[fremen][stun] 0;
        #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: stun already active - stun toggle set to 0:<088> $toggles[fremen][stun]}};
    };
} {2};

#NOP -- voice enfeeblement;
#action {recoils from your} {
    #if {!$idle_flag} {
        #var toggles[fremen][enfeeblement] 0;
        #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: enfeeblement hit - enfeeblement toggle set to 0:<088> $toggles[fremen][enfeeblement]}};
    };
} {2};

#action {recovers from the Voice and seems mentally stronger!$} {
    #if {!$idle_flag} {
        #var toggles[fremen][enfeeblement] 1;
        #if {$debug[fremen][toggle]} {#echo {<ffa>debug toggles: enfeeblement recovered - enfeeblement toggle set to 1:<088> $toggles[fremen][enfeeblement]}};
    };
} {2};

#NOP -- fremen skills;
#action {You are in the yali of the Reverend Mother Jessica} {
    #if {!$idle_flag} {
        #echo {<caf>Trainable skills: <fff>litany, pain, prescience, voice, weirding<088>};
    };
} {2};

#action {This is the yali of the legendary Otheym} {
    #if {!$idle_flag} {
        #echo {<caf>Trainable skills: <fff>ambush, assault, chaumary, crysknife, desert hardening, devices, hide, projectile<088>};
    };
} {2};

#action {Inside this fremen yali} {
    #if {!$idle_flag} {
        #echo {<caf>Trainable skills: <fff>bindu, fiqh, ilm, nerve, prana<088>};
    };
} {2};

#action {You stand before a vast lake of water deep under the sietch.} {
    #if {!$idle_flag} {
        #echo {<caf>Trainable skills: <fff>fai, huanui, water discipline<088>};
    };
} {2};

#NOP -- spice;
#var spiceBeers 0;
#action {You pay %d gold coins for spice beer.} {
    #if {!$idle_flag} {
        #math {spiceBeers} {$spiceBeers + 1};
    };
} {2};

#action {^You drink the spice beer.} {
    #if {!$idle_flag} {
        #var spicebeertoggle 0;
        #if {"$spiceBeers" != "0"} {
            #math {spiceBeers} {$spiceBeers - 1};
        };
    };
} {2};

#action {^The effects of the spice wear off.} {
    #if {!$idle_flag} {
        #if {$spiceBeers > 0} {
            #var spicebeertoggle 1;
        };  #else {
            #var spicebeertoggle 0;
        };
    };
} {2};

#NOP -- Sand;
#action {^You throw some sand in %* eyes.$} {
    #var mob[sand] 1;
} {2};

#action {Grimy Beast is already blinded.$} {
    #var mob[sand] 1;
} {2};

#action {%* wipes some sand from its eyes.$} {
    #var mob[sand] 0;
} {2};


#NOP -- suspsend;
#var suspend_toggle 1;
#action {You close your eyes and focus on your breathing. Every nerve in your body} {
    #if {!$idle_flag} {
        #var suspend_toggle 0;
    };
} {2};

#action {You shock your body out of its catalepsis and awaken to this world.} {
    #if {!$idle_flag} {
        #var suspend_toggle 1;
    };
} {2};

#action {Having regained your mental focus completely, you emerge from the Bindu} {
    #if {!$idle_flag} {
        #var suspend_toggle 1;
    };
} {2};

#action {^Literjons attached to belt:} {
    #line oneshot #action {Max:%* Contains:%*} {
        #format my[literjons][max] {%d} {%%1};
        #format my[literjons][carrying] {%d} {%%2};
    } {1};
    #line oneshot #action {Filled:%* Drying:%*} {
        #format my[literjons][filled] {%d} {%%1};
        #format my[literjons][drying] {%d} {%%2};
    } {1};
    #line oneshot #action {Empty:%*} {
        #format my[literjons][empty] {%d} {%%1};
    } {1};
} {2};


#action {^You carefully fill a literjon.} {
    #NOP #math my[literjons][filled] {$my[literjons][filled] + 1};
    #math my[literjons][empty] {$my[literjons][empty] - 1};
} {2};

#action {^You sip the water carefully from a literjon.} {
    #NOP #math my[literjons][filled] {$my[literjons][filled] - 1};
    #math my[literjons][empty] {$my[literjons][empty] + 1};
    #math my[literjons][drying] {$my[literjons][drying] + 1};
} {2};

#action {^You have no empty literjons.} {
    #math my[literjons][empty] {$my[literjons][empty] + 0};
} {2};

#action {^You carefully place your literjons on the rack.} {
    #var my[literjons][empty] $my[literjons][carrying];
    #var my[literjons][filled] 0
} {2};




#alias gxp-stats {
    #local tmpItem {};
    #local tmpTotal 0;
    #echo {%-15s   %-30s  %+15s} {<dfc>-++- -++- -++- -++-<088>} {     <fbc>FREMEN GXP SUMMARY<088>} {<dfc>-++- -++- -++- -++- <088>};
    #echo {<cfd>%-30s   %-6s    %-6s   %-6s<088>} {  Category} {Count} {% of Total} {Avg. Gxp};
    #echo {%-15s} {    <ebc>Agony<088>};
    #foreach *stats[agony][] tmpItem {
        #math tmpTotal {$tmpTotal + $stats[agony][${tmpItem}][count]};
    };
    #foreach *stats[agony][] tmpItem {
        #if {"$tmpItem" == ""} {#NOP} {
            #local tmpAvg 0;
            #local tmpPercent 0.00;
            #math tmpAvg {$stats[agony][${tmpItem}][gxp] / $stats[agony][${tmpItem}][count]};
            #math tmpPercent {$stats[agony][${tmpItem}][count] / $tmpTotal * 100.00};
            #format tmpPercent {%d} {$tmpPercent};
            #echo {%-30s   %-6s    %-10s   %-6s} {      <edd>${tmpItem}<088>} {<eee>$stats[agony][${tmpItem}][count]} {    ${tmpPercent}%} {    $tmpAvg<088>};
        };
    };
    #echo {%-15s   %-30s  %+15s} {<dfc>-++- -++- -++- -++-<088>} {<fbc><088>} {<dfc>-++- -++- -++- -++- <088>};

};

#alias .trackPowerGxp {
    #local tmpCategory %1;
    #local tmpItem %2;
    #local tmpNumber 0;
    #if {$stats[$tmpCategory][$tmpItem][count] > 0} {
        #math stats[$tmpCategory][$tmpItem][count] {$stats[$tmpCategory][$tmpItem][count] + 1};
    } {
        #var stats[$tmpCategory][$tmpItem][count] 1;
    };
    #math stats[$tmpCategory][$tmpItem][gxp] {$stats[$tmpCategory][$tmpItem][gxp] + $my[gxp][last_round]};
};

#NOP -- Tracker for gxp/powers;
#action {You are too exhausted to inflict agony.} {
    .trackPowerGxp agony too-exhausted;
} {2};

#action {You probe %* mind ineffectually.} {
    .trackPowerGxp agony ineffectually;
} {2};

#action {You inflict pain on %*.} {
    .trackPowerGxp agony inflict-pain;
} {2};

#action {Though tired, you cause some pain in %* mind.} {
    .trackPowerGxp agony tired-some;
} {2};

#action {You find %* mind open to you as you cause %* some pain.} {
    .trackPowerGxp agony mind-open;
} {2};

#action {You invade %* mind, causing %* pain.} {
    .trackPowerGxp agony invade;
} {2};

#action {You cause %* to have a momentary seizure!} {
    .trackPowerGxp agony seizure-momentary;
} {2};

#action {Your mind powers cause %* to have a seizure.} {
    .trackPowerGxp agony seizure;
} {2};

#action {You set off %* nerves in a dreadful seizure.} {
    .trackPowerGxp agony seizure-dreadful;
} {2};   

#action {You project your mind into %*, causing %* agony.} {
    .trackPowerGxp agony agony;
} {2};

#action {You crumble the barrier between your minds and overwhelm %*.} {
    .trackPowerGxp agony overwhelm;
} {2};

#action {You tear into %* mind, causing intense agony!} {
    .trackPowerGxp agony intense;
} {2};


#class {guild_actions} {close}