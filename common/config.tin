#class common_config kill;
#class common_config open;

#SESSION {3k - $user} {3k.org} {3000};

#config {packet patch} 1;
#config {color} on;
#config {repeat enter} on;
#config log plain;
#config connect_retry 60;

#info env save;
#info system save;

#var cwd $info[SYSTEM][CWD];
#format user_cap {%n} $user;

#NOP -- Ticker to set incombat variable;
#ticker check_combat {
    #if {$mip[round] == $incombat_last_round || $mip[round] == 0} {
        #var incombat 0
    } {
        #var incombat 1;
    };
    #var incombat_last_round $mip[round];
} {4};

#NOP -- In general...if you're idle >4 minutes gxp cuts off;
#NOP -- so this ticker will send a "non-command" to the mud if you're in combat and not deadman;
#ticker {noise} {#if {$incombat && !$idle_flag} {#cr}} {240};

#NOP -- Load variables from last session and reset all the things;
#alias {defaults_on} {
    #echo {<fff> CONNECTED TO 3KDB -- UPDATING SCREEN IN 3 seconds...};
    _variable_import;
	mipon;
    #read common/draw.tin;
    loadmap;
    postload;
	i;
    examine cooler;
    touch diamond rune;
    #send {(powers};
    .update_gags;
    init_screen;
    draw_all;
    _mob_status_clear;
    reset_trackers;
    #NOP -- Gag the few things this checks if it's not there for spam;
    #line oneshot #gag {^That is not here or it is uninteresting. (cooler?)};
    #line oneshot #gag {^You find nothing of interest.};
    #line oneshot #gag {^Perform touch at whom?};
    #line oneshot #gag {^There is no reason to '(powers' here.};
    #line oneshot #gag {^There is no reason to 'spellDmg-clear' here.};
    #line oneshot #gag {^There is no reason to 'summonDmg-clear' here.};
};

#alias {reset_trackers} {
    istats_reset;
    #if $vaf[numbers] {dreset};
    #if {"$guild" == "juggernaut"} {
        mountDmg-clear;
        truckDmg-clear;
        supportDmg-clear;
    };
    #if {"$guild" == "mage"} {
        spellDmg-clear;
        summonDmg-clear;
    };
};

#ALIAS {postload}
{
     #map flag static on;
     #map flag vtmap on;
     #map flag unicodegraphics on;
     #map flag terrain on;
     #map flag direction on;
     #delay {1} {hp};
     #delay {4} {who2};
     #delay {6} {_area_speedruns;_area_bots;3kdb_banner};
};

#alias {load_guild} {
    #class guild_%* kill;
    #read common/guilds/${guild}/index.tin;
};

#alias setup_3kdb {
    #echo {Type 'setup_3kdb_hpbar' to configure your guild hpbar.  If this alias exists, it will setup your guild hpbar to provide information to 3kdb.};
        #echo {If it doesn't exist, please contact your local 3kdb dealer.};
    forcehp;
    notify on;
    init_screen;
    #send {aset look_monster_pref -M-_};
	#send {aset look_player -P-_};
	#send {aset room_short_pref -R-_};
	#send {aset room_short_suff -R-_};
	#send {aset room_exits_pref -X-_};
	#send {aset room_exits_suff -X-_};
	#send {aset room_long_pref -D-_};
	#send {aset room_long_suff -D-_};
	#send {aset look_weapon -w-};
	#send {aset look_object -o-};
	#send {aset look_armor -a-};
	#send {aset look_armour -a-};
	#send {aset look_other -i-};
    #send {aset say_pref hicyan};
	#send {prompt >$nl$};
    #send {aset world_drop_pref -!!!-};
    #line oneshot #action {^You will no longer see the HP bar on the main screen.$} {forcehp};
    #line oneshot #action {^Notify off for all.$} {notify on};
    mka-on;
    mka-clear;
	#echo {Setup successful!};
};

#NOP -- Welcome banner with info and updates;
#alias 3kdb_banner {
    #unvar db3k[banner];
    #list db3k[banner] add {<dfc>   ---------   WELCOME TO 3kdb   ---------<088>  };
    #list db3k[banner] add {<ddd>      If this is your first time, type <ffd>setup_3kdb<088>};
    #list db3k[banner] add {<ddd>      <ffd>library3kdb<088> to see a list of aliases <088>};
    #list db3k[banner] add {<ddd>      <ffd>mapgo cot<088> to set your position to Center of Town (cot) <088>};
    #list db3k[banner] add {<ddd>       Be sure to check out <ffd>botlist<ddd>, <ffd>speedruns<ddd>, and <ffd>strategies<088>};
    #draw $theme[chatbox] lalign ralign left right side 2 1 $chat[height]-1 $chat[right] $db3k[banner][1..5];
};

#NOP -- Get online players and monitor as they fall off/on with notify;
#alias who2 {
    #send {who2};
    #var players {};
    #class tmp open;
    #unvar tmpList;
    #action {^%+1..d%s %*} {
        #cat tmpList {%%3};
        #replace tmpList {A ghostly form} {};
    };
    #class tmp close;
    #line oneshot #action {This list is sorted by level, type: who2 <xp/age>} {
        #list tmpList explode { };
        #list tmpList filter {} {%d};
        #list tmpList filter {} {%w@3s};
        #var players $tmpList;
        #unvar tmpList;
        #class tmp kill;
        #if {&players > 0} {#echo {<ffd>3kdb -- Successfully updated players list with &players[] players added.}};
    };
};

#NOP -- Add players to online tracking;
#action {^[%w reconnects]$} {
    #list players add {%1};
} {2};
#action {^[%w enters the game]$} {
    #list players add {%1};
} {2};

#NOP -- Remove players from online tracking;
#action {^[%w disconnects]$} {
    #unvar tmpItem;
    #if {&players[] > 0} {
        #list players find %1 tmpItem;
        #if {$tmpItem} {#list players delete $tmpItem};
    };
} {2};
#action {^[%w leaves the game]$} {
    #unvar tmpItem;
    #if {&players > 0} {
        #list players find %1 tmpItem;
        #if {$tmpItem} {#list players delete $tmpItem};
    };
} {2};


#NOP -- Alias to load common/guilds/character-specific files;
#alias {_load_3kdb_common} {
    #NOP -- Load previous sessions variables;
    #read chars/$user/vars.tin;

    #NOP -- Load Common;
    #read common/bot/bots.tin;
    #read modules/helpers/helpers.tin;
    #read modules/corpsemanager/corpsemanager.tin;

    #NOP -- Load Guilds;
    #read common/guilds/$guild/index.tin;
    #read common/guilds/eternal/eternal.tin;

    #NOP -- Load Character specific;
    #read chars/$user/actions.tin;
    #read chars/$user/aliases.tin;
    #read chars/$user/config.tin;
    #read chars/$user/heartbeat.tin;
    #read chars/$user/miphp.tin;
    #read chars/$user/private.tin;
    #read chars/$user/tickers.tin;

    #NOP -- Load Profession;
    _load_profession;
};

#NOP -- Choose which profession to load;
#alias {_load_profession} {
    #class tmp_profession open;
    #action {Profession #1 : Transmuter%s\(Level %d\)%*} {
        #read modules/professions/transmuter.tin;
        #var transmuter[level] %%2;
    } {1};

    #action {Profession #1 : Reforger%s\(Level %d\)%*} {
        #read modules/professions/reforger.tin;
        #var reforger[level] %%2;
    } {1};

    #action {Profession #1 : Golem Master%s\(Level %d\)%*} {
        #read modules/professions/golem_master.tin;
        #var golem_master[level] %%2;
    } {1};

    #action {Profession #1 : Marshal%s\(Level %d\)%s\[%*\]} {
        #read modules/professions/marshal.tin;
        #var marshal[level] %%2;
    } {1};
    #class tmp_profession close;

    #send {profs};
    #delay 3 {#class tmp_profession kill};
};

#NOP -- Auto defaults_on - the primary reason for turning this off is to debug things since the mip;
#NOP -- scripts are so spammy;
#var sessionActive 0;
#action {^3Kingdoms welcomes you!} {
    #var sessionActive 1;
    _load_3kdb_common;
    defaults_on;
    #delay 1 {#map goto 6220};
} {1};
#action {^3Kingdoms welcomes you back from linkdeath.} {
    #var sessionActive 1;
    _load_3kdb_common;
    defaults_on;
    look;
    #delay 1 {
        #if {"$room[name]" == "Entrance Building to Pinnacle (jump)"} {
            #map goto 6220;
        };
    };
} {1};

#NOP -- If numbers are detected, read in the damage tracker;
#act {{^You hit (.*) (\d+) (time|times) for (\d+) damage\.$}} {
    #unact {{^You hit (.*) (\d+) (time|times) for (\d+) damage\.$}};
    #var vaf[numbers] 1;
    #read modules/dmgtracker/dmgtracker.tin;
    #if {"$guild" == "mage"} {
        #read modules/dmgtracker/dmgtracker_mage.tin;
    };
    #if {"$guild" == "juggernaut"} {
        #read modules/dmgtracker/dmgtracker_jugger.tin;
    };
    istats_reset;
} {2};


#class common_config close;