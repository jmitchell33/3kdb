#class {item_tracker} {kill};
#class {item_tracker} {open};

#NOP -- This is a generic tracker for any item that also hooks into the item gag;
#NOP -- Soon to be added reports, but for the moment you can inspect the variable and also gag;

#NOP -- Inputs are %1 item/keyname %2 category;
#alias .trackItem {
    #if $gags[combat_items] {#line gag};
    #math itemTracker[%1][%2] {$itemTracker[%1][%2] + 1};
};

#var itemTracker[start_round] $total_rounds;

#alias istats_reset {
    #unvar itemTracker;
    #var itemTracker[start_round] $total_rounds;
};

#alias istats {
    #math tmpRounds {$total_rounds - $itemTracker[start_round]};
    #format fmtTmpRounds {%g} {$tmpRounds};
    #echo {<efe>   ----    ITEM TRACKER EXTRAORDINAIRE   ----    <088>};
    #format fmtTmpHeader { %-15s     %-10s  %-10s} {Item} {Count} {% of Rnds};
    #echo {<efe>$fmtTmpHeader<088>};
    #foreach *itemTracker[] tmpKey {
        #if {"$tmpKey" == "start_round"} {#NOP} {
            #format fmtTmpKey {%n} {$tmpKey};
            #format fmtTmpKey {%-15s} {$fmtTmpKey};
            #echo {<efb>  $fmtTmpKey<088>};
            #foreach *itemTracker[$tmpKey][] tmpSubItem {
                #format fmtTmpKey {%n} {$tmpSubItem};
                #format fmtTmpCnt {%g} {$itemTracker[$tmpKey][$tmpSubItem]};
                #math tmpSubPct {$itemTracker[$tmpKey][$tmpSubItem] / $tmpRounds * 100.0};
                #format fmtTmpItem {    %-15s   %-10s  %-10s} {$fmtTmpKey} {$fmtTmpCnt} {${tmpSubPct}%};
                #echo {<bdf>$fmtTmpItem <088>};
            };
        };
    };
    #echo {<efe>   ----    TRACKING OVER $fmtTmpRounds ROUNDS   ----    <088>};
};

#NOP -- Zodiac Shield;
#action {^The shield's etchings glow, a brief wave of exhaustion washes over you.$} {
    .trackItem {Feline Shield} {bash};
} {2};

#NOP -- Helmet of Aeonos;
#action {The divine power of Aeonos flows through your helmet.} {
    .trackItem {Helmet of Aeonos} {heal};
} {2};

#action {Warm yellow energy restores your life and refreshes your psyche.} {
    .trackItem {Helmet of Aeonos} {sp heal};
} {2};

#action {The moonstones on your helmet glow softly} {
    .trackItem {Helmet of Aeonos} {dodge};
} {2};

#action {^The sunstones embedded in your helmet flash with warmth, momentarily} {
    .trackItem {Helmet of Aeonos} {shield};
} {2};

#action {^The sapphire on your helmet glows brightly, and you feel a surge of} {
    .trackItem {Helmet of Aeonos} {dodge};
} {2};

#action {^A shimmering light from the diamonds on your helmet surrounds you} {
    .trackItem {Helmet of Aeonos} {dodge};
} {2};

#NOP -- Ring of Magical Deflection (Rimalkin King);
#action {^Your hand glows softly, and the attack dissipates!$} {
    .trackItem {Ring of Deflection} {deflect};
} {2};

#NOP -- Rune Sword;
#action {^%* wound causes %* to shudder in pain. \{%d\}$} {
    #var cmbt[affects][rune_sword_green_rune] 1;
    .trackItem {rune sword} {green rune};
} {2};

#action {^Bolts of blue electricity leap from the blade and strike %*! \{%d\}$} {
    #var cmbt[affects][rune_sword_blue_rune] 1;
    .trackItem {rune sword} {blue rune};
} {2};

#action {^The runesword emits a yellow glow as it causes you to strike again!$} {
    .trackItem {rune sword} {yellow rune};
} {2};

#action {^The black rune flares and enshrouds %* with dark energy!$} {
    .trackItem {rune sword} {black rune};
} {2};


#NOP -- Witch hat;
#action {A bolt of electricity streams from your hat into %*!$} {
    #var cmbt[affects][witchHat] 1;
    .trackItem {witch hat} dmg;
} {2};

#NOP -- Spirit Bracers;
#action {The bracers flare, and the attack misses you!} {
    .trackItem {spirit bracers} block;
} {2};

#NOP -- Charm Bracelet;
#action {^Your charm bracelet grants you an extra attack!$} {
    .trackItem {charm bracelet} xtra-attack;
} {2};

#action {^Your charm bracelet brightens.$} {
    .trackItem {charm bracelet} dodge;
    blockSelfTrack item {charm bracelet};
} {2};

#NOP -- Alien Queen's Skin;
#action {Acid from the Queen's skin sizzles %*!} {
    .trackItem {alien skin} {acid dmg};
} {2};

#NOP -- CMC;
#action {A flicker in the containment field surrounding the CMC's plasma tank} {
    #line oneshot #action {results in a plasma leak within your suit, causing you great pain.} {#line gag} {1};
    .trackItem cmc leak;
} {2};

#action {The armour infuses you with chemicals, augmenting your abilities.} {
    .trackItem cmc stats;
} {2}; 

#action {The CMC-660 infuses you with adrenaline!} {
    .trackItem cmc heal;
} {2};

#action {^A flash of light from the CMC-660 fills everyone in your party with invigorating warmth!$} {
    .trackItem cmc {party heal};
} {1};

#action {The CMC-660 REFLECTS the entire attack!!} {
    .trackItem cmc reflect;
    blockSelfTrack item CMC;
    #var cmbt[affects][CMC] 1;
} {2};

#action {^The CMC-660 blocks the entire attack!!$} {
    blockSelfTrack item CMC;
    .trackItem cmc block;
} {2};

#NOP -- Rolm;
#action {^The healing power of the phoenix restores your mind and body.$} {
    .trackItem rolm {phoenix heal};
} {2};

#action {^A %* spirit of %* pushes forth your attack.$} {
    .trackItem rolm {angel attack};
} {2};

#action {^Renewed by the healing energy, your mind is calmed and body strengthened.$} {
    .trackItem rolm {phoenix heal};
} {2};

#action {^The %* siphons energy from your enemy, passing it along to you.$} {
    .trackItem rolm {skull siphon};
} {2};

#action {^The aegis rune on the PsiMace pulses for a moment.$} {
    blockSelfTrack item rolm;
    .trackItem rolm {aegis block};
} {2};


#NOP -- Banu Cloak;
#action {Your cloak causes your form to phase, reducing the impact from the attack.} {
    .trackItem {banu cloak} DR;
} {2};

#action {^Your cloak causes your form to blink out, avoiding the incoming attack.$} {
    .trackItem {banu cloak} Dodge;
} {2};

#NOP -- Regalia of the Faerie Queen;
#action {^You're briefly blinded by your armor absorbing the incoming damage.$} {
    blockSelfTrack item regalia;
    .trackItem FaerieReg DR;
} {2};

#NOP -- Vblade;
#action {The sword flashes brightly, absorbing the force of the attack!} {
    .trackItem vblade DR;
} {2};

#NOP -- Pet Rock;
#action {Your pet rock takes control and jerks your arm to deflect the attack.} {
    .trackItem {pet rock} deflect;
} {2};

#NOP -- Cocoon;
#action {The sticky cocoon absorbs part of the attack!} {
    .trackItem {sticky cocoon} DR;
} {2};

#NOP -- Guyahi's amulet;
#action {The amulet's protective barrier reduces the incoming attack.} {
    .trackItem {guyahi's amulet} DR;
} {2};

#action {The amulet's protective barrier dissipates as it reduces the incoming} {
    .trackItem {guyahi's amulet} DR;
} {2};

#action {The amulet opens and surrounds you with rejuvenating energies.} {
    .trackItem {guyahi's amulet} heal;
} {2};

#action {The amulet opens and shoots a magical force toward$} {
    .trackItem {guyahi's amulet} heal;
} {2};

#action {The amulet opens and floods your mind with ancient teachings.} {
    .trackItem {guyahi's amulet} stats;
} {2};

#NOP -- Zelligar's headband;
#action {Zelligar's Headband warms you with a radiant glow.} {
    .trackItem {zelligar's headband} heal;
} {2};

#action {%* stumbles in confusion as the headband hypnotizes %*!} {
    .trackItem {zelligar's headband} {stun};
} {2};

#NOP -- Charm bracelet;
#action {Your charm bracelet brightens, and you evade the edged attack.} {
    .trackItem {charm bracelet} evade;
} {2};

#action {Your charm bracelet grants you an extra attack!} {
    .trackItem {charm bracelet} {extra attack};
} {2};

#NOP -- Adamantite bracers;
#action {The bracers flare brightly and your form wavers slightly!} {
    .trackItem {adamantite bracers} DR;
} {2};

#action {through you, fueling your life force rather than draining!} {
    .trackItem {adamantite bracers} {hp heal};
} {2};


#class {item_tracker} {close};