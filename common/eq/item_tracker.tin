#class item_tracker kill;
#class item_tracker open;

#NOP -- This is a generic tracker for any item that also hooks into the item gag;
#NOP -- Soon to be added reports, but for the moment you can inspect the variable and also gag;

#NOP -- Inputs are %1 item/keyname %2 category
#alias .trackItem {
    #if $gags[combat_items] {#line gag};
    #math itemTracker[%1][%2] {$itemTracker[%1][%2] + 1};
};

#var itemTracker[start_round] $total_rounds;

#alias iStats-reset {
    #unvar itemTracker;
    #var itemTracker[start_round] $total_rounds;
};

#alias iStats {
    #math tmpRounds {$total_rounds - $itemTracker[start_round]};
    #format fmtTmpRounds {%g} {$tmpRounds};
    #echo {<efe>   ----    ITEM TRACKER EXTRAORDINAIRE   ----    <088>};
    #format fmtTmpHeader { %-15s     %-10s  %-10s} {Item} {Count} {% of Rnds};
    #echo {<efe>$fmtTmpHeader<088>};
    #foreach *itemTracker[] tmpKey {
        #if {"$tmpKey" == "start_round"} {#NOP} {
            #format fmtTmpKey {%n} {$tmpKey};
            #format fmtTmpKey {%-15s} {$fmtTmpKey};
            #echo {<efb>  $fmtTmpKey<088>};
            #foreach *itemTracker[$tmpKey][] tmpSubItem {
                #format fmtTmpKey {%n} {$tmpSubItem};
                #format fmtTmpCnt {%g} {$itemTracker[$tmpKey][$tmpSubItem]};
                #math tmpSubPct {$itemTracker[$tmpKey][$tmpSubItem] / $tmpRounds * 100.0};
                #format fmtTmpItem {    %-15s   %-10s  %-10s} {$fmtTmpKey} {$fmtTmpCnt} {${tmpSubPct}%};
                #echo {<bdf>$fmtTmpItem <088>};
            };
        };
    };
    #echo {<efe>   ----    TRACKING OVER $fmtTmpRounds ROUNDS   ----    <088>};
};

#NOP -- Witch hat;
#action {A bolt of electricity streams from your hat into %*!} {
    .trackItem witch-hat dmg;
} {2};

#NOP -- Spirit Bracers;
#action {The bracers flare, and the attack misses you!} {
    .trackItem spirit-bracers block;
} {2};

#NOP -- CMC;
#action {A flicker in the containment field surrounding the CMC's plasma tank} {
    #line oneshot #action {results in a plasma leak within your suit, causing you great pain.} {#line gag} {1};
    .trackItem cmc leak;
} {2};

#action {The armour infuses you with chemicals, augmenting your abilities.} {
    .trackItem cmc stats;
} {2}; 

#action {The CMC-660 infuses you with adrenaline!} {
    .trackItem cmc heal;
} {2};

#action {^A flash of light from the CMC-660 fills everyone in your party with invigorating warmth!$} {
    .trackItem cmc party-heal;
} {1};

#action {The healing power of the phoenix restores your mind and body.} {
    .trackItem rolm phoenix-heal;
} {2};

#class item_tracker close;