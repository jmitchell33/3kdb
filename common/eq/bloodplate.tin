#class eq_bloodplate kill;
#class eq_bloodplate open;

#NOP -- Assume blood plate is full when we start so it doesn't spam corpses nonstop if it can't find the variable;
#NOP -- Blood Plate needs to be unbriefed for this to work;

#var blood_plate[status] 100;
#var blood_plate_toggle 1;

#action {1 Blood Plate \{%* stained\} \(worn\).} {
    #if {"%1" != "completely"} {
        #var blood_plate[status] 90;
        #var blood_plate_toggle 1;
    } {
        #var blood_plate[status] 100;
    };
} {2};

#action {Your armour senses your near-death experience and infuses you with blood!} {
    #if $gags[combat_items] {#line gag};
    #math blood_plate[status] {$blood_plate[status] - 10};
    #var blood_plate_toggle 1;
    .trackItem blood-plate dsave;
} {2};

#action {Your armour extends a tendril of blood that deflects the incoming attack!} {
    #if $gags[combat_items] {#line gag};
    #math blood_plate[status] {$blood_plate[status] - 10};
    #var blood_plate_toggle 1;
    .trackItem blood-plate block;
} {2};

#action {Tendrils from ${user_cap}'s armour reach for the corpse and absorb %*} {
    #if $gags[combat_items] {#line gag};
    #if {$blood_plate[status] < 100} {#var blood_plate_toggle 1;};
} {2};

#action {Your armour has reached its maximum saturation level.} {
    #if $gags[combat_items] {#line gag};
    #var blood_plate[status] 100;
    #var blood_plate_toggle 0;
} {2};

#ticker checkBloodPlate {
    #if {!$idle_flag && $incombat} {
        #if {$blood_plate_toggle && $corpses[total] > 0 && $blood_plate[status] < 100} {
            corpse_select;
            #var blood_plate_toggle 0;
        };
    };
} {2};

#class eq_bloodplate close;