#class {3kdb_whitelist} {kill}
#class {3kdb_whitelist} {open}

#action {^[PARTY] %w joins the party.$} {
    whitelist+ %1;
} {2};

#action {^[PARTY] %w has been booted from the party.} {
    whitelist- %1;
} {2};

#action {^[PARTY] %w has quit the party.$} {
    #NOP -- If you quit the party, clear whitelist and update to default;
    #if {"%1" == "$user_cap"} {
        whitelist-clear;
        whitelist_default;
    } {
        whitelist- %1;
    };
} {2};

#action {^[PARTY] %w: whitelist room} {
    whitelist-room;
} {2};

#action {^[PARTY] %w: whitelist me} {
    whitelist+ %1;
} {2};

#NOP -- Player whitelist;
#alias whitelist_default {
    #NOP;
};

#alias whitelist {
    #if {&whitelist[] > 0} {
        #unvar item;
        #echo {<caa>----        <fff> CURRENTLY ON THE VIP LIST <caa>        ----<088>};
        #loop 1 &whitelist[] item {
            #echo {   <cec>$item   <fbc>$whitelist[$item]<088>};
        };
    } {
        #echo {<caa>----                  <fff> THE VIP LIST IS EMPTY <caa>                  ----<088>};
    };
    #echo {};
    #echo {<caa>----    <ced> whitelist+ name <dac>to add someone to the VIP list <caa>       ----<088>};
    #echo {<caa>----    <ced> whitelist- name <dac>to remove someone from the VIP list <caa>  ----<088>};
    #echo {<caa>----    <ced> whitelist-clear <dac>to clear the VIP list <caa>                ----<088>};
    #echo {};
    #echo {<caa>----    <ced> whitelist-room <dac> to add the room to the the VIP list <caa>  ----<088>};
    #echo {<caa>----    <ced> whitelist-party <dac>to add your party to the VIP list <caa>    ----<088>};
};

#alias whitelist-clear {
    #list whitelist clear;
    #echo {<caa>----  <fff> Whitelist has been cleared <caa>  ----<088>};
};

#alias whitelist-party {
    whitelist_default;
    members;
    #line oneshot #action {Total XP gain since party creation :} {
        #local item {};
        #foreach $party[member][%*] item {
            #unvar tmpItem;
            #format tmpItem {%n} {$item[name]};
            #var tmpItemExists 0;
            #list whitelist find {$tmpItem} tmpItemExists;
            #if !$tmpItemExists {
                #list whitelist add {$tmpItem};
                #echo {==> <cfb> Added $tmpItem to VIP List <088> <==};
            };
        };
    };
};

#alias whitelist-room {
    whitelist_default;
    _update_players;
    #if $bot[active] {.pause};
    look;
    #delay 4 {
        #unvar item {};
        #foreach $room[players][%*] item {
            #format tmpItem {%n} {$item};
            #var tmpItemExists 0;
            #list whitelist find {$tmpItem} tmpItemExists;
            #if !$tmpItemExists {
                #list whitelist add {$tmpItem};
                #echo {==> <cfb> Added $tmpItem to VIP List <088> <==};
            };   
        };
        #if $bot[active] {.unpause};
    };
};

#alias whitelist+ {
    #if {"%0" != ""} {
        #format tmpItem {%n} {%0};
        #var tmpItemExists 0;
        #list whitelist find {$tmpItem} tmpItemExists;
        #if !$tmpItemExists {
            #list whitelist add {$tmpItem};
            #echo {==> <cfb> Added $tmpItem to VIP List <088> <==};
        };
    };
};
        
#alias whitelist- {
    #if {"%0" != ""} {
        #format tmpItem {%n} {%0};
        #list whitelist find {$tmpItem} {index};
        #if {$index > 0} {
            #list whitelist delete $index;
            #echo {==> <eab> Removed $tmpItem from VIP List <088> <==};
        };
    };
};


#class {3kdb_whitelist} {close}