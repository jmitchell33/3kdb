#class helpers open;

#alias {.tickers} {
	#info tickers save;
	#draw Yellow scroll line 1 1 1 62;
	#echo {<128>%-20s %+20s %+20s} {Name} {Interval} {Remaining};
	#draw Yellow scroll line 1 1 1 62;
	#format utime %U;

	#loop 1 &info[TICKERS][] index {
		#math uval $info[TICKERS][+$index][arg3] * 1000000;

		#echo {%-20s %+20s %+20m} {$info[TICKERS][+$index][arg1]} {$info[TICKERS][+$index][arg3]} {($uval - ($utime - $info[TICKERS][+$index][arg4]) % $uval) / 1000000};
	};
};

#function {rnd} {#math {result} {1 d (%2 - %1 + 1) + %1 - 1}}
#function {min} {#if {%1 < %2} {#return %1} {#return %2}};
#function {max} {#if {%1 > %2} {#return %1} {#return %2}};
#function formatThousands {#format result {%g} {%0};};
#function lowerText {#format result {%l} {%0};#return $result};

#function timeToRounds {
	#var timeWeeks 0;
	#var timeDays 0;
	#var timeHrs 0;
	#var timeMins 0;
	#var timeSecs 0;
	#regex {%0} {%d week{|s}} {
		#format timeWeeks {%d} &1;
	};
	#regex {%0} {%d day{|s}} {
		#format timeDays {%d} &1;
	};
	#regex {%0} {%d hr{|s}} {
		#format timeHrs {%d} &1;
	};
	#regex {%0} {%d min{|s}} {
		#format timeMins {%d} &1;
	};
	#regex {%0} {%d sec{|s}} {
		#format timeSecs {%d} &1;
	};
	#math result {(($timeWeeks * 86400 * 7) + ($timeDays * 86400) + ($timeHrs * 3600) + ($timeMins * 60) + $timeSecs) / 2};
};

#function {graph} {
	#regex {%1} {\d} {
		#math per {100.0*%1/%2};
		#format {per} {%d} {$per};
		#if {$per > 100} {#var graph {<006>   $per%   <088>}};
		#if {$per == 100} {#var graph {<008><AFA>   $per%   <088>}};
		#if {$per < 100} {#var graph {<008><AEA>    $per%  <088> }};
		#if {$per < 90}	{#var graph {<008><BEA>    $per% <070>  <088>}};
		#if {$per < 80} {#var graph {<008><BDA>    $per%<070>   <088>}};
		#if {$per < 70} {#var graph {<008><CDA>    $per<070>%   <088>}};
		#if {$per < 60} {
			#format {per_1} {%.1s} {$per};
			#format {per_2} {%r} {$per};
			#format {per_2} {%.1s} {$per_2};
			#var graph {<008><DCA>    ${per_1}<070>${per_2}%   <088>}};
		#if {$per < 50} {#var graph {<008><DBA>    <070>$per%   <088>}};
		#if {$per < 40} {#var graph {<008><EBA>   <070> $per%   <088>}};
		#if {$per < 30} {#var graph {<008><EAA>  <070>  $per%   <088>}};
		#if {$per < 20} {#var graph {<008><FAA> <070>   $per%   <088>}};
		#if {$per < 10} {#var graph {<070>     $per%   <088>}}
	} {
		#format {var} {%.3s} {%1};
		#if {"$var" == "bet" || "$var" == "hea"} {
			#var graph {<006>          <088>}
		}; #elseif {"$var" == "per"} {
			#var graph {<008><AFA>          <088>}
		}; #elseif {"$var" == "bru"} {
			#var graph {<008><BEA>        <088>  }
		}; #elseif {"$var" == "ble"} {
			#var graph {<008><DBA>    <088>      }
		}; #elseif {"$var" == "eme" || "$var" == "alm"} {
			#var graph {<008><FAA> <088>         }
		}; #else {
			#var graph {          }
		}
	};

	#return $graph;
};

#function {word_to_number} {
	#class tmpNumbers open;
	#var numSmall[zero] 0;
	#var numSmall[one] 1;
	#var numSmall[two] 2;
	#var numSmall[three] 3;
	#var numSmall[four] 4;
	#var numSmall[five] 5;
	#var numSmall[six] 6;
	#var numSmall[seven] 7;
	#var numSmall[eight] 8;
	#var numSmall[nine] 9;
	#var numSmall[ten] 10,;
	#var numSmall[eleven] 11;
	#var numSmall[twelve] 12;
	#var numSmall[thirteen] 13;
	#var numSmall[fourteen] 14;
	#var numSmall[fifteen] 15;
	#var numSmall[sixteen] 16;
	#var numSmall[seventeen] 17;
	#var numSmall[eighteen] 18;
	#var numSmall[nineteen] 19,;
	#var numSmall[twenty] 20;
	#var numSmall[thirty] 30;
	#var numSmall[forty] 40;
	#var numSmall[fifty] 50;
	#var numSmall[sixty] 60;
	#var numSmall[seventy] 70;
	#var numSmall[eighty] 80;
	#var numSmall[ninety] 90;
	#class tmpNumbers close;

	#unvar tmpList;
	#var tmpList %0;
	#replace tmpList {-} { };
	#replace tmpList {  } { };
	#list tmpList explode { };
  	#var tmpResult 0;
  	#loop 1 &tmpList[] i {
		#var x $numSmall[$tmpList[$i]];
		#if {"$x" != ""} {#math tmpResult {$tmpResult + $x}} {
			#if {"$tmpList[$i]" == "hundred"} {#math tmpResult {$tmpResult * 100}};
		};
	};
	#class tmpNumbers kill;
	#return $tmpResult;
};

#function {roman_to_number} {
	#class tmpRomantoNumbers open;
	#var romanNumber[I] 1;
	#var romanNumber[II] 2;
	#var romanNumber[III] 3;
	#var romanNumber[IV] 4;
	#var romanNumber[V] 5;
	#var romanNumber[VI] 6;
	#var romanNumber[VII] 7;
	#var romanNumber[VIII] 8;
	#var romanNumber[IX] 9;
	#var romanNumber[X] 10;
	#var romanNumber[XI] 11;
	#var romanNumber[XII] 12;
	#var romanNumber[XIII] 13;
	#var romanNumber[XIV] 14;
	#var romanNumber[XV] 15;
	#var romanNumber[XVI] 16;
	#class tmpRomantoNumbers close;

	#unvar tmpList;
	#var tmpList %0;
	#replace tmpList {-} { };
	#replace tmpList { } { };
	#list tmpList explode { };
	#var tmpResult 0;
	#loop 1 &tmpList[] i {
		#var x $romanNumber[$tmpList[$i]];
		#if {"$x" != ""} {#math tmpResult {$tmpResult + $x}} {
		};
	};
	#class tmpRomantoNumbers kill;
	#return $tmpResult;
};

#NOP -- Unique ID that is reasonably random enough for mudding;
#function makeID {
	#local randomAlphabet abcdefghijklmnopqrstuvwxyz1234567890;
	#local tmpAlphabetList {};
	#list tmpAlphabetList tokenize $randomAlphabet;
	#local result {};
	#local i 1;
	#loop 1 15 i {
		#var result {${result}${tmpAlphabetList[@rnd{1;36}]}};
	};
	#return $result;
};

#class helpers close;