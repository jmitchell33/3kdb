#class {dmgtracker} {kill}
#class {dmgtracker} {open}

#var dmgtracker 1;

#alias .trackExtraAttack {
    #math cmbt[extra_attacks][round] {$cmbt[extra_attacks][round] + 1};
    #math cmbt[extra_attacks][total] {$cmbt[extra_attacks][total] + 1};
};

#alias dmgTrack {
    #format newBest {%u} {NEW BEST %1 ROUND: };
    #var cmbt[category][%1][name] %1;
    #var cmbt[category][%1][curr] %2;
    #math cmbt[category][%1][dmg] {$cmbt[category][%1][dmg] + %2};
    #math cmbt[category][%1][rnd] {$cmbt[category][%1][rnd] + 1};
    #math cmbt[category][%1][avg] {$cmbt[category][%1][dmg] / $cmbt[category][%1][rnd]};
    

    #if {$cmbt[category][%1][freq][%3][count] > 0} {
        #math cmbt[category][%1][freq][%3][count] {$cmbt[category][%1][freq][%3][count] + 1};
        #math cmbt[category][%1][freq][%3][dmg] {$cmbt[category][%1][freq][%3][dmg] + %2};
        #math cmbt[category][%1][freq][%3][avg] {$cmbt[category][%1][freq][%3][dmg] / $cmbt[category][%1][freq][%3][count]};
    } {
        #var cmbt[category][%1][freq][%3][name] %1;
        #var cmbt[category][%1][freq][%3][count] 1;
        #var cmbt[category][%1][freq][%3][dmg] %2;
        #var cmbt[category][%1][freq][%3][avg] %2;
    };

    #if {$cmbt[category][%1][dmg] < 1000} {
        #math cmbt[histogram][999] {$cmbt[histogram][999] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 999 && $cmbt[category][%1][dmg] <= 2000} {
        #math cmbt[histogram][2000] {$cmbt[histogram][2000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 1999 && $cmbt[category][%1][dmg] <= 3000} {
        #math cmbt[histogram][3000] {$cmbt[histogram][3000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 2999 && $cmbt[category][%1][dmg] <= 4000} {
        #math cmbt[histogram][4000] {$cmbt[histogram][4000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 3999 && $cmbt[category][%1][dmg] <= 5000} {
        #math cmbt[histogram][5000] {$cmbt[histogram][5000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 4999 && $cmbt[category][%1][dmg] <= 6000} {
        #math cmbt[histogram][6000] {$cmbt[histogram][6000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 5999 && $cmbt[category][%1][dmg] <= 7000} {
        #math cmbt[histogram][7000] {$cmbt[histogram][7000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 6999 && $cmbt[category][%1][dmg] <= 8000} {
        #math cmbt[histogram][8000] {$cmbt[histogram][8000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 7999 && $cmbt[category][%1][dmg] <= 9000} {
        #math cmbt[histogram][9000] {$cmbt[histogram][9000] + 1};
    };

    #if {$cmbt[category][%1][dmg] > 8999 && $cmbt[category][%1][dmg] <= 10000} {
        #math cmbt[histogram][10000] {$cmbt[histogram][10000] + 1};
    };
    
    #if {"${cmbt[category][%1][high]}" === ""} {#var cmbt[category][%1][high] 0};
    #if {$cmbt[category][%1][curr] > $cmbt[category][%1][high]} {#var cmbt[category][%1][high] $cmbt[category][%1][curr];#echo {$newBest $dmgDisplay};};
};

#alias dodgeTrack {
    #if {$cmbt[dodge][total] > 0} {
        #math cmbt[dodge][total] {$cmbt[dodge][total] + 1};
    } {
        #var cmbt[dodge][total] 1;
    };
};

#alias dmgFormat {
    #math tmpPercent {1.00 * $cmbt[category][%1][rnd] / $cmbt[category][total][rnd] * 100};
    #format dmgName {%n} {%1};
    #format dmgAvg {%g} {$cmbt[category][%1][avg]};
    #format dmgTotal {%M} {$cmbt[category][%1][dmg]};
    #replace dmgTotal {G} {B};
    #format dmgBest {%g} {$cmbt[category][%1][high]};
    #format dmgActive {%d} {$tmpPercent};
    #format dmgSummary {%+12s %+9s %+9s %+9s %+9s} {$dmgName} {$dmgAvg} {$dmgTotal} {$dmgBest} {${dmgActive}%};
    #echo $dmgSummary;
};

#alias dmgFormatHits {
    #math tmpPercent {1.00 * $cmbt[category][%1][rnd] / $cmbt[category][total][rnd] * 100};
    #format dmgName {%n} {%1};
    #format dmgActive {%d} {$tmpPercent};
    #echo {<efb>$dmgName - $dmgActive of Rounds<088>};
    #loop 1 &cmbt[category][%1][freq][] freq {
        #format dmgAvg {%g} {$cmbt[category][%1][freq][$freq][avg]};
        #format dmgRounds {%g} {$cmbt[category][%1][freq][$freq][count]};
        #format dmgSummary {%+12s %+9s %+9s} {$freq} {$dmgAvg} {$dmgRounds};
        #echo $dmgSummary;
    };
};

#alias {dreset} {
    #var golem_dmg[curr] 0;
    #var golem_dmg[avg] 0;
    #var golem_dmg[rnd] 0;
    #var golem_dmg[high] 0;
    #var golem_dmg[dmg] 0;

    #var spell_dmg[curr] 0;
    #var spell_dmg[avg] 0;
    #var spell_dmg[rnd] 0;
    #var spell_dmg[dmg] 0;
    #var spell_dmg[high] 0;

    #var dmgt[curr] 0;
    #var dmgt[avg] 0;
    #var dmgt[count] 0;
    #var dmgt[high] 0;
    #var dmgt[dmg] 0;

    #var cmbt[affects][big_game_hunter] 0;
    #var cmbt[affects][assassin] 0;
    #var cmbt[affects][tactician] 0;
    #var cmbt[affects][magician] 0;
    #var cmbt[affects][drunken_master] 0;
    #var cmbt[affects][bloodfuse] 0;
    #var cmbt[affects][clutch] 0;
    #var cmbt[affects][jump] 0;
    #var cmbt[penetration][round] 0;
    #var cmbt[penetration][total] 0;
    #var cmbt[extra_attacks][round] 0;
    #var cmbt[extra_attacks][total] 0;
    #var ewell_round 0;

    #var dmgt[curr] 0;
    #var dmgt[avg] 0;
    #var dmgt[count] 0;
    #var dmgt[high] 0;
    #var dmgt[dmg] 0;

    #foreach $cmbt[category][%*] item {
        #var cmbt[category][${item[name]}][curr] 0;
        #var cmbt[category][${item[name]}][dmg] 0;
        #var cmbt[category][${item[name]}][rnd] 0;
        #var cmbt[category][${item[name]}][avg] 0;
        #var cmbt[category][${item[name]}][high] 0;
    };

    #var golem_dmg[curr] 0;
    #var golem_dmg[avg] 0;
    #var golem_dmg[rnd] 0;
    #var golem_dmg[high] 0;
    #var golem_dmg[dmg] 0;

    #var spell_dmg[curr] 0;
    #var spell_dmg[avg] 0;
    #var spell_dmg[rnd] 0;
    #var spell_dmg[dmg] 0;
    #var spell_dmg[high] 0;

    #var dog[rnd] 0;
    #var dog[dmg] 0;
};

dreset;

#NOP -- Toggles for necro powers;
#action {^B:You grab your enemy with your talons in a clutch of death!$} {#var cmbt[affects][clutch] 1} {2};
#action {^B:You grasp hold of your enemy with the dark touch of death.$} {#var cmbt[affects][grasp] 1} {2};
#action {^You grab hold of %w with your left talon} {#var cmbt[affects][bloodarc] 1} {2};
#action {^Scarlet lightning flies from your hand and scorches a massive wound } {#var cmbt[affects][bloodarc] 1} {2};
#action {^Your eyes burn with a blinding rage that strikes fear into your very} {#var cmbt[affects][bloodarc] 1} {2};
#action {^You reach back and strike your bony hand outward, toward %*} {#var cmbt[affects][bloodarc] 1} {2};
#action {^A violet scream erupts from your lips as you strikes forth with a} {#var cmbt[affects][bloodarc] 1} {2};
#action {^You raise your palm forward and a %* skull of %*} {#var cmbt[affects][skullfire] 1} {2};

#NOP -- Toggles for profession boosts;
#action {^Your crazed ambition overflows!$} {#var cmbt[affects][big_game_hunter] 1} {2};
#action {^Your wild ambition takes a back seat to prudence.$} {#var cmbt[affects][big_game_hunter] 0} {2};
#action {^You feel a sudden wave of alcohol induced fury overcome you!$} {#var cmbt[affects][drunken_master] 1} {2};
#action {^Your alcohol fueled strength dissipates.} {#var cmbt[affects][drunken_master] 0} {2};
#action {^A flash of insidious insight strikes you!$} {#var cmbt[affects][assassin] 1} {2};
#action {^Your insidious inspiration fades.$} {#var cmbt[affects][assassin] 0} {2};
#action {^You outfox your opponent and take advantage of it!$} {#var cmbt[affects][tactician] 1} {2};
#action {^Your tactical advantage ends.$} {#var cmbt[affects][tactician] 0} {2};


#ACTION {^[%d] %* {mind warp|sunspear|incinerate|frostbite}%*} {
    #var incombat 1;
    #var spell_dmg[curr] %1;
    #math spell_dmg[dmg] {$spell_dmg[dmg] + %1};
    #math spell_dmg[rnd] {$spell_dmg[rnd] + 1};
    #math spell_dmg[avg] {$spell_dmg[dmg] / $spell_dmg[rnd]};
    #math new_best {$spell_dmg[curr] > $spell_dmg[high]};
    #if $new_best {
        #var spell_dmg[high] $spell_dmg[curr];
        #ECHO {NEW BEST SPELL DMG: $spell_dmg[high]};
    };

    #math spell_dmg[%3][dmg] {$spell_dmg[%3][dmg] + $spell_dmg[dmg]};
    #math spell_dmg[%3][rnd] {$spell_dmg[%3][rnd] + 1};
    #math spell_dmg[%3][avg] {$spell_dmg[%3][dmg] / $spell_dmg[%3][rnd]};

    #math new_best {$spell_dmg[curr] > $spell_dmg[%3][high]};
    #if $new_best {#var spell_dmg[%3][high] $spell_dmg[curr]};
    
} {2};

#action {^You drive your attack past %* resistances!$} {
    #math cmbt[penetration][round] {$cmbt[penetration][round] + 1};
    #math cmbt[penetration][total] {$cmbt[penetration][total] + 1};
} {2};

#action {^Your great speed allows you to attack again!$} {
    .trackExtraAttack;
} {2};

#NOP -- Damage modifier captures;
#action {^You bash %* with your shield%*} {
    #var cmbt[affects][shield_bash] 1;
} {2};

#action {A bolt of electricity streams from your hat into %*!$} {
    #var cmbt[affects][witch_hat] 1;
} {2};

#action {^A bolt of pure energy flies out of the sky striking %*!$} {
    #var cmbt[affects][eradication] 1;
} {2};

#action {^With iron will, you finally guide the deadly spell toward its target!$} {
    #var cmbt[affects][iprime] 1;
} {2};

#action {^Smoldering from the impact, the corpse rises with an unholy energy.} {
    #var cmbt[affects][bloodfuse] 1;
} {2};

#action {^Its energy spent, the dark creature lets loose a horrible scream} {
    #var cmbt[affects][bloodfuse] 0;
} {2};

#NOP -- Convoke we'll track each round to see how the damage increases each round;
#action {^B:Your vampiric essence calls out to the children of the night.} {#var cmbt[affects][convoke] 1;} {2};

 
#action {^The brilliant sphere of energy explodes!} {
    #var ewell_round 1;
} {2}

#alias .dmg_round_reset {
    #var cmbt[affects][shield_bash] 0;
    #var cmbt[affects][witch_hat] 0;
    #var cmbt[affects][iprime] 0;
    #var cmbt[affects][eradication] 0;
    #var cmbt[affects][clutch] 0;
    #var cmbt[affects][grasp] 0;
    #var cmbt[affects][bloodarc] 0;
    #var cmbt[affects][skullfire] 0;
    #var cmbt[affects][lucanus_cloak] 0;
    #var cmbt[penetration][round] 0;
    #var cmbt[extra_attacks][round] 0;
    #var ewell_round 0;
    #if {$cmbt[affects][convoke] == 4} {#var cmbt[affects][convoke] 0;};
};
    
#NOP -- If you've just conjured undeads with necromancer, ignore those;
#act {{^You hit (.*) (\d+) (time|times) for (\d+) damage\.$}} {
    #if {"$guild" == "necromancer" && "%2" != "Undead"} {
        #var vaf[numbers] 1;
        #var incombat 1;
        #format enemy[name_numbers] {%l} {%2};
        #format dmgTimes {%d} %3;
        #format dmgAmount {%d} %5;
        #format dmgDisplay {%g} {$dmgAmount};
        #line gag;
        #echo {You hit %2 %3 %4 for $dmgDisplay damage.};

        #if {$vaf[numbers]} {#var incombat 1};

        #if !$ewell_round {
            dmgTrack total $dmgAmount $dmgTimes;

            #if {"$guild" == "necromancer" && $my[necromancer][glamors][furious]} {
                dmgTrack fury $dmgAmount $dmgTimes;
            };

            #if {"$guild" == "necromancer" && $my[necromancer][glamors][raging]} {
                dmgTrack rage $dmgAmount $dmgTimes;
            };

            #NOP -- If it's an iprime or eradication round, pull those out;
            #if {$cmbt[affects][eradication] || $cmbt[affects][iprime]} {
                #if {$cmbt[affects][eradication]} {
                    dmgTrack eradication $dmgAmount $dmgTimes;
                };

                #if {$cmbt[affects][iprime]} {
                    dmgTrack iprime $dmgAmount $dmgTimes;
                };
            } {
                #if {$mob[lucanus_ring]} {
                    dmgTrack lucanus $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][lucanus_cloak]} {
                    dmgTrack lucan_cloak $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][big_game_hunter]} {
                    dmgTrack BGH $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][assassin]} {
                    dmgTrack assassin $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][tactician]} {
                    dmgTrack tactician $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][magician]} {
                    dmgTrack magician $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][drunken_master]} {
                    dmgTrack DM $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][bloodfuse]} {
                    dmgTrack bloodfuse $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][bloodarc]} {
                    dmgTrack bloodarc $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][clutch]} {
                    dmgTrack clutch $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][grasp]} {
                    dmgTrack grasp $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][skullfire]} {
                    dmgTrack skullfire $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][witch_hat]} {
                    dmgTrack WitchHat $dmgAmount $dmgTimes;
                };
                #if {$cmbt[affects][convoke]} {
                    dmgTrack convoke_${cmbt[affects][convoke]} $dmgAmount $dmgTimes;
                    #math cmbt[affects][convoke] {$cmbt[affects][convoke] + 1};
                };
            };
        };

        .dmg_round_reset;
    }
} {2};

#act {{^El Jefe (.*) hit for (\d+) damage.$}} {
    #var incombat 1;
    #var golem_dmg[curr] %3;#math golem_dmg[dmg] {$golem_dmg[dmg] + %3};
    #math golem_dmg[rnd] {$golem_dmg[rnd] + 1};
    #math {golem_dmg[avg]} {$golem_dmg[dmg] / $golem_dmg[rnd]};

    #math new_best {$golem_dmg[curr] > $golem_dmg[high]};
    #if $new_best {#var golem_dmg[high] $golem_dmg[curr]};
} {2};
    

#act {{^(.*) hits you for (\d+) damage\!$}} {
    #var incombat 1;
    #var dmgt[curr] %3;
    #math dmgt[dmg] {$dmgt[dmg] + %3};
    #math dmgt[count] {$dmgt[count] + 1};
    #math {dmgt[avg]} {$dmgt[dmg] / $dmgt[count]};
} {2};



#alias {dstats} {
    #format dmgHeader {%+12s %+9s %+9s %+9s %+9s} {} {Avg} {Total} {Best} {% Rnds};
    #format tempNumber {%g} {$cmbt[category][total][rnd]};
    #format dmgTitle  {%+48s} {DAMAGE REPORT ($tempNumber Rounds)};
    #echo {<faa>$dmgTitle<088>};
    #echo $dmgHeader;

    #if {"%0" == "hits"} {
        dmgFormatHits total;

        #foreach $cmbt[category][%*] item {
            #if {"$item[name]" != "total" && $item[rnd] > 0} {
                dmgFormatHits $item[name];
            };
        };
    } {
        dmgFormat total;

        #foreach $cmbt[category][%*] item {
            #if {"$item[name]" != "total" && $item[rnd] > 0} {
                dmgFormat $item[name];
            };
        };
    };
    
    #if {"$guild" == "mage"} {
        #echo {\nAverage Spell damage : $spell_dmg[avg] over $spell_dmg[rnd] rounds.  Total: $spell_dmg[dmg] damage.};
        #echo {\High Spell damage : $spell_dmg[high]};
        #echo {\nAverage Golem damage : $golem_dmg[avg] over $golem_dmg[rnd] rounds.  Total: $golem_dmg[dmg] damage.};
        #echo {\High Golem damage : $golem_dmg[high]};
    };
    
    #echo {\n------- DAMAGE BY # OF HTIS -------};
    #foreach $cmbt[freq][%*] item {
        #echo {$item[name] hits : @formatThousands{$item[avg]} Avg. achieved $item[count] times};
    };

    #echo {\nDamage taken   : @formatThousands{$dmgt[avg]} over @formatThousands{$dmgt[count]} hits.  Total: @formatThousands{$dmgt[dmg]} damage.\n};

    #math {dog[avg]} {$dog[dmg] / $dog[rnd]};
    #math {cmbt[totalavg]} {$dog[avg] + $cmbt[avg]};
    #nop showme {Dog Avg: $dog[avg] over $dog[rnd] rounds.  Total dog: $dog[dmg] damage.  Total average: $cmbt[totalavg] damage/rnd.};
};

#NOP;
#action {Your cloak glows as the attack strikes it and suddenly sends a blast of} {
    #var cmbt[affects][lucanus_cloak] 1;
} {2};

#NOP Juggernaut Dmg report;
#action {You jump attack %* for %*. \{%d\}} {
    #var dmgTimes 1;
    #var dmgAmount %3;
    dmgTrack jump $dmgAmount $dmgTimes;
} {2};

#NOP Mob Dodge Tracking;
#action {%* deftly slips away from your attack!$} {dodgeTrack};
#action {%* blinks out in time to evade your attack.$} {dodgeTrack};
#action {%* parries the attack.$} {dodgeTrack};

#NOP #ticker dmg_stats {    #var caption_data[dmg] {\n Average damage : $cmbt[avg] over $cmbt[rnd] rounds.  Total: $cmbt[dmg] damage.\n Best damage : $cmbt[high] damage};} 2;

#class {dmgtracker} {close}