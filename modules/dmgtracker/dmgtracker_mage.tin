#class dmgtracker_mage kill;
#class dmgtracker_mage open;

#unvar summon_types_list;

#list summon_types_list add {Celestial lion};
#list summon_types_list add {Undead wraith};
#list summon_types_list add {Shadow panther};
#list summon_types_list add {Something};

#unvar summon_dmg;
#var summon_dmg[first_round] $total_rounds;

#alias summonDmg-clear {
    #unvar summon_dmg;
    #var summon_dmg[first_round] $total_rounds;
    #echo {<dfa> +-+-+-    SUMMON DAMAGE TRACKER RESET    -+-+-+<088>};
};

#alias summonDmg {
    #format dmgSummary {<dfa>%+20s %+9s %+9s %+9s %+9s<088>} {Summon} {Hits/Rnd} {Dmg/Hit} {Dmg/Rnd} {Total};
    #echo $dmgSummary;

    #var summon_dmg[current_round] $total_rounds;
    #math summon_dmg[tracked_rounds] {$summon_dmg[current_round] - $summon_dmg[first_round]};
    #foreach $summon_types_list[%*] summon {
        #var summonTotalDmg 0;
        #var summonPerHitDmg 0;
        #var summonTotalHits 0;
        #foreach $dmg_dataKey[%*] dmgItem {
            #if {$summon_dmg[$summon][total][$dmgItem][count] > 0} {
                #math summonItemTotalDmg {$summon_dmg[$summon][total][$dmgItem][count] * $dmg_data[melee][$dmgItem][min_dmg]};
                #math summonTotalDmg {$summonTotalDmg + $summonItemTotalDmg};
                #math summonTotalHits {$summonTotalHits + $summon_dmg[$summon][total][$dmgItem][count]};
            };
        };
        #format summonTotalDmg {%M} {$summonTotalDmg};
        #replace summonTotalDmg {G} {B};
        
        #math summonPerHitDmg {$summonTotalDmg / $summon_dmg[$summon][total][attempts]};
        #format summonPerHitDmg {%d} {$summonPerHitDmg};
        #format summonPerHitDmg {%g} {$summonPerHitDmg};

        #math summonHitsPerRnd {$summonTotalHits / $summon_dmg[tracked_rounds]};
        #format summonHitsPerRnd {%d} {$summonHitsPerRnd};

        #math summonPerRoundDmg {$summonTotalDmg / $summon_dmg[tracked_rounds] * 1.0};
        #format summonPerRoundDmg {%d} {$summonPerRoundDmg};
        #format summonPerRoundDmg {%g} {$summonPerRoundDmg};

        #if {$summonTotalDmg > 0} {
            #if {"$summon" == "Something"} {#var summon Phantasmal Killer};
            #format dmgSummary {%+20s %+9s %+9s %+9s %+9s} {$summon} {$summonHitsPerRnd} {$summonPerHitDmg} {$summonPerRoundDmg} {$summonTotalDmg};
            #echo $dmgSummary;
        };
    };
};


#alias summonDmgRecord {
    #var foundItem 0;
    #format summonType {%p} {%1};
    #format summonHit {%p} {%2};
    #list summon_types_list find {$summonType} {foundItem};
    #if $foundItem {
        #math summon_dmg[$summonType][round][$summonHit][count] {$summon_dmg[$summonType][round][$summonHit][count] + 1};
        #math summon_dmg[$summonType][total][$summonHit][count] {$summon_dmg[$summonType][total][$summonHit][count] + 1};
        #math summon_dmg[$summonType][total][attempts] {$summon_dmg[$summonType][total][attempts] + 1};
    };
};

#ACTION {^%* blow was deflected.$} {summonDmgRecord {%1} deflected};
#ACTION {^%* strike was ineffective.$} {summonDmgRecord {%1} ineffective};
#ACTION {^%* bobble your swing.$} {summonDmgRecord {%1} bobble};
#ACTION {^%* missed %*.$} {summonDmgRecord {%1} missed};
#ACTION {^%* tickled %* in the stomach.$} {summonDmgRecord {%1} tickled};
#ACTION {^%* grazed %*.$} {summonDmgRecord {%1} grazed};
#ACTION {^%* hit %*.} {summonDmgRecord {%1} hit};
#ACTION {^%* hit %* hard.$} {summonDmgRecord {%1} hit_hard};
#ACTION {^%* hit %* very hard.$} {summonDmgRecord {%1} hit_vhard};
#ACTION {^%* struck %* a mighty blow.$} {summonDmgRecord {%1} mighty};
#ACTION {^%* smashed %* with a bone crushing sound.$} {summonDmgRecord {%1} bonecrush};
#ACTION {^%* pulverized %* with a powerful attack.$} {summonDmgRecord {%1} pulverized};
#ACTION {^%* trounced %* up and down.$} {summonDmgRecord {%1} trounced};
#ACTION {^%* pummeled %* into small fragments.$} {summonDmgRecord {%1} pummeled};
#ACTION {^%* massacred %* into tiny fragments.$} {summonDmgRecord {%1} massacred};
#ACTION {^%* utterly annihilated %*.$} {summonDmgRecord {%1} annihilated};
#ACTION {^%* completely devastated %* with awesome force.$} {summonDmgRecord {%1} devastated};
#ACTION {^%* destroyed %*.$} {summonDmgRecord {%1} destroyed};
#ACTION {^%* absolutely massacred %*.$} {summonDmgRecord {%1} amassacred};
#ACTION {^%* thrashed %* completely.$} {summonDmgRecord {%1} thrashed};
#ACTION {^%* demolished %*.$} {summonDmgRecord {%1} demolished};
#ACTION {^%* nearly exterminated %*.$} {summonDmgRecord {%1} exterminated};
#ACTION {^%* obliterated %*.$} {summonDmgRecord {%1} obliterated};
#ACTION {^%* eradicated %*.$} {summonDmgRecord {%1} eradicated};
#ACTION {^%* decimated %*.} {summonDmgRecord {%1} decimated};
#ACTION {^%* beat %* like a rented mule.$} {summonDmgRecord {%1} rented_mule};
#ACTION {^%* exsanguinated %*.$} {summonDmgRecord {%1} exsanguinated};
#ACTION {^%* atomized %*.$} {summonDmgRecord {%1} atomized};
#ACTION {^%* beat %* like a red-headed step child.$} {summonDmgRecord {%1} step_child};
#ACTION {^%* drop-kicked %*.$} {summonDmgRecord {%1} dropkick};

#class dmgtracker_mage close;