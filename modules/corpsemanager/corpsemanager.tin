#class {corpse_manager} {kill}
#class {corpse_manager} {open}

#NOP -- GENERAL CORPSE MANAGEMENT TRACKING FOR HPBAR;
#var corpse_check 0;
#var corpse_check_toggle 1;
#var corpses[inventory] 0;
#var corpses[smuggle] 0;
#var corpses[cooler] 0;
#var corpses[coffin] 0;
#var corpses[freezer] 0;
#var corpses[servant] 0;
#var corpses[golem] 0;


#NOP -- Block certain things from picking up as "corpse";
#act {There is no corpse in this room master.} {#NOP} {1};
#act {You must have a corpse to absorb.} {#NOP} {1};

#alias check_corpses {
	#if $corpse_check_toggle {
		#var corpse_check_toggle 0;
		#delay 3 {#var corpse_check_toggle 1};
		#class corpseCapture open;
		#if $test_gag {
			#action {# Item} {
				#line gag;
				#event {CATCH PROCESSED LINE} {
					#format tmpLine {%p} {%%%1};
					#if $inventory[cooler] {
						#regex {$tmpLine} {It looks very light.} {#class corpseCapture kill};
						#regex {$tmpLine} {It looks light.} {#class corpseCapture kill};
						#regex {$tmpLine} {It doesn't look too heavy.} {#class corpseCapture kill};
						#regex {$tmpLine} {It looks heavy.} {#class corpseCapture kill};
						#regex {$tmpLine} {It would take considerable effort to lift this.} {#class corpseCapture kill};
						#regex {$tmpLine} {You'll most likely herniate a disc lifting this} {#class corpseCapture kill};
					} {
						#regex {$tmpLine} {You are lightly burdened.} {#class corpseCapture kill};
						#regex {$tmpLine} {You are burdened.} {#class corpseCapture kill};
						#regex {$tmpLine} {You are encumbered.} {#class corpseCapture kill};
						#regex {$tmpLine} {You are heavily loaded.} {#class corpseCapture kill};
						#regex {$tmpLine} {You are over taxed.} {#class corpseCapture kill};
						#regex {$tmpLine} {You are collapsing under your load. } {#class corpseCapture kill};
					};
					#if !$debug[event] {#NOP};
				};
			};
		};
		#if {"$guild" == "necromancer"} {
			#var corpse_check 1;
        	#var corpses[inventory] 0;
        	#send i3;
        	#delay 2 {#var corpse_check 0;#var inventory_check 0;};
		} {
			i;
		};
		#if $inventory[cooler] {examine cooler};
		#NOP -- Delete the class and unevent for secondary measure;
		#if $test_gag {
			#delay 2 {#class corpseCapture kill};
			#delay 2 {#unevent {CATCH PROCESSED LINE}};
		};
	};
};

#NOP -- Inventory tracking is often inconsistent since you can use them from there.  If there are corpses in inventory,;
#NOP -- Verify every so often that they're still there;
#ticker corpses_inventory {
	#if !$idle_flag {
		#if {$corpses[inventory] > 0} {
			check_corpses
		};
	};
} {120};


#action {You have no {corpse|remains}.} {check_corpses;} {1};

#NOP -- Donate your corpses to the morgue and then go back/resuming botting;
#alias dcoffin {
	#if $bot[active] {.pause};
    #map get roomvnum donate_go_back;
	go morgue;
    donate coffin;donate all;
    #map run $donate_go_back;
    #if $bot[active] {
		#delay 6 {
			#var bot[paused] 0;
			.bot
		};
	};
};

#alias dcoffin1 {
    #if $bot[active] {.pause};
    #map get roomvnum donate_go_back;
	go morgue;
    donate coffin;donate all;
	deslab all;
	#${corpses[cooler]} uncooler corpse;
	#10 {wrap all;donate coffin};
	#5 {get all;donate all};
    #map run $donate_go_back;
    #if $bot[active] {#var bot[paused] 0;.bot};
};

#alias cooler_rotate {
	trace glyph;
	#%1 {deslab;get corpse;cooler corpse};
	trace glyph;
};

#NOP -- $corpse_string is a generic string to be inserted in your hpbar - added to the ticker for the tickhp;
#alias {_update_corpse_string} {
	#NOP -- If any of them happen to go negative, set to 0;
	#if {$corpses[coffin] < 0} {#var corpses[coffin] 0;};
	#if {$corpses[inventory] < 0} {#var corpses[inventory] 0;};
	#if {$corpses[smuggle] < 0} {#var corpses[smuggle] 0;};
	#if {$corpses[freezer]  < 0} {#var corpses[freezer] 0;};
	#if {$corpses[cooler] < 0} {#var corpses[cooler] 0;};
	#math corpses[total] {$corpses[coffin] + $corpses[inventory] + $corpses[smuggle] + $corpses[freezer] + $corpses[cooler] + $corpses[servant] + $corpses[golem]};
	#delay 0.1 {
		#var corpse_string <088>ðŸ’€ <ceb>$corpses[total] <088>Corpses ðŸ’€\n;
	    #if {$corpses[coffin] > 0} {#var corpse_string $corpse_string C: $corpses[coffin];};
		#if {$corpses[inventory] > 0} {#var corpse_string $corpse_string I: $corpses[inventory];};
		#if {$corpses[smuggle] > 0} {#var corpse_string $corpse_string S: $corpses[smuggle];};
		#if {$corpses[freezer] > 0} {#var corpses[freezer] @min{$corpses[freezer];50};#var corpse_string $corpse_string F: $corpses[freezer];};
		#if {$corpses[cooler] > 0} {#var corpses[cooler] @min{$corpses[cooler];50};#var corpse_string $corpse_string Clr: $corpses[cooler];};
		#if {$corpses[servant] > 0} {#var corpse_string $corpse_string Srv: $corpses[servant];};
		#if {$corpses[golem] > 0} {#var corpse_string $corpse_string Gol: $corpses[golem];};
		#if {"$draw[caption][active]" == "on"} {draw_caption_corpses};
	};
};

#NOP -- Corpse use order is:  Coffin -> Freezer -> Cooler -> Golem/Servant -> Inventory -> Smuggle;
#alias corpse_select {

	#if {$corpses[smuggle] && !$corpses[inventory] && !$corpses[coffin] && !$corpses[freezer] && !$corpses[golem] && !$corpses[servant]} {
		smuggle remove corpse;drop corpse;
	};
	#if $corpses[coffin] {
		unwrap;
	} {
		#if $corpses[freezer] {
			deslab;
		} {
			#if $corpses[cooler] {
				uncooler corpse;
			} {
				#if {$corpses[golem] || $corpses[servant]} {
					#if $corpses[golem] {
						golem drop corpse;
					} {
						=drop corpse;
					}
				} {
					unkeep corpse;drop corpse;
				};
			};
		};
	};
};

#NOP -- Necro alias to use # of corpses;
#alias {corpse_select_necro} {
	#format tmpCorpses {%d} {%1};
	#format tmpAbsorb {%d} {%1};
	#var tmpMoreCorpses 1;
	

	#if $debug_corpse_necro {#echo {Necro Corpse Select - Input is $tmpAbsorb}};

	#if {$corpses[inventory] > 0} {
		#if {$corpses[inventory] >= $tmpCorpses} {
			qtrance absorb>$tmpCorpses;
			#var tmpMoreCorpses 0;
			#var tmpAbsorb 0;
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Inventory: Using $tmpCorpses}};
		} {
			#math tmpAbsorb {$tmpAbsorb - $corpses[inventory]};
			#math tmpCorpses {$tmpCorpses - $corpses[inventory]};
			qtrance absorb>$corpses[inventory];
			
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Inventory 2: Using $corpses[inventory]}};
		};
	};

	#if $tmpMoreCorpses {
		#if {$corpses[coffin] > 0} {
			#if {$corpses[coffin] >= $tmpCorpses} {
				qtrance unwrap>$tmpCorpses;
				#var tmpMoreCorpses 0;
				#if $debug_corpse_necro {#echo {Necro Corpse Select - Coffin: Unwrapping $tmpCorpses}};
			} {
				#math tmpCorpses {$tmpCorpses - $corpses[coffin]};
				qtrance unwrap>$corpses[coffin];
				#if $debug_corpse_necro {#echo {Necro Corpse Select - Coffin 2: Unwrapping $corpses[coffin]}};
			};
		};
	};

	#if $tmpMoreCorpses {
		#if {$corpses[freezer] > 0 && $corpses[freezer] >= $tmpCorpses} {
			qtrance deslab>$tmpCorpses;
			#var tmpMoreCorpses 0;
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Freezer: Deslabbing $tmpCorpses}};
		} {
			#math tmpCorpses {$tmpCorpses - $corpses[freezer]};
			qtrance deslab>$corpses[freezer];
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Freezer 2: Deslabbing $corpses[freezer]}};
		};
	};

	#if $tmpMoreCorpses {
		#if {$corpses[cooler] > 0 && $corpses[cooler] >= $tmpCorpses} {
			qtrance uncooler corpse>$tmpCorpses;
			#var tmpMoreCorpses 0;
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Cooler: Dropping $tmpCorpses}};
		} {
			qtrance uncooler corpse>$corpses[cooler];
			#math tmpCorpses {$tmpCorpses - $corpses[cooler]};
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Cooler 2: Dropping $corpses[cooler]}};
		};
	};
	#action {eyes glow bright green upon the fallen} {
		#math tmpAbsorb {$tmpAbsorb + 1};
		#delay {corpseAbsorb} {
			#if $debug_corpse_necro {#echo {Necro Corpse Select - Duped Absorbing $tmpAbsorb}};
			qtrance absorb>$tmpAbsorb;
			#unaction {eyes glow bright green upon the fallen};
			#unvar tmpCorpses;
			#unvar tmpMoreCorpses;
			#unvar tmpAbsorb;
		} {1};
	} {1};

	#delay {corpseAbsorb} {
		#if $debug_corpse_necro {#echo {Necro Corpse Select - No Dupe Absorbing $tmpAbsorb}};
		qtrance absorb>$tmpAbsorb;
		#unaction {eyes glow bright green upon the fallen};
		#unvar tmpCorpses;
		#unvar tmpMoreCorpses;
		#unvar tmpAbsorb;
	} {4};

	#delay corpse_burn_toggle {
		#var corpse_burn 1;
		i;
	} {10};
};

#alias check_box {
	#var box_name %1;
	#act {^-i-A colossal box (empty).$} {
		#var corpses[$box_name] 0;
	} {2};
	#act {^-i-A colossal box (contains %d items).$} {
		#var corpses[$box_name] %%1;
	} {2};
	#delay .1 {#unact {^-i-A colossal box (contains %d items).$}};
	#delay .1 {#unact {^-i-A colossal box (empty).$}};
};

#NOP -- SOL Crypt Boxes;
#alias check_crypts {
	go sol_crypt1;
	#delay 1 {check_box sol_crypt1;look};
	#delay 3 {
		go sol_crypt2;
		#delay 1 {check_box sol_crypt2;look};
	};
};

#NOP -- Inventory overrides.  These actions update the count when you check inventory;
#action {%*Death's Freezer%*\(%d corpses\)} {
	#var inventory[freezer] 1;
	#if $corpse_check {
		#if {%3 != $corpses[freezer]} {
			#var corpses[freezer] %3;
		};
	};
} {2};

#NOP -- Normal Coffin
#action {%*An enchanted coffin (%d %*.$} {
	#var inventory[coffin] 1;
	#if $corpse_check {
		#if {%2 != $corpses[coffin]} {
			#var corpses[coffin] %2;
		};
	};
} {2};

#NOP -- Coffin for Necromancers;
#action {%*Coffin%s\[%s%d\/%s%d|%*%\|%s%dc]%*} {
	#var inventory[coffin] 1;
	#if $corpse_check {
		#if {%9 != $corpses[coffin]} {
			#var corpses[coffin] %9;
		};
	};
} {2};


#NOP -- Corpse Management COFFIN / FREEZER;
#act {the coffin's protective hold!$} {#math corpses[coffin] {$corpses[coffin] + 1}} {2};
#act {The coffin expels a corpse!} {#line gag;#echo {<169>Coffin: -1 corpse};#math corpses[coffin] {$corpses[coffin] - 1};} {2};
#act {The coffin expels all its corpses!} {#var corpses[coffin] 0;} {2};
#act {You picked up %d {corpse|corpses} into the coffin.} {#math corpses[coffin] {$corpses[coffin] + %1};} {2};
#act {There are no corpses in the coffin!} {#var corpses[coffin] 0;} {2};

#NOP -- max_coffin_capacity should just be set to MUD max. if you have lower, it will recalc at runtime;
#var corpses[coffin_max] 50;
#NOP -- We'll set this back to the absolute max, and when you get full message again it will reset to correct amount;
#action {^Your coffin flashes as its capacity expands!$} {#var corpses[coffin_max] 50} {2};
#action {^The coffin is full!$} {#var corpses[coffin_max] $corpses[coffin]} {2};
      

#NOP -- Deaths Freezer;
#act {frame causing it to get sucked in!$} {#line gag;#echo {<169>Freezer: +1 corpse};#math {corpses[freezer]} {$corpses[freezer] + 1};} {2};
#act {^You shake the frame and out drops} {#line gag;#echo {<169>Freezer: -1 corpse};#math {corpses[freezer]} {$corpses[freezer] - 1};} {2};
#act {^There are no corpses in the freezer!$} {#var corpses[freezer] 0} {2};
#act {^There is no reason to 'deslab' here.$} {#var corpses[freezer] 0;#var inventory[freezer] 0} {2};
#act {^There is no reason to 'slab' here.$} {#var corpses[freezer] 0;#var inventory[freezer] 0} {2};

#NOP -- Corpse Management Golem;
#action {The command is: inventory} {
	#var corpses[golem] 0;
	#var golem_update 1;
	#action {{(.*) corpse (.*)( \{([0-9]+)\})?}} {
		#if $golem_update {
			#var foundtimes 1;
			#regex {%%3} {7B{[0-9]+}} {#var foundtimes &1;};
			#math corpses[golem] {$corpses[golem] + $foundtimes};
		};
	} {2};
	#action {{(.*) remains (.*)( \{([0-9]+)\})?}} {
		#if $golem_update {
			#var foundtimes 1;
			#regex {%%3} {7B{[0-9]+}} {#var foundtimes &1;};
			#math corpses[golem] {$corpses[golem] + $foundtimes};
		};
	} {2};
} {5};

#action {Your packmule gives you %*corpse%*.} {
	#math corpses[golem] {$corpses[golem] - 1};
	#math corpses[inventory] {$corpses[inventory] + 1};
} {5};

#action {Your packmule gives you %*remains%*.} {
	#math corpses[golem] {$corpses[golem] - 1};
	#math corpses[inventory] {$corpses[inventory] + 1};
} {5};

#action {Estimated Capacity: %*} {
	#var golem_update 0;
	_update_corpse_string;
} {2};

#NOP -- Corpse Management Tech Ultra-Cooler;
#act {^You activate the Ultra-Cooler to retrieve} {#math corpses[cooler] {$corpses[cooler] - 1}} {2};
#act {^You place the %* into the Ultra-Cooler} {#math corpses[cooler] {$corpses[cooler] + 1};#math {corpses[inventory]} {$corpses[inventory] - 1}} {2};
#act {^There is no more room in the Ultra-Cooler} {#var corpses[cooler] 50} {2};
#act {^The cooler does not contain a corpse.$} {#var corpses[cooler] 0} {2};

#act {^The Tech Ultra-Cooler. It is large and oblong. The top of it is made} {
	#var inventory[cooler] 1;
	#var corpses[cooler] 0;
	#var cooler_update 1;
	#action {{^The (.*)(corpse|remains) of (.*)}} {
		#if $cooler_update {#math corpses[cooler] {$corpses[cooler] + 1}};
	} {6};
	#action {{(.*) a preserved, (.*) of (.*)}} {
		#if $cooler_update {#math corpses[cooler] {$corpses[cooler] + 1}};
	} {6};
	#delay 2 {
		#var cooler_update 0;
		#unaction {{^The (.*)(corpse|remains) of (.*)}};
		#unaction {{(.*) a preserved, (.*) of (.*)}};
	};
} {2};

#NOP -- Corpse Management INVENTORY;
#act {^You drop %* {corpse|remains}%*.$} {#math {corpses[inventory]} {$corpses[inventory] - 1};} {2};
#act {^%* {corpse|remains}%*: Taken.} {#math {corpses[inventory]} {$corpses[inventory] + 1};} {2};
#act {^%* {corpse|remains}%*: Ok.$} {#math {corpses[inventory]} {$corpses[inventory] + 1};} {2};
#act {^%s%+1..d %* {corpse|remains}%*.$} {#if $corpse_check {#math {corpses[inventory]} {$corpses[inventory] + %2};};} {2};
#act {%*Encumberance%s\[%s%d\/%s%d|%*%\|%s%dc]%*} {
	#if $corpse_check {
		#if {%9 != $corpses[inventory]} {
			#var corpses[inventory] %9;
		};
	};
	#var inventory[encumberance] %7;
} {2};

#NOP -- Corpse Management SMUGGLE;
#act {^Items you are currently smuggling (%*):} {
	#var smuggle_update 1;
	#var corpses[smuggle] 0;
	#action {{^>(.*)(corpse|remains)(.*)}} {
		#if $smuggle_update {#math corpses[smuggle] {$corpses[smuggle] + 1}};
	} {6};
	#delay 2 {
		#var smuggle_update 0;
	};
	
} {2};

#action {%*Smuggling%s\[%s%d\/%s%d|%*%\|%s%dc]%*} {
	#if $corpse_check {
		#if {%9 != $corpses[smuggle]} {
			#var corpses[smuggle] %9;
		};
	};
} {2};

#NOP -- Corpse Management PRIEST SERVANT;
#action {%* swirls slowly and reveals that it is carrying:$} {
	#var corpses[servant] 0;
	#act {^%s%d\)%* corpse%*.$} {#math {corpses[servant]} {$corpses[servant] + 1};};
	#delay 4 {#unact {^%s%d\)%* corpse%*.$}};
} {2};
#action {^You take %* corpse %* from ElBefe.$} {#math {corpses[servant]} {$corpses[servant] - 1};} {2};
#action {^Aerial servant takes: %* corpse.$} {#math {corpses[servant]} {$corpses[servant] + 1};} {2};
#action {^The aerial servant drops a %* corpse.$} {#math {corpses[servant]} {$corpses[servant] - 1};} {2};
#action {^Your aerial servant has no 'corpse' to drop.$} {#var corpses[servant] 0} {2};
#action {^The aerial servant drops everything.$} {#var corpses[servant] 0} {2};

#act {^> %* corpse%*} {#math {corpses[smuggle]} {$corpses[smuggle] + 1}} {2};
#act {^You smuggle away: %* corpse%*} {
	#math {corpses[smuggle]} {$corpses[smuggle] + 1};
	#math {corpses[inventory]} {$corpses[inventory] - 1};
} {2};

#act {^You unsmuggle: %* corpse%*} {
	#math {corpses[smuggle]} {$corpses[smuggle] - 1};
	#math {corpses[inventory]} {$corpses[inventory] + 1};
} {2};


#class {corpse_manager} {close}