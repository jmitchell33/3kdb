#class {player_aliases} {kill}
#class {player_aliases} {open}

#NOP -- alias to fire powers at start of combat
#alias {fire_powers} {#var Powers[startOfcombat] 0;psibleed;scythe;corrode;convoke ravens;call;rabies;con 100;#delay 1 #var Powers[startOfcombat] 1}

#NOP -- EXAMINING/SEARCHING
#alias {x} {exa}

#NOP -- ATTACKING
#alias {ass} {call;scythe;psibleed;corrode;convoke ravens;rabies}
#action {Brianna unloads one of his long range missiles and carefully removes} {load core;say CORE LOADED LET'R"RIP}
#action {You say: CORE LOADED LET'R"RIP} {ip1}
#action {The ghostly apparition then dissipates away.} {harvest;bloodarc}
#action {Brianna leaves by opening the door with a passcard and slipping} {swipe card}

#NOP -- QUICK WALKS TO ROOMVNUMS
#alias {wa} {#map run %1}

#NOP --KNIGHT TOUR GAMES
#alias {knight6} {play;start;dt6}
#alias {knight8} {play;start;dt8}
#alias {winaxe} {play;start;#5 throw axe}

#NOP --NUMPAD ON OR OFF
#alias {numoff} {#class numpad kill}
#alias {numon} {#read chars/brianna/numpad.tin}

#NOP -- BOT STATUS
#alias {3kdb} {ptell 3kdb bot status}

#alias sz_study {pause game;qtrance teleport/w/w/w/w/w/d/sw/sw/se/s/u/nw/nw/path/sw/sw/w;.study;qtrance e/ne/ne/path/se/se/d/n/nw/ne/ne/u/e/e/e/e/e/teleport 1;play;unpause game;};
#alias sz_reagents {pause game;qtrance teleport/w/w/w/w/w/u/w/w/w;topup;#delay 6 {qtrance e/e/e/d/e/e/e/e/e/teleport 1;play;unpause game;};}

#alias track_gxp {
    #var gxp_rounds 0;
    #if $tracking_gxp {
        #showme ----- NO LONGER TRACKING GXP -----;
        #send {repeat combat/off};
        #var tracking_gxp 0;
        #unact { |  Next Circle:[%*]};
        #unact {%*>-=-=-=-=-=-=-=-=-=-<*> Untouchable, Necromancer of the Golden White Robes <*>=-=-=-=-=-=-=-=-=-=-<%*};
        #unact {%*>-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-<%*};
    } {
        #showme ----- NOW TRACKING GXP -----;
        #send {repeat combat/gs};
        #list gxp_tracker clear;
        #unvar old_gxp;
        #unvar new_gxp;
        #act {%*>-=-=-=-=-=-=-=-=-=-<*> Untouchable, Necromancer of the Golden White Robes <*>=-=-=-=-=-=-=-=-=-=-<%*} {
            #line gag;
            #gag { |%*|};
        };
        #act {%*>-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-<%*} {
            #line gag;
            #ungag { |%*|};
        };
        #act { |  Next Circle:[%*]} {
            #var tracking_gxp 1;
            #if {$gxp_rounds == 0} {#var old_gxp %%1;#var gxp_rounds 1;} {
                #var new_gxp %%1;
                #math gxp_rounds {$gxp_rounds + 1};
                #if {$old_gxp != $new_gxp} {
                    #list gxp_tracker add {{{old_gxp} {$old_gxp} {new_gxp} {$new_gxp} {rounds} {$gxp_rounds}}};
                    #var gxp_rounds 0;
                };
            };
        };
    };
};

#alias gxp_stats {
    #var gxp_stats[avg] 0;
    #var gxp_stats[rounds] 0;
    #var gxp_stats[increases] 0;
    #foreach $gxp_tracker[%*] round {
        #math gxp_stats[increases] {$gxp_stats[increases] + 1};
        #math gxp_stats[rounds] {$gxp_stats[rounds] + $round[rounds]}
    };
    #math gxp_stats[avg_rounds_for_increase] {1.00 * $gxp_stats[rounds] / $gxp_stats[increases]};
    #showme {Each Increase Took On Average $gxp_stats[avg_rounds_for_increase] rounds};
};

#alias pg {
    qtrance preserve gemino remains>25;
    qtrance get preservation>25;
};

#alias corpse_dump2 {
    #path save forward path;
    #path get position pos;
    go sol_crypt2;
    unkeep all preservation;
    #30 {put preservation in box};
    #map run $go_back;
};

#alias .check_death {
    #if $darkness {light torch};
    #if $actions_stacked {
        do_action;
    } {
        #if {$inventory[freezer] && $corpses[freezer] < 50} {
            slab remains;
            slab remains;
        };
        #if {$inventory[coffin] && $corpses[coffin] < 28} {
            get remains;
            wrap remains;
            get remains;
            wrap remains;
        };
        #if {$inventory[encumberance] > 95} {
            #delay 1 {absorb;absorb;get all;.bot};
        } {
            #delay 1 {preserve_remains;preserve_remains;get all;put all in buckler;.bot};
        };
    };
    fuse 200;
};

#alias {_player_deadmanActions} {
    #if $bot[active] {.pause;#echo {paused from deadman}};
};

#alias {.mob_kill_action} {
    #if $bloodlet_area {
        bloodlet $mob[target];
        .botStep;
        #delay 1 {
            ..;
        };
    } {
        #var stats[starting-gp] $my[gp1][current];
        kill $mob[target];
        checkStrategy {snare} {snare $mob[target]};
        checkStrategy {rabies} {rabies};
        checkStrategy {corrode} {corrode};
        checkStrategy {psibleed} {psibleed};
        checkStrategy {scythe} {scythe};
        #if {$my[necromancer][maim] > 0} {checkStrategy {maim} {#if {$my[necromancer][altered]} {maim} {alter hybrid;maim}}};
        checkStrategy {call} {call};
        checkStrategy {undead} {=kill $mob[target]};
        #if {"$area" == "Banshee's Lair"} {
            #delay 3 {
                kill undead 2;
                snare undead 2;
                rabies undead 2;
                corrode undead 2;
            };
            #delay 6 {
                kill undead 3;
                snare undead 3;
                rabies undead 3;
                corrode undead 3;
            };
        };
    };
};

#NOP ---THIS SECTION IS FOR NECRO SPECIFIC ALIASES
#alias {ktrigg} {corpsetrig clear;corpsetrig preserve remains;corpsetrig get corpse;corpsetrig divvy gold;corpsetrig cooler corpse};
#alias {ein} {n;w;n;n;e;n;enter;e;e;e;s;s;e;e;n;n;n;trail;ne;climb gate;w;search tree;e;e;n;n;enter;unlock paperholder;turn paperholder;climb bench;stagedive;s;s;w;unlock door;open door;n};
#alias {sna} #ticker {snar} {snare mage} {3};
#alias {heal} #ticker {heals} {dream zzzzzzzzzzzzzzzzzzzzzzzzz} {2};
#alias {bas} #ticker {tbash} {bash} {3};
#alias {tstop} {#unticker tbash;#unticker snar;#unticker heals};
#alias {chestp} {retrieve;open chest;put all in chest;deliver};
#alias {peace} {sleep;.botStep};

#NOP ---NECRO TIER MSGS
#action {As you ponder the runes, they swim before your eyes.} {
    #echo {<feb>1 The path before you is a long and hard one name here<088>};
    #echo {<dfb>2 The path before you is a long one, barely have you begin your quest<088>};
    #echo {<fcb>3 A long road remains before you in the quest for ascension<088>};
    #echo {<ffa>4 A portion complete, your quest still awaits<088>};
    #echo {<acf>5 More remains of your path than what you have seen thus far<088>};
    #echo {<caf>6 The end of your journey is not yet in sight<088>};
    #echo {<fac>7 The road before you is long, but shorter than that behind you<088>};
    #echo {<afc>8 The end draws near, you sense this in your soul<088>};
    #echo {<cfa>9 Your goal draws closer than you realize<088>};
    #echo {<fca>10 There remains but only a few steps in your quest<088>};
    #echo {<feb>11 You are ready to ascend in the ranks<088>}};

#alias {set_ticker} {#NOP;};

#alias {go_donate} {
    #map get roomvnum donate_go_back;
    go necro_box;
    donate all;donate all from bag;donate all from bag 2;donate all from buckler;
    #if {$bot[corpse-run]} {
        go sol_crypt2;
        unkeep all preservation;
        put all in box;
        go sol_crypt1;
        put all in box;
    } {
        go morgue;
        donate coffin;donate all;
        go necro_corpse;
        store all;
    };
    #map run $donate_go_back;
};

#alias {go_donate2} {
    #map get roomvnum donate_go_back;
    keep all preservation;
    keep all component;
    go necro_box;
    donate all;donate all from bag;donate all from bag 2;donate all from buckler;
    #map run $donate_go_back;
};


#alias {go_study} {
    powers;gs;
    #map get roomvnum study_go_back;
    #if $auto_study_teleport {
        teleport;
        mapgo necro_rr;
        go necro_were;
    } {
        go necro_were;
    };
    #delay 2 {.study};
    #delay 3 {go necro_reagent};
    #delay 4 {topup};
    #delay 6 {#if {$my[coins] < 25} {go bank;withdraw 25000}};
    #delay 8 {
        #if $auto_study_teleport {
            teleport 1;
            #map goto $study_go_back;
        } {
            #map run $study_go_back;
        };
        powers;gs;
    };
    #delay 10 {
        #if $bot[active] {
            #var bot[paused] 0;
            ..;
        } {
            #NOP;
        };
    };
};

#alias {necro_reset} {
    #NOP;
};

#alias {gxp_tracker} {#act { |  Next Circle:[%*]%*]}  {#line log comms.txt %1}};


#class {player_aliases} {close}