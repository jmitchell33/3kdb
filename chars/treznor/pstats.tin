#class {pstats} kill
#class {pstats} open

#var pstatsCols 30;

#alias {pstatsColPrint} {
  #format pstatsCol1Length {%L} {%1};
  #format pstatsCol2Length {%L} {%3};
  #math pstatsCol1Length $pstatsCols - $pstatsCol1Length;
  #math pstatsCol2Length $pstatsCols - $pstatsCol2Length;
  #echo {%1<070>%+${pstatsCol1Length}s<070> || %3<070>%+${pstatsCol2Length}s} {%2} {%4};
};

#alias {pstatsFormatNumber} {
  #var pstatsNumberToFormat ${%1};
  #var pstatsNumberToFormatVar %1;
  #var pstatsFormattedNumber {};
  #foreach {1000;1000000;1000000000} {pstatsTmp} {
    #math pstatsNumMod $pstatsNumberToFormat / $pstatsTmp * 1.00;
    #if {$pstatsNumMod > 1} {
      #switch {$pstatsTmp} {
        #case 1000 #var pstatsFormattedNumber {${pstatsNumMod}<070>K<070>};
        #case 1000000 #var pstatsFormattedNumber {${pstatsNumMod}<170>M<070>};
        #case 1000000000 #var pstatsFormattedNumber {${pstatsNumMod}<120>B<070>};
      };
    };
  };
  #if {"$pstatsFormattedNumber" == ""} {#var pstatsFormattedNumber $pstatsNumberToFormat};
  #var $pstatsNumberToFormatVar $pstatsFormattedNumber;
  #var pstatsFormattedNumber {};
  #var pstatsNumberToFormatVar {};
};

#alias {pstatsHeaderPrint} {
  #var pstatsHeaderString %0;
  #var pstatsHeader {<100>-<260>-<060>[<160>[<170> ${pstatsHeaderString} <160>]<060>]<260>-<100>-};
  #format pstatsHeaderLength {%L} {$pstatsHeader};
  #math pstatsHeaderMid (($pstatsCols *2)+4+$pstatsHeaderLength)/2;
  #echo {%+${pstatsHeaderMid}s} {$pstatsHeader};

};

#action {^Player stats for this login:} {
  pstatsHeaderPrint Player Stats since Clear;
  #echo {};
  #line gag;
};

#action {^Damage Taken:%s{[0-9,]+}$} {
  #var pstatsDamageTaken %2;
  #line gag;
};

#action {^Combat Rounds:%s{[0-9,]+}$} {
  #var pstatsCRounds %2;
  #math pstatsCHours %2 / 1800;
  #math pstatsCMinutes %2 % 1800 / 30;
  #math pstatsCSeconds %2 % 1800 % 30 * 2; 
  #line gag;
  pstatsFormatNumber pstatsDamageTaken;
  pstatsColPrint {<110>Damage Taken<070>:} {<130>${pstatsDamageTaken}} {<130>Rounds<070>:} {<120>$pstatsCRounds<080> (<020>${pstatsCHours}<070>h<020>${pstatsCMinutes}<070>m<020>${pstatsCSeconds}<070>s)};
};

#action {^Overall Combat Rounds:%s{[0-9,]+}$} {#line gag;};

#action {^XP Gained:%s{[0-9,-]+}} {
  #var pstatsXpGained %2;
  #line gag;
};

#action {^GXP Gained:%s{[0-9,]+}$} {
  #var pstatsGxpGained %2;
  #math {averageGxpPerRound} {($pstatsGxpGained / $pstatsCRounds) * 1.00};
  #math {gxpGlvlIndicated} {$averageGxpPerRound * 2};
  pstatsColPrint {GXP Per Round:} {$averageGxpPerRound} {GXP GL Rate:} {$gxpGlvlIndicated};
  pstatsFormatNumber pstatsXpGained;
  pstatsFormatNumber pstatsGxpGained;
  pstatsColPrint {XP Gained:} {$pstatsXpGained} {GXP Gained:} {$pstatsGxpGained};
  #line gag;
};

#action {^XP Per Hour:%s{[0-9,-]+}} {
  #var pstatsXpRate %2;
  #line gag;
};

#action {^GXP Per Hour:%s{[0-9,]+}$} {
  #var pstatsGxpRate %2;
  pstatsFormatNumber pstatsXpRate;
  pstatsFormatNumber pstatsGxpRate;
  pstatsColPrint {XP Per Hour:} {$pstatsXpRate} {GXP Per Hour:} {$pstatsGxpRate};
  #line gag;
};

#action {^Total Kills:%s{[0-9,]+}} {
  #line gag;
  #var pstatsTotalKills %2;
};

#action {^XP Per Kill:%s{[0-9.,-]+}} {
  #var pstatsXpPerKill %2;
  #line gag;
};

#action {^GXP Per Kill:%s{[0-9,.]+}} {
  #var pstatsGxpPerKill %2;
  pstatsFormatNumber pstatsXpPerKill;
  pstatsFormatNumber pstatsGxpPerKill;
  pstatsColPrint {XP Per Kill:} {$pstatsXpPerKill} {GXP Per Kill:} {$pstatsGxpPerKill};
  #line gag;
};

#action {^Top %d kills:} {
  #line gag;
  #echo {};
  pstatsHeaderPrint Top %1 Kills;
  #action {^%s{[0-9,]+}%s: %+} {
    #var pstatsMobClass %%2;
    #var pstatsMobName %%4;
    #format pstatsMobName {%.${pstatsCols}s} {$pstatsMobName};
    pstatsFormatNumber pstatsMobClass;
    pstatsColPrint {} {<160>$pstatsMobName} {<060>$pstatsMobClass} {};
    #line gag;
  };
};

#action {^Last %d kills:} {
  #unaction {^%s%d%s: %+};
};

#action {^Last %d kills:} {
  #line gag;
  #echo {};
  pstatsHeaderPrint Last %1 Kills;
  #action {^%s{[0-9,]+}%s: %+} {
    #var pstatsMobClass %%2;
    #var pstatsMobName %%4;
    #format pstatsMobName {%.${pstatsCols}s} {$pstatsMobName};
    pstatsFormatNumber pstatsMobClass;
    pstatsColPrint {} {<160>$pstatsMobName} {<060>$pstatsMobClass} {};
    #line gag;
  };
};

#action {^Rooms Explored: %d} {
  #echo {};
  #unaction {^%s%d%s: %+};
  pstatsColPrint {<100>Rooms Explored:} {%1} {<100>Kills:} {$pstatsTotalKills};
  #line gag;
};

#class {pstats} close